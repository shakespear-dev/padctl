const std = @import("std");
const builtin = @import("builtin");
const posix = std.posix;
const linux = std.os.linux;

const device_instance_mod = @import("device_instance.zig");
const DeviceInstance = device_instance_mod.DeviceInstance;
const openDeviceWithRetry = device_instance_mod.openDeviceWithRetry;
const DeviceIO = @import("io/device_io.zig").DeviceIO;
const MappingConfig = @import("config/mapping.zig").MappingConfig;
const mapping_cfg = @import("config/mapping.zig");
const DeviceConfig = @import("config/device.zig").DeviceConfig;
const config_device = @import("config/device.zig");
const Mapper = @import("core/mapper.zig").Mapper;
const HidrawDevice = @import("io/hidraw.zig").HidrawDevice;
const readPhysicalPath = @import("io/hidraw.zig").readPhysicalPath;
const readInterfaceId = @import("io/hidraw.zig").readInterfaceId;
const netlink = @import("io/netlink.zig");
const shadow_grab = @import("io/shadow_grab.zig");
const ioctl = @import("io/ioctl_constants.zig");
const config_paths = @import("config/paths.zig");
const mapping_discovery = @import("config/mapping_discovery.zig");
const user_config_mod = @import("config/user_config.zig");
const chord_detector_mod = @import("core/chord_detector.zig");
const ControlSocket = @import("io/control_socket.zig").ControlSocket;
const control_socket = @import("io/control_socket.zig");

const socket_client = @import("cli/socket_client.zig");
const event_loop = @import("event_loop.zig");

const RebindDeviceOpener = *const fn (
    ctx: *anyopaque,
    allocator: std.mem.Allocator,
    iface: config_device.InterfaceConfig,
    vid: u16,
    pid: u16,
) anyerror!DeviceIO;

fn openDeviceWithRetryForRebind(
    _: *anyopaque,
    allocator: std.mem.Allocator,
    iface: config_device.InterfaceConfig,
    vid: u16,
    pid: u16,
) !DeviceIO {
    return openDeviceWithRetry(allocator, iface, vid, pid);
}

/// True when a client-supplied absolute mapping path resides directly in one of
/// the trusted mapping config dirs. Rejects arbitrary absolute paths so a local
/// caller on the world-reachable control socket cannot make the daemon parse
/// (and leak parse diagnostics from) files outside the mapping search path.
fn absolutePathInMappingDirs(path: []const u8, dirs: []const []const u8) bool {
    const parent = std.fs.path.dirname(path) orelse return false;
    for (dirs) |dir| {
        const trimmed = std.mem.trimRight(u8, dir, "/");
        if (std.mem.eql(u8, parent, trimmed)) return true;
    }
    return false;
}

/// One running device under Supervisor management.
pub const ManagedInstance = struct {
    phys_key: []const u8,
    devname: ?[]const u8, // null for statically-spawned; set by attach()
    instance: *DeviceInstance,
    thread: std.Thread,
    mapping_arena: std.heap.ArenaAllocator,
    switch_mapping: ?*mapping_cfg.ParseResult = null,
    switch_mapping_stem: ?[]const u8 = null,
    default_mapping_pr: ?*mapping_cfg.ParseResult = null,
    default_mapping_stem: ?[]const u8 = null,
    suspended: bool = false,
    /// CLOCK_MONOTONIC deadline (ns). When non-null and `now >= deadline`,
    /// `gcExpiredGrace()` tears the entry down. Set by `detach()` when
    /// `suspend_grace_sec > 0`; cleared on successful rebind.
    grace_deadline_ns: ?u64 = null,
    /// Grabbed shadow /dev/input/event* fds of kernel drivers (e.g. xpad)
    /// bound to this managed device (issue #406). Released on suspend and
    /// teardown; refilled by the sweep on spawn/resume.
    shadow_grabs: shadow_grab.GrabList = .{},
};

/// Config snapshot used for hot-reload diffing.
pub const ConfigEntry = struct {
    phys_key: []const u8,
    device_cfg: *const DeviceConfig,
    mapping_cfg: ?*MappingConfig,
};

const InotifyResult = struct {
    inotify_fd: posix.fd_t,
    debounce_fd: posix.fd_t,
    config_dir: ?[]const u8,
};

const SwitchTx = struct {
    idx: usize,
    new_mapper: ?Mapper,
    parsed_ptr: ?*mapping_cfg.ParseResult,
    path_stem: ?[]const u8 = null,
    old_mapper: ?Mapper = null,
    old_mapping_cfg: ?*const MappingConfig = null,
    old_switch_mapping: ?*mapping_cfg.ParseResult = null,
    old_switch_mapping_stem: ?[]const u8 = null,
    committed: bool = false,
};

threadlocal var test_fail_next_restart_managed: bool = false;

fn parseChordSwitchConfig(maybe_cfg: ?user_config_mod.ChordSwitchConfig) ?chord_detector_mod.Config {
    return chord_detector_mod.fromUserConfig(maybe_cfg);
}

fn shouldInjectSwitchFailure(self: *const Supervisor, commit_index: usize) bool {
    if (!builtin.is_test) return false;
    return self.test_switch_fail_commit_index != null and self.test_switch_fail_commit_index.? == commit_index;
}

fn initInotify(allocator: std.mem.Allocator) InotifyResult {
    const disabled: InotifyResult = .{ .inotify_fd = -1, .debounce_fd = -1, .config_dir = null };

    const config_dir = config_paths.userConfigDir(allocator) catch return disabled;

    std.fs.accessAbsolute(config_dir, .{}) catch {
        allocator.free(config_dir);
        return disabled;
    };

    const dir_z = allocator.dupeZ(u8, config_dir) catch {
        allocator.free(config_dir);
        return disabled;
    };
    defer allocator.free(dir_z);

    const rc_init = linux.inotify_init1(linux.IN.CLOEXEC | linux.IN.NONBLOCK);
    const init_err = linux.E.init(rc_init);
    if (init_err != .SUCCESS) {
        allocator.free(config_dir);
        return disabled;
    }
    const in_fd: posix.fd_t = @intCast(rc_init);

    const root_mask = linux.IN.CLOSE_WRITE | linux.IN.MOVED_TO | linux.IN.MOVED_FROM | linux.IN.DELETE | linux.IN.CREATE;
    const rc_watch = linux.inotify_add_watch(in_fd, dir_z.ptr, root_mask);
    const watch_err = linux.E.init(rc_watch);
    if (watch_err != .SUCCESS) {
        posix.close(in_fd);
        allocator.free(config_dir);
        return disabled;
    }

    const mappings_dir = std.fmt.allocPrint(allocator, "{s}/mappings", .{config_dir}) catch {
        posix.close(in_fd);
        allocator.free(config_dir);
        return disabled;
    };
    defer allocator.free(mappings_dir);
    const mappings_z = allocator.dupeZ(u8, mappings_dir) catch {
        posix.close(in_fd);
        allocator.free(config_dir);
        return disabled;
    };
    defer allocator.free(mappings_z);

    const map_mask = linux.IN.CLOSE_WRITE | linux.IN.MOVED_TO | linux.IN.MOVED_FROM | linux.IN.DELETE | linux.IN.CREATE;
    const rc_map_watch = linux.inotify_add_watch(in_fd, mappings_z.ptr, map_mask);
    const map_watch_err = linux.E.init(rc_map_watch);
    if (map_watch_err != .SUCCESS) {
        std.log.warn("inotify watch on {s} failed: {} (continuing with root watch only)", .{ mappings_dir, map_watch_err });
    }

    const db_fd = posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true }) catch {
        posix.close(in_fd);
        allocator.free(config_dir);
        return disabled;
    };

    return .{ .inotify_fd = in_fd, .debounce_fd = db_fd, .config_dir = config_dir };
}

fn threadEntry(inst: *DeviceInstance) void {
    inst.run() catch |err| {
        std.log.err("DeviceInstance.run failed: {}", .{err});
    };
}

const HotplugPending = struct {
    devname: [64]u8,
    len: u8,
    retries: u8,
    kind: PendingKind,
    // Absolute CLOCK_MONOTONIC deadline for this entry's next attempt. Each
    // entry rides its own backoff schedule so a shared timer firing for the
    // nearest entry never collapses a slower entry's window.
    next_deadline_ns: u64,
};

const PendingKind = enum { hidraw, input_grab };

// Per-attempt delay (ms) for the event-driven hotplug attach retry. The array
// length is the give-up cap; the cumulative window (~7.6s) covers a cold-boot /
// upgrade transient where udev permissions, kernel driver bind, or firmware are
// not yet settled and a single ADD uevent must not be dropped permanently.
const HOTPLUG_RETRY_BACKOFF_MS = [_]u32{ 300, 300, 500, 800, 1200, 1500, 1500, 1500 };

// 8 fixed (stop, hup, netlink, inotify, debounce, hotplug_retry, grace, liveness) + 1 listen + 4 clients.
pub const SUPERVISOR_MAX_FDS: usize = 8 + 1 + 4;

/// Type-erased binding to the active dispatch's reload strategy. `ctx` points at
/// the dispatch value living on `serveLoop`'s stack for the loop's duration.
const ReloadHook = struct {
    ctx: *const anyopaque,
    call: *const fn (ctx: *const anyopaque, sup: *Supervisor) void,
};

pub const Supervisor = struct {
    allocator: std.mem.Allocator,
    managed: std.ArrayList(ManagedInstance),
    stop_fd: posix.fd_t,
    hup_fd: posix.fd_t,
    netlink_fd: posix.fd_t,
    inotify_fd: posix.fd_t,
    debounce_fd: posix.fd_t,
    hotplug_retry_fd: posix.fd_t,
    hotplug_pending: std.ArrayList(HotplugPending),
    // Set by attachWithRoot when an open failed because the hidraw node already
    // vanished (libusb claim removed it, or the device unplugged); lets the
    // retry give-up log at debug instead of a misleading warning.
    last_attach_node_gone: bool = false,
    config_dir: ?[]const u8,
    // ParseResults whose DeviceConfig is referenced by at least one managed instance.
    configs: std.ArrayList(*config_device.ParseResult),
    // devname → phys_key (both slices owned by this map)
    devname_map: std.StringHashMap([]const u8),
    ctrl_sock: ?ControlSocket,
    user_cfg: ?user_config_mod.ParseResult = null,
    test_switch_mapping_override: ?[]const u8 = null,
    test_default_mapping_override: ?[]const u8 = null,
    test_switch_fail_commit_index: ?usize = null,
    /// `detach()` preserves the virtual uinput for this many seconds to survive
    /// wireless sleep/wake. Once the deadline passes without a matching ADD,
    /// `gcExpiredGrace()` fully tears the entry down so the uinput fd is not
    /// leaked indefinitely (zombie device). Set to 0 to tear down immediately.
    suspend_grace_sec: u32 = 15,
    /// Timerfd that fires when a grace deadline comes due. Armed by
    /// `detach()`; drained by `drainGraceTimer()`. -1 = unavailable
    /// (e.g. `initForTest`); callers must call `gcExpiredGrace()` directly.
    grace_timer_fd: posix.fd_t = -1,
    /// Recurring timerfd (1s) that sweeps managed libusb-backed instances for a
    /// real physical unplug. These instances never receive a hidraw REMOVE that
    /// means "unplug" (their hidraw node was deleted by padctl's own claim), so
    /// liveness is probed via the UsbrawDevice pipe fd instead. -1 = unavailable
    /// (e.g. `initForTest`).
    liveness_timer_fd: posix.fd_t = -1,
    /// Test-only clock override (ns). When non-null, `nowNs()` returns
    /// this value instead of reading CLOCK_MONOTONIC. Production paths
    /// leave this null.
    test_now_override_ns: ?u64 = null,
    /// Test-only switch: when true, `finalizeRebind()` short-circuits
    /// with `error.TestInjectedRestartFailure` just before calling
    /// `restartManagedThread`. Used by regression tests to exercise the
    /// "restart failed after bookkeeping" branch without racing real
    /// thread creation.
    test_fail_rebind_restart: bool = false,
    /// Test-only seam: when non-null, `attachWithRoot()` returns
    /// `error.HotplugTransient` and decrements this counter on each call until
    /// it reaches zero, then attaches normally. Lets retry-budget tests drive a
    /// device that is transient for a controllable number of consecutive
    /// retries without touching real device nodes.
    test_attach_transient_remaining: ?usize = null,
    /// Daemon-wide fallback counter for UHID uniq strings when `phys_key` is
    /// null (virtual / Bluetooth devices without sysfs phys). Passed by pointer
    /// into every `DeviceInstance.init` call site; see `src/io/uniq.zig`.
    daemon_uniq_counter: u16 = 1,
    /// Parsed `[chord_switch]` from user_cfg, derived once at init/reload.
    /// null = feature disabled. Installed onto every newly initialised Mapper
    /// via `installChordDetector`.
    chord_detector_cfg: ?chord_detector_mod.Config = null,
    /// True when PADCTL_TRACE_LIFECYCLE=1 at startup. Cached once so hot paths
    /// pay only a bool branch, not a getenv call per event.
    trace_lifecycle: bool = false,
    /// Reload trigger bound by `serveLoop` to the active dispatch strategy, so a
    /// RELOAD control-socket command runs the same path as SIGHUP. null until the
    /// loop installs it.
    reload_hook: ?ReloadHook = null,
    /// First config that failed to parse during the in-flight reload. The reload
    /// path swallows per-file errors (a bad file just drops its device), so this
    /// records the first failure for `handleReload` to surface as `ERR <file>:
    /// <reason>` instead of a misleading `OK`. Cleared before each reload.
    reload_failure: ?ReloadFailure = null,

    /// First failing config of a reload, kept in inline buffers so the socket
    /// reply path needs no allocation. `recordReloadFailure` fills it once.
    pub const ReloadFailure = struct {
        file_buf: [160]u8 = undefined,
        file_len: usize = 0,
        reason_buf: [80]u8 = undefined,
        reason_len: usize = 0,

        fn file(self: *const ReloadFailure) []const u8 {
            return self.file_buf[0..self.file_len];
        }
        fn reason(self: *const ReloadFailure) []const u8 {
            return self.reason_buf[0..self.reason_len];
        }
    };

    /// Record the first config that failed to parse during a reload. Later
    /// failures are ignored so the reply names the first broken file.
    fn recordReloadFailure(self: *Supervisor, file_path: []const u8, err: anyerror) void {
        if (self.reload_failure != null) return;
        var f: ReloadFailure = .{};
        const fc = @min(file_path.len, f.file_buf.len);
        @memcpy(f.file_buf[0..fc], file_path[0..fc]);
        f.file_len = fc;
        const name = @errorName(err);
        const rc = @min(name.len, f.reason_buf.len);
        @memcpy(f.reason_buf[0..rc], name[0..rc]);
        f.reason_len = rc;
        self.reload_failure = f;
    }

    /// Emit a [lifecycle] info line when trace_lifecycle is enabled.
    fn traceLifecycle(self: *const Supervisor, comptime fmt: []const u8, args: anytype) void {
        if (!self.trace_lifecycle) return;
        std.log.info("[lifecycle] " ++ fmt, args);
    }

    pub fn init(allocator: std.mem.Allocator) !Supervisor {
        var stop_mask = posix.sigemptyset();
        posix.sigaddset(&stop_mask, linux.SIG.TERM);
        posix.sigaddset(&stop_mask, linux.SIG.INT);
        posix.sigprocmask(linux.SIG.BLOCK, &stop_mask, null);
        const stop_fd = try posix.signalfd(-1, &stop_mask, 0);
        errdefer posix.close(stop_fd);

        var hup_mask = posix.sigemptyset();
        posix.sigaddset(&hup_mask, linux.SIG.HUP);
        posix.sigprocmask(linux.SIG.BLOCK, &hup_mask, null);
        const hup_fd = try posix.signalfd(-1, &hup_mask, 0);
        errdefer posix.close(hup_fd);

        const nl_fd = netlink.openNetlinkUevent() catch |err| blk: {
            std.log.warn("netlink unavailable: {}", .{err});
            break :blk -1;
        };
        errdefer if (nl_fd >= 0) posix.close(nl_fd);

        const retry_fd = posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true }) catch blk: {
            std.log.warn("hotplug retry timer unavailable", .{});
            break :blk -1;
        };
        errdefer if (retry_fd >= 0) posix.close(retry_fd);

        // Timerfd that fires when a suspended instance's grace window expires
        // so the main loop can tear the uinput down.
        const grace_fd = posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true }) catch blk: {
            std.log.warn("suspend grace timer unavailable", .{});
            break :blk -1;
        };
        errdefer if (grace_fd >= 0) posix.close(grace_fd);

        // Recurring 1s liveness sweep for libusb-backed instances.
        const liveness_fd = posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true }) catch blk: {
            std.log.warn("liveness sweep timer unavailable", .{});
            break :blk -1;
        };
        errdefer if (liveness_fd >= 0) posix.close(liveness_fd);
        if (liveness_fd >= 0) {
            const spec = linux.itimerspec{
                .it_value = .{ .sec = 1, .nsec = 0 },
                .it_interval = .{ .sec = 1, .nsec = 0 },
            };
            _ = linux.timerfd_settime(liveness_fd, .{}, &spec, null);
        }

        const inotify_result = initInotify(allocator);

        var sock_path_buf: [256]u8 = undefined;
        const sock_path = socket_client.resolveSocketPath(&sock_path_buf);
        const sock = ControlSocket.init(allocator, sock_path) catch |err| blk: {
            if (err == error.AlreadyRunning) return err;
            std.log.warn("control socket unavailable: {}", .{err});
            break :blk null;
        };

        const trace_on = std.mem.eql(u8, std.posix.getenv("PADCTL_TRACE_LIFECYCLE") orelse "", "1");
        var sup: Supervisor = .{
            .allocator = allocator,
            .managed = .{},
            .stop_fd = stop_fd,
            .hup_fd = hup_fd,
            .netlink_fd = nl_fd,
            .inotify_fd = inotify_result.inotify_fd,
            .debounce_fd = inotify_result.debounce_fd,
            .hotplug_retry_fd = retry_fd,
            .hotplug_pending = .{},
            .config_dir = inotify_result.config_dir,
            .configs = .{},
            .devname_map = std.StringHashMap([]const u8).init(allocator),
            .ctrl_sock = sock,
            .user_cfg = user_config_mod.load(allocator),
            .test_switch_mapping_override = null,
            .test_switch_fail_commit_index = null,
            .grace_timer_fd = grace_fd,
            .liveness_timer_fd = liveness_fd,
            .trace_lifecycle = trace_on,
        };
        sup.applyUserConfigRuntime();
        if (trace_on) {
            std.log.info("[lifecycle] trace enabled suspend_grace_sec={d}", .{sup.suspend_grace_sec});
        }
        return sup;
    }

    /// Apply runtime tunables from `user_cfg` onto `self`. Called after
    /// `init()` loads the config and again from `doReload()` so live
    /// `padctl reload` picks up edits. No-op when `user_cfg` is null.
    fn applyUserConfigRuntime(self: *Supervisor) void {
        self.chord_detector_cfg = null;
        const uc = (self.user_cfg orelse return).value;
        const raw = uc.supervisor.suspend_grace_sec;
        self.suspend_grace_sec = if (raw <= 0)
            0
        else if (raw > @as(i64, std.math.maxInt(u32)))
            std.math.maxInt(u32)
        else
            @intCast(raw);
        self.chord_detector_cfg = parseChordSwitchConfig(uc.chord_switch);
    }

    fn installChordDetector(self: *Supervisor, m: *ManagedInstance) void {
        const cfg = self.chord_detector_cfg orelse return;
        if (m.instance.mapper) |*mp| mp.setChordDetector(cfg);
    }

    pub fn initForTest(allocator: std.mem.Allocator) !Supervisor {
        const stop_fd = try posix.eventfd(0, ioctl.EFD_CLOEXEC | ioctl.EFD_NONBLOCK);
        errdefer posix.close(stop_fd);
        const hup_fd = try posix.eventfd(0, ioctl.EFD_CLOEXEC | ioctl.EFD_NONBLOCK);
        errdefer posix.close(hup_fd);
        return .{
            .allocator = allocator,
            .managed = .{},
            .stop_fd = stop_fd,
            .hup_fd = hup_fd,
            .netlink_fd = -1,
            .inotify_fd = -1,
            .debounce_fd = -1,
            .hotplug_retry_fd = -1,
            .hotplug_pending = .{},
            .config_dir = null,
            .configs = .{},
            .devname_map = std.StringHashMap([]const u8).init(allocator),
            .ctrl_sock = null,
            .test_switch_mapping_override = null,
            .test_switch_fail_commit_index = null,
        };
    }

    pub fn deinit(self: *Supervisor) void {
        if (self.ctrl_sock) |*cs| cs.deinit();
        if (self.user_cfg) |*uc| uc.deinit();
        if (self.test_switch_mapping_override) |p| self.allocator.free(p);
        if (self.test_default_mapping_override) |p| self.allocator.free(p);
        posix.close(self.stop_fd);
        posix.close(self.hup_fd);
        if (self.netlink_fd >= 0) posix.close(self.netlink_fd);
        if (self.inotify_fd >= 0) posix.close(self.inotify_fd);
        if (self.debounce_fd >= 0) posix.close(self.debounce_fd);
        if (self.hotplug_retry_fd >= 0) posix.close(self.hotplug_retry_fd);
        if (self.grace_timer_fd >= 0) posix.close(self.grace_timer_fd);
        if (self.liveness_timer_fd >= 0) posix.close(self.liveness_timer_fd);
        self.hotplug_pending.deinit(self.allocator);
        if (self.config_dir) |dir| self.allocator.free(dir);
        if (self.managed.items.len > 0) self.stopAll();
        self.managed.deinit(self.allocator);
        for (self.configs.items) |c| {
            c.deinit();
            self.allocator.destroy(c);
        }
        self.configs.deinit(self.allocator);
        var it = self.devname_map.iterator();
        while (it.next()) |entry| {
            self.allocator.free(entry.key_ptr.*);
            self.allocator.free(entry.value_ptr.*);
        }
        self.devname_map.deinit();
    }

    fn hasLiveDuplicate(self: *Supervisor, devname: []const u8, phys_key: []const u8, cfg: *const DeviceConfig) bool {
        if (self.devname_map.contains(devname)) return true;
        for (self.managed.items) |m| {
            if (!std.mem.eql(u8, m.phys_key, phys_key)) continue;
            if (m.instance.device_cfg.device.vid != cfg.device.vid or
                m.instance.device_cfg.device.pid != cfg.device.pid)
            {
                continue;
            }
            if (m.suspended) continue;
            if (managedInstanceAlive(&m)) return true;
        }
        return false;
    }

    pub fn attachWithInstanceResult(self: *Supervisor, devname: []const u8, phys_key: []const u8, instance: *DeviceInstance, default_pr: ?*mapping_cfg.ParseResult) !bool {
        if (self.devname_map.contains(devname)) return false;
        // issue #236: forfeit grace for stale suspended entries sharing this
        // device's VID:PID but parked under a different phys_key. The grace
        // window's contract is "same controller will return at the same
        // physical topology"; once a fresh ADD arrives for the same VID:PID at
        // a different phys (dongle re-enumeration, USB renumber, PR #304's
        // cold-scan retry replay after boot), the old uinput is stale — and
        // leaving it alive in parallel with the new one causes consumers
        // (Steam) to keep reading inputs from the now-dead device.
        self.pruneStaleSuspendedForId(
            @intCast(instance.device_cfg.device.vid),
            @intCast(instance.device_cfg.device.pid),
            phys_key,
        );
        // Dedup by phys_key. Race guard: when a controller is unplugged and
        // replugged within the detach-delay window (or a USB re-enumeration
        // skips the REMOVE uevent entirely), an ADD can reach this path while
        // a managed, not-yet-suspended instance still holds the same phys_key.
        // Silently returning in that case leaves the entry stuck on dead hidraw
        // fds and produces no input until `padctl reload`. Probe the backing fd
        // liveness; if the fd is dead force-detach the stale entry and fall
        // through to a fresh attach.
        var stale_devname_buf: [64]u8 = undefined;
        var stale_devname: ?[]const u8 = null;
        var i: usize = 0;
        while (i < self.managed.items.len) : (i += 1) {
            const m = &self.managed.items[i];
            if (!std.mem.eql(u8, m.phys_key, phys_key)) continue;
            const same_device_id =
                m.instance.device_cfg.device.vid == instance.device_cfg.device.vid and
                m.instance.device_cfg.device.pid == instance.device_cfg.device.pid;
            if (m.suspended and !same_device_id) {
                std.log.info("hotplug: replacing suspended {x:0>4}:{x:0>4} at phys \"{s}\" with {x:0>4}:{x:0>4}", .{
                    @as(u16, @intCast(m.instance.device_cfg.device.vid)),
                    @as(u16, @intCast(m.instance.device_cfg.device.pid)),
                    phys_key,
                    @as(u16, @intCast(instance.device_cfg.device.vid)),
                    @as(u16, @intCast(instance.device_cfg.device.pid)),
                });
                self.teardownManaged(m);
                _ = self.managed.swapRemove(i);
                break;
            }
            if (managedInstanceAlive(m)) return false;
            // Dead fds. Capture devname for detachFull (detach frees it).
            if (m.devname) |dn| {
                const n = @min(dn.len, stale_devname_buf.len);
                @memcpy(stale_devname_buf[0..n], dn[0..n]);
                stale_devname = stale_devname_buf[0..n];
            } else {
                // Orphan entry (static-spawn or hotplug allocation-failure
                // edge): phys_key matches but devname is null, so we cannot
                // force-detach via the devname_map path. Preserve the
                // original silent-return dedup invariant — falling through
                // would spawn a second ManagedInstance with the same
                // phys_key.
                return false;
            }
            break;
        }
        if (stale_devname) |dn| {
            std.log.info("hotplug: stale entry for phys \"{s}\" has dead fds; force-detaching {s}", .{ phys_key, dn });
            self.detachFull(dn);
        }
        // All fallible bookkeeping must run BEFORE spawnInstance commits
        // the managed item — once spawnInstance succeeds, returning an error
        // would leave a dangling instance in self.managed for the caller's
        // catch-and-destroy path to UAF on. m.devname must also be a separate
        // allocation from the map key — teardownManaged frees both independently.
        const dev_copy = try self.allocator.dupe(u8, devname);
        errdefer self.allocator.free(dev_copy);
        const phys_copy = try self.allocator.dupe(u8, phys_key);
        errdefer self.allocator.free(phys_copy);
        const dn_copy = try self.allocator.dupe(u8, devname);
        errdefer self.allocator.free(dn_copy);
        try self.devname_map.put(dev_copy, phys_copy);
        errdefer _ = self.devname_map.fetchRemove(dev_copy);
        try self.spawnInstance(phys_key, instance, default_pr);
        self.managed.items[self.managed.items.len - 1].devname = dn_copy;
        return true;
    }

    /// Attach a pre-constructed instance under a given devname / phys_key.
    /// Returns without error if devname already tracked (dedup guard).
    /// Ownership of default_pr (if non-null) transfers to ManagedInstance.
    pub fn attachWithInstance(self: *Supervisor, devname: []const u8, phys_key: []const u8, instance: *DeviceInstance, default_pr: ?*mapping_cfg.ParseResult) !void {
        _ = try self.attachWithInstanceResult(devname, phys_key, instance, default_pr);
    }

    /// Stop and suspend the instance attached under devname. The uinput fds
    /// stay open so consumers (e.g. Steam) keep their cached eventN
    /// references. On reconnect, attachWithRoot() rebinds the new hidraw fds
    /// to the existing instance.
    ///
    /// When `suspend_grace_sec > 0` the entry keeps its uinput but is marked
    /// with a `grace_deadline_ns`; if no matching ADD arrives before the
    /// deadline, `gcExpiredGrace()` tears it down so the virtual gamepad does
    /// not linger forever after a permanent disconnect. When `suspend_grace_sec
    /// == 0` the entry is torn down immediately.
    pub fn detach(self: *Supervisor, devname: []const u8) void {
        if (self.suspend_grace_sec == 0) {
            // Grace window disabled: fall through to full teardown so the
            // uinput fd is released alongside the hidraw handle.
            self.detachFull(devname);
            return;
        }

        // Peek first: a libusb-backed instance must keep its devname binding so
        // it stays addressable. The hidraw REMOVE that triggered this detach is
        // caused by padctl's own libusb claim deleting the node, not a physical
        // unplug; real unplug for these instances is detected by the liveness
        // sweep over the UsbrawDevice pipe fd.
        const peek = self.devname_map.get(devname) orelse {
            std.log.debug("detach: {s} not in devname_map", .{devname});
            return;
        };
        for (self.managed.items) |*m| {
            if (!std.mem.eql(u8, m.phys_key, peek)) continue;
            if (instanceHoldsLibusb(m)) {
                std.log.debug("detach: {s} holds libusb; ignoring hidraw REMOVE", .{devname});
                return;
            }
            break;
        }

        const entry = self.devname_map.fetchRemove(devname) orelse {
            std.log.debug("detach: {s} not in devname_map", .{devname});
            return;
        };
        self.allocator.free(entry.key);
        const phys_key = entry.value;
        defer self.allocator.free(phys_key);

        for (self.managed.items) |*m| {
            if (!std.mem.eql(u8, m.phys_key, phys_key)) continue;
            std.log.info("device suspended: \"{s}\" {s} (grace {d}s)", .{ m.instance.device_cfg.device.name, devname, self.suspend_grace_sec });
            m.instance.stop();
            m.thread.join();
            m.instance.quiesceOutputs(.{ .reset_input_state = true, .reset_mapper_state = true });
            m.instance.closeDeviceIO();
            m.shadow_grabs.releaseAll();
            m.suspended = true;
            // Schedule grace deadline. Saturating add keeps us safe against
            // pathological `suspend_grace_sec` values.
            const now = self.nowNs();
            const grace_ns: u64 = @as(u64, self.suspend_grace_sec) *| std.time.ns_per_s;
            m.grace_deadline_ns = now +| grace_ns;
            self.armGraceTimer();
            if (m.devname) |dn| {
                self.allocator.free(dn);
                m.devname = null;
            }
            return;
        }
        std.log.debug("detach: no managed instance for phys {s}", .{phys_key});
    }

    /// Current CLOCK_MONOTONIC in nanoseconds, honouring `test_now_override_ns`
    /// when set. All issue-#131-A grace deadline arithmetic goes through
    /// this helper so tests can drive the clock deterministically.
    pub fn nowNs(self: *const Supervisor) u64 {
        if (self.test_now_override_ns) |t| return t;
        return @intCast(@max(0, event_loop.monotonicNs()));
    }

    /// Clear the grace deadline on a managed entry — called by the rebind
    /// path in `attachWithRoot()` when a matching ADD arrives before the
    /// deadline. Public for tests.
    pub fn clearGraceDeadline(_: *Supervisor, m: *ManagedInstance) void {
        m.grace_deadline_ns = null;
    }

    /// Iterate the managed list and tear down any suspended entry whose
    /// grace deadline has passed. Called by the serve loop on `grace_timer_fd`
    /// fire and exposed to tests for deterministic exercise.
    pub fn gcExpiredGrace(self: *Supervisor, now_ns: u64) void {
        var i: usize = self.managed.items.len;
        while (i > 0) {
            i -= 1;
            const m = &self.managed.items[i];
            const deadline = m.grace_deadline_ns orelse continue;
            if (now_ns < deadline) continue;
            std.log.info("grace window expired for phys \"{s}\"; tearing down uinput", .{m.phys_key});
            self.traceLifecycle("gc_teardown model={s} phys={s} reason=grace_expired", .{
                m.instance.device_cfg.device.name,
                m.phys_key,
            });
            // `detach()` has already joined the worker thread and closed
            // the hidraw fds, so `teardownManaged` just frees the uinput
            // + bookkeeping.
            self.teardownManaged(m);
            _ = self.managed.swapRemove(i);
        }
        self.armGraceTimer();
    }

    /// Drain `liveness_timer_fd` and sweep libusb-backed instances. Called by
    /// the serve loop on the recurring 1s fire.
    fn drainLivenessTimer(self: *Supervisor) void {
        if (self.liveness_timer_fd < 0) return;
        var tbuf: [8]u8 = undefined;
        _ = posix.read(self.liveness_timer_fd, &tbuf) catch {};
        self.sweepLivenessLibusb();
    }

    /// Tear down any managed libusb-backed instance whose backing device fd has
    /// hung up (physical unplug). Pure hid-class instances are left to the
    /// hidraw REMOVE + grace path and are not touched here. Exposed for tests.
    pub fn sweepLivenessLibusb(self: *Supervisor) void {
        var i: usize = self.managed.items.len;
        while (i > 0) {
            i -= 1;
            const m = &self.managed.items[i];
            if (!instanceHoldsLibusb(m)) continue;
            if (m.suspended) continue;
            // No read fd to probe for POLLHUP — unplug detection here is out of scope.
            if (m.instance.devices.len == 0) continue;
            if (managedInstanceAlive(m)) continue;
            if (m.devname) |devname| {
                std.log.info("device unplugged: \"{s}\" {s}; tearing down", .{ m.instance.device_cfg.device.name, devname });
                self.detachFull(devname);
            } else {
                std.log.info("device unplugged: \"{s}\" phys=\"{s}\"; tearing down", .{
                    m.instance.device_cfg.device.name,
                    m.phys_key,
                });
                m.instance.stop();
                m.thread.join();
                m.instance.quiesceOutputs(.{ .reset_input_state = true, .reset_mapper_state = true });
                self.teardownManaged(m);
                _ = self.managed.swapRemove(i);
            }
        }
    }

    /// Arm the grace timerfd to fire at the soonest pending deadline. If
    /// no entries are pending, the timer is disarmed. A no-op when
    /// `grace_timer_fd < 0` (e.g. `initForTest`).
    fn armGraceTimer(self: *Supervisor) void {
        if (self.grace_timer_fd < 0) return;
        var soonest: ?u64 = null;
        const now_arm = self.nowNs();
        for (self.managed.items) |*m| {
            const d = m.grace_deadline_ns orelse continue;
            if (soonest == null or d < soonest.?) soonest = d;
            const remaining_ms: u64 = if (d > now_arm) (d - now_arm) / std.time.ns_per_ms else 0;
            self.traceLifecycle("arm_grace model={s} phys={s} deadline_ms={d} grace_sec={d}", .{
                m.instance.device_cfg.device.name,
                m.phys_key,
                remaining_ms,
                self.suspend_grace_sec,
            });
        }
        const disarm = linux.itimerspec{
            .it_value = .{ .sec = 0, .nsec = 0 },
            .it_interval = .{ .sec = 0, .nsec = 0 },
        };
        if (soonest == null) {
            _ = linux.timerfd_settime(self.grace_timer_fd, .{}, &disarm, null);
            return;
        }
        const now = self.nowNs();
        const target = soonest.?;
        // Minimum 1 ms so a deadline that is already in the past still
        // triggers a timer fire (itimerspec with zero values disarms).
        const delay_ns: u64 = if (target > now) target - now else 1_000_000;
        const sec: i64 = @intCast(delay_ns / std.time.ns_per_s);
        const nsec: i64 = @intCast(delay_ns % std.time.ns_per_s);
        const spec = linux.itimerspec{
            .it_value = .{ .sec = sec, .nsec = nsec },
            .it_interval = .{ .sec = 0, .nsec = 0 },
        };
        _ = linux.timerfd_settime(self.grace_timer_fd, .{}, &spec, null);
    }

    /// Drain `grace_timer_fd` and run expiry GC. Called by the serve loop.
    fn drainGraceTimer(self: *Supervisor) void {
        if (self.grace_timer_fd < 0) return;
        var tbuf: [8]u8 = undefined;
        _ = posix.read(self.grace_timer_fd, &tbuf) catch {};
        self.gcExpiredGrace(self.nowNs());
    }

    /// Unconditionally tear down and remove a managed instance (used by
    /// stopAll/reload — not by hotplug detach).
    pub fn detachFull(self: *Supervisor, devname: []const u8) void {
        const entry = self.devname_map.fetchRemove(devname) orelse return;
        self.allocator.free(entry.key);
        const phys_key = entry.value;
        defer self.allocator.free(phys_key);

        var i: usize = self.managed.items.len;
        while (i > 0) {
            i -= 1;
            const m = &self.managed.items[i];
            if (std.mem.eql(u8, m.phys_key, phys_key)) {
                m.instance.stop();
                m.thread.join();
                m.instance.quiesceOutputs(.{ .reset_input_state = true, .reset_mapper_state = true });
                self.teardownManaged(m);
                _ = self.managed.swapRemove(i);
                return;
            }
        }
    }

    /// True when the instance owns the physical device through libusb rather
    /// than through a kernel hidraw node: it claims a suppress-only interface
    /// or reads a vendor-class interface. Such an instance must not be torn
    /// down on a hidraw REMOVE uevent — the REMOVE is a side effect of padctl's
    /// own claim deleting the hidraw node, not a physical unplug.
    fn instanceHoldsLibusb(m: *const ManagedInstance) bool {
        if (m.instance.suppress_devs.len > 0) return true;
        for (m.instance.device_cfg.device.interface) |iface| {
            if (config_device.isSuppressClass(iface.class)) continue;
            if (std.mem.eql(u8, iface.class, "vendor")) return true;
        }
        return false;
    }

    /// Probe whether the backing device fds of `m` are still alive.
    /// Returns false when the primary device fd has been invalidated (EBADF)
    /// or the other end has hung up (POLLHUP / POLLERR / POLLNVAL), which
    /// is what happens after USB removal even if `detach()` has not yet
    /// run. Used by `attachWithInstance` to distinguish a genuine dedup
    /// collision from a race where the ADD uevent for a replug arrives
    /// before REMOVE drains.
    fn managedInstanceAlive(m: *const ManagedInstance) bool {
        // A suspended instance is an explicit, structured state: fds are
        // already closed and the entry is reserved for rebind via
        // attachWithRoot(). Callers of attachWithInstance that reach a
        // suspended match have skipped the normal rebind path — preserve
        // the existing dedup behavior (return true = "block this attach").
        if (m.suspended) return true;
        if (m.instance.devices.len == 0) return false;
        var pfd = [_]posix.pollfd{m.instance.devices[0].pollfd()};
        pfd[0].events = posix.POLL.IN;
        pfd[0].revents = 0;
        _ = posix.poll(&pfd, 0) catch return false;
        return (pfd[0].revents & (posix.POLL.NVAL | posix.POLL.HUP | posix.POLL.ERR)) == 0;
    }

    fn spawnInstance(self: *Supervisor, phys_key: []const u8, instance: *DeviceInstance, default_pr: ?*mapping_cfg.ParseResult) !void {
        // Wire wedge atomics through the chokepoint so all 4 spawn call
        // sites (run() initial_configs, doReload, hotplug retry,
        // attachWithInstanceResult) get Bug E instrumentation uniformly.
        instance.attachWedges();
        // Install chord detector before spawning the thread so the mapper sees
        // the cfg from the very first frame.
        if (self.chord_detector_cfg) |cfg| {
            if (instance.mapper) |*mp| mp.setChordDetector(cfg);
        }
        const thread = try std.Thread.spawn(.{}, threadEntry, .{instance});
        errdefer {
            instance.stop();
            thread.join();
        }
        const key_copy = try self.allocator.dupe(u8, phys_key);
        errdefer self.allocator.free(key_copy);
        try self.managed.append(self.allocator, .{
            .phys_key = key_copy,
            .devname = null,
            .instance = instance,
            .thread = thread,
            .mapping_arena = std.heap.ArenaAllocator.init(self.allocator),
            .switch_mapping = null,
            .default_mapping_pr = default_pr,
        });
        self.sweepShadowNodes(&self.managed.items[self.managed.items.len - 1]);
    }

    fn pruneStaleSuspendedForId(self: *Supervisor, vid: u16, pid: u16, keep_phys: []const u8) void {
        var i: usize = self.managed.items.len;
        var pruned: bool = false;
        while (i > 0) {
            i -= 1;
            const m = &self.managed.items[i];
            if (!m.suspended) continue;
            const m_vid: u16 = @intCast(m.instance.device_cfg.device.vid);
            const m_pid: u16 = @intCast(m.instance.device_cfg.device.pid);
            if (m_vid != vid or m_pid != pid) continue;
            if (std.mem.eql(u8, m.phys_key, keep_phys)) continue;
            std.log.info("hotplug: forfeiting grace for stale suspended phys \"{s}\" (VID={x:0>4} PID={x:0>4}); new attach at \"{s}\"", .{ m.phys_key, vid, pid, keep_phys });
            self.traceLifecycle("prune_stale_suspended vid=0x{x:0>4} pid=0x{x:0>4} kept_phys={s} pruned_phys={s}", .{
                vid, pid, keep_phys, m.phys_key,
            });
            self.teardownManaged(m);
            _ = self.managed.swapRemove(i);
            pruned = true;
        }
        if (pruned) self.armGraceTimer();
    }

    fn teardownManaged(self: *Supervisor, m: *ManagedInstance) void {
        m.shadow_grabs.releaseAll();
        if (m.switch_mapping) |pm| {
            pm.deinit();
            self.allocator.destroy(pm);
        }
        if (m.switch_mapping_stem) |s| self.allocator.free(s);
        if (m.default_mapping_pr) |pm| {
            pm.deinit();
            self.allocator.destroy(pm);
        }
        if (m.default_mapping_stem) |s| self.allocator.free(s);
        m.instance.deinit();
        self.allocator.destroy(m.instance);
        m.mapping_arena.deinit();
        self.allocator.free(m.phys_key);
        if (m.devname) |dn| {
            if (self.devname_map.fetchRemove(dn)) |e| {
                self.allocator.free(e.key);
                self.allocator.free(e.value);
            }
            self.allocator.free(dn);
        }
    }

    fn restartManagedThread(m: *ManagedInstance) !void {
        if (builtin.is_test and test_fail_next_restart_managed) {
            test_fail_next_restart_managed = false;
            return error.TestInjectedRestartFailure;
        }
        @atomicStore(bool, &m.instance.stopped, false, .release);
        @atomicStore(bool, &m.instance.loop.running, true, .release);
        @atomicStore(bool, &m.instance.loop.disconnected, false, .release);
        m.thread = std.Thread.spawn(.{}, threadEntry, .{m.instance}) catch |err| {
            @atomicStore(bool, &m.instance.stopped, true, .release);
            @atomicStore(bool, &m.instance.loop.running, false, .release);
            return err;
        };
    }

    fn rerunInitAfterRebind(m: *ManagedInstance) !void {
        m.instance.rerunInitSequence() catch |err| {
            m.instance.closeDeviceIO();
            return err;
        };
    }

    /// Commit bookkeeping for a suspended instance whose device fds have
    /// just been rebound by `attachWithRoot()`. Allocates the devname /
    /// phys slices, updates `devname_map`, restarts the worker thread,
    /// and only then flips `m.suspended`/`m.grace_deadline_ns`.
    ///
    /// Contract: any failure leaves `m.suspended`, `m.grace_deadline_ns`,
    /// `m.devname`, `m.thread`, and `devname_map` exactly as they were
    /// on entry. The caller's grace-window GC is preserved so the entry
    /// is still cleaned up on deadline. On error the caller is responsible for
    /// `m.instance.closeDeviceIO()`.
    pub fn finalizeRebind(self: *Supervisor, m: *ManagedInstance, devname: []const u8, phys: []const u8) !void {
        const dn_copy = try self.allocator.dupe(u8, devname);
        errdefer self.allocator.free(dn_copy);

        const dev_copy = try self.allocator.dupe(u8, devname);
        errdefer self.allocator.free(dev_copy);

        const phys_copy = try self.allocator.dupe(u8, phys);
        errdefer self.allocator.free(phys_copy);

        // devname_map takes ownership of dev_copy/phys_copy on success.
        try self.devname_map.put(dev_copy, phys_copy);
        errdefer _ = self.devname_map.remove(dev_copy);

        if (builtin.is_test and self.test_fail_rebind_restart) return error.TestInjectedRestartFailure;
        try restartManagedThread(m);

        m.devname = dn_copy;
        m.suspended = false;
        m.grace_deadline_ns = null;
        self.armGraceTimer();
        self.sweepShadowNodes(m);
    }

    fn clearSwitchMapping(self: *Supervisor, m: *ManagedInstance) void {
        if (m.switch_mapping) |pm| {
            pm.deinit();
            self.allocator.destroy(pm);
            m.switch_mapping = null;
        }
        if (m.switch_mapping_stem) |s| {
            self.allocator.free(s);
            m.switch_mapping_stem = null;
        }
    }

    // Clears the mapper and returns to passthrough. Transactional like
    // handleSwitch: each target is committed (current mapper saved into the tx),
    // and any restart failure rolls back every already-committed target so no
    // device is left permanently mapper-less.
    fn commitSwitchToNone(self: *Supervisor, tx: *SwitchTx) !void {
        const m = &self.managed.items[tx.idx];
        tx.old_mapper = m.instance.mapper;
        tx.old_mapping_cfg = m.instance.mapping_cfg;
        tx.old_switch_mapping = m.switch_mapping;
        tx.old_switch_mapping_stem = m.switch_mapping_stem;

        m.instance.stop();
        m.thread.join();

        if (builtin.is_test and self.test_switch_fail_commit_index != null and self.test_switch_fail_commit_index.? == tx.idx) {
            self.restoreSwitchTarget(tx, m, false);
            return error.SwitchFailed;
        }

        m.instance.quiesceOutputs(.{});
        m.instance.mapper = null;
        m.instance.mapping_cfg = null;
        m.switch_mapping = null;
        m.switch_mapping_stem = null;
        restartManagedThread(m) catch |err| {
            self.restoreSwitchTarget(tx, m, false);
            return err;
        };
        tx.committed = true;
    }

    fn handleSwitchNone(self: *Supervisor, fd: posix.fd_t, device_id: ?[]const u8) void {
        var cs = &self.ctrl_sock.?;

        var txs = std.ArrayList(SwitchTx){};
        defer {
            self.cleanupSwitchTxs(txs.items);
            txs.deinit(self.allocator);
        }

        var found = false;
        for (self.managed.items, 0..) |*m, idx| {
            if (m.suspended) continue;
            if (device_id) |dev_id| {
                const dn = m.devname orelse continue;
                if (!std.mem.eql(u8, dn, dev_id)) continue;
            }
            found = true;
            txs.append(self.allocator, .{ .idx = idx, .new_mapper = null, .parsed_ptr = null }) catch {
                cs.sendResponse(fd, "ERR oom\n");
                return;
            };
            if (device_id != null) break;
        }

        if (device_id != null and !found) {
            cs.sendResponse(fd, "ERR device-not-found\n");
            return;
        }

        for (txs.items, 0..) |*tx, commit_idx| {
            if (shouldInjectSwitchFailure(self, commit_idx)) {
                self.rollbackCommittedSwitches(txs.items);
                cs.sendResponse(fd, "ERR restart-failed\n");
                return;
            }
            self.commitSwitchToNone(tx) catch {
                self.rollbackCommittedSwitches(txs.items);
                cs.sendResponse(fd, "ERR restart-failed\n");
                return;
            };
        }

        cs.sendResponse(fd, "OK none\n");
    }

    fn lookupSwitchMappingPath(self: *Supervisor, name: []const u8) !?[]const u8 {
        if (builtin.is_test) {
            if (self.test_switch_mapping_override) |override_path| {
                return @as(?[]const u8, try self.allocator.dupe(u8, override_path));
            }
        }
        return @as(?[]const u8, try mapping_discovery.findMapping(self.allocator, name));
    }

    fn restoreSwitchTarget(self: *Supervisor, tx: *SwitchTx, m: *ManagedInstance, deinit_current_mapper: bool) void {
        if (deinit_current_mapper) {
            if (m.instance.mapper) |*cur| {
                cur.deinit();
                m.instance.mapper = null;
            }
            if (m.switch_mapping) |pm| {
                pm.deinit();
                self.allocator.destroy(pm);
                m.switch_mapping = null;
            }
            if (m.switch_mapping_stem) |s| {
                self.allocator.free(s);
                m.switch_mapping_stem = null;
            }
        }

        m.instance.mapper = tx.old_mapper;
        m.instance.mapping_cfg = tx.old_mapping_cfg;
        if (m.instance.mapper) |*old| {
            old.resetRuntimeState();
            old.seedInputState(m.instance.loop.gamepad_state);
        }
        m.switch_mapping = tx.old_switch_mapping;
        m.switch_mapping_stem = tx.old_switch_mapping_stem;
        restartManagedThread(m) catch |err| {
            std.log.err("rollback restart failed for {s}: {}", .{ m.phys_key, err });
        };

        tx.old_mapper = null;
        tx.old_mapping_cfg = null;
        tx.old_switch_mapping = null;
        tx.old_switch_mapping_stem = null;
        tx.committed = false;
    }

    fn rollbackCommittedSwitches(self: *Supervisor, txs: []SwitchTx) void {
        var r = txs.len;
        while (r > 0) {
            r -= 1;
            const tx = &txs[r];
            if (!tx.committed) continue;
            const m = &self.managed.items[tx.idx];
            m.instance.stop();
            m.thread.join();
            m.instance.quiesceOutputs(.{});
            self.restoreSwitchTarget(tx, m, true);
        }
    }

    fn cleanupSwitchTxs(self: *Supervisor, txs: []SwitchTx) void {
        for (txs) |*tx| {
            if (tx.committed) {
                if (tx.old_mapper) |*old| old.deinit();
                if (tx.old_switch_mapping) |pm| {
                    pm.deinit();
                    self.allocator.destroy(pm);
                }
                if (tx.old_switch_mapping_stem) |s| self.allocator.free(s);
            } else {
                if (tx.new_mapper) |*new| new.deinit();
                if (tx.parsed_ptr) |pm| {
                    pm.deinit();
                    self.allocator.destroy(pm);
                }
                if (tx.path_stem) |s| self.allocator.free(s);
            }
        }
    }

    fn commitSwitchTarget(self: *Supervisor, tx: *SwitchTx) !void {
        const m = &self.managed.items[tx.idx];
        tx.old_mapper = m.instance.mapper;
        tx.old_mapping_cfg = m.instance.mapping_cfg;
        tx.old_switch_mapping = m.switch_mapping;
        tx.old_switch_mapping_stem = m.switch_mapping_stem;

        m.instance.stop();
        m.thread.join();

        if (builtin.is_test and self.test_switch_fail_commit_index != null and self.test_switch_fail_commit_index.? == tx.idx) {
            self.restoreSwitchTarget(tx, m, false);
            return error.SwitchFailed;
        }

        m.instance.quiesceOutputs(.{});
        if (tx.new_mapper) |*new_mapper| {
            new_mapper.seedInputState(m.instance.loop.gamepad_state);
        }
        m.instance.mapper = tx.new_mapper.?;
        tx.new_mapper = null;
        m.instance.mapping_cfg = &tx.parsed_ptr.?.value;
        self.installChordDetector(m);
        // Rebuild AuxDevice when switching mappings so newly required KEY_* or
        // REL_* capabilities become available. Warn rather than propagate on
        // failure, matching the reload path.
        m.instance.rebuildAuxIfChanged(
            &tx.parsed_ptr.?.value,
            tx.old_mapping_cfg,
        ) catch |err| {
            std.log.warn("rebuildAuxIfChanged during switch: {}", .{err});
        };
        restartManagedThread(m) catch |err| {
            if (tx.old_mapping_cfg) |old_cfg| {
                m.instance.rebuildAuxIfChanged(old_cfg, &tx.parsed_ptr.?.value) catch |rollback_aux_err| {
                    std.log.warn("rebuildAuxIfChanged rollback during switch: {}", .{rollback_aux_err});
                };
            }
            if (m.instance.mapper) |*cur| {
                cur.deinit();
                m.instance.mapper = null;
            }
            m.instance.mapper = tx.old_mapper;
            m.instance.mapping_cfg = tx.old_mapping_cfg;
            if (m.instance.mapper) |*old| {
                old.resetRuntimeState();
                old.seedInputState(m.instance.loop.gamepad_state);
            }
            m.switch_mapping = tx.old_switch_mapping;
            m.switch_mapping_stem = tx.old_switch_mapping_stem;
            restartManagedThread(m) catch |rollback_err| {
                std.log.err("rollback restart failed for {s}: {}", .{ m.phys_key, rollback_err });
            };
            tx.old_mapper = null;
            tx.old_mapping_cfg = null;
            tx.old_switch_mapping = null;
            tx.old_switch_mapping_stem = null;
            tx.committed = false;
            return err;
        };
        m.switch_mapping = tx.parsed_ptr;
        // switch_mapping_stem must always be set together with switch_mapping so
        // handleStatus can report the mapping name. Any new apply-mapping path
        // must derive and assign switch_mapping_stem here.
        m.switch_mapping_stem = tx.path_stem;
        tx.parsed_ptr = null;
        tx.path_stem = null;
        tx.committed = true;
    }

    fn reloadUserConfig(self: *Supervisor) void {
        if (self.user_cfg) |*uc| uc.deinit();
        self.user_cfg = user_config_mod.load(self.allocator);
        self.applyUserConfigRuntime();
    }

    // m.devname must be a separate allocation from the map key — teardownManaged
    // frees both independently after fetchRemove. Used by startFromDirWithRoot,
    // which calls with `catch {}` (best-effort OOM swallow). NOT safe to call
    // in attachWithInstance flow where bookkeeping must happen before spawn.
    fn bindManagedDevname(self: *Supervisor, m: *ManagedInstance, devname: []const u8, phys: []const u8) !void {
        const map_key_dup = try self.allocator.dupe(u8, devname);
        errdefer self.allocator.free(map_key_dup);
        const phys_dup = try self.allocator.dupe(u8, phys);
        errdefer self.allocator.free(phys_dup);
        const m_devname_dup = try self.allocator.dupe(u8, devname);
        errdefer self.allocator.free(m_devname_dup);
        try self.devname_map.put(map_key_dup, phys_dup);
        m.devname = m_devname_dup;
    }

    fn clearDevnameMap(self: *Supervisor) void {
        var it = self.devname_map.iterator();
        while (it.next()) |entry| {
            self.allocator.free(entry.key_ptr.*);
            self.allocator.free(entry.value_ptr.*);
        }
        self.devname_map.clearRetainingCapacity();
    }

    fn clearAllManagedAndConfigs(self: *Supervisor) void {
        self.stopAll();
        for (self.configs.items) |c| {
            c.deinit();
            self.allocator.destroy(c);
        }
        self.configs.clearRetainingCapacity();
        self.clearDevnameMap();
    }

    fn doReload(
        self: *Supervisor,
        reloadFn: *const fn (allocator: std.mem.Allocator) anyerror![]ConfigEntry,
        reload_allocator: std.mem.Allocator,
        initFn: *const fn (allocator: std.mem.Allocator, entry: ConfigEntry) anyerror!*DeviceInstance,
    ) void {
        self.reloadUserConfig();

        const new_configs = reloadFn(reload_allocator) catch |err| {
            std.log.err("reload failed: {}", .{err});
            return;
        };
        defer reload_allocator.free(new_configs);
        self.reload(new_configs, initFn) catch |err| {
            std.log.err("hot-reload diff failed: {}", .{err});
        };
    }

    pub fn stopAll(self: *Supervisor) void {
        for (self.managed.items) |*m| {
            if (!m.suspended) m.instance.stop();
        }
        for (self.managed.items) |*m| {
            if (!m.suspended) m.thread.join();
        }
        for (self.managed.items) |*m| {
            if (!m.suspended) m.instance.quiesceOutputs(.{ .reset_input_state = true, .reset_mapper_state = true });
        }
        for (self.managed.items) |*m| self.teardownManaged(m);
        self.managed.clearRetainingCapacity();
    }

    /// Hot-reload: diff new_configs against running instances by phys_key.
    pub fn reload(
        self: *Supervisor,
        new_configs: []const ConfigEntry,
        initFn: *const fn (allocator: std.mem.Allocator, entry: ConfigEntry) anyerror!*DeviceInstance,
    ) !void {
        var to_remove = std.ArrayList(usize){};
        defer to_remove.deinit(self.allocator);

        outer: for (self.managed.items, 0..) |*m, i| {
            for (new_configs) |nc| {
                if (std.mem.eql(u8, m.phys_key, nc.phys_key)) continue :outer;
            }
            try to_remove.append(self.allocator, i);
        }

        var r = to_remove.items.len;
        while (r > 0) {
            r -= 1;
            const idx = to_remove.items[r];
            const m = &self.managed.items[idx];
            if (!m.suspended) {
                m.instance.stop();
                m.thread.join();
                m.instance.quiesceOutputs(.{ .reset_input_state = true, .reset_mapper_state = true });
            }
            self.teardownManaged(m);
            _ = self.managed.swapRemove(idx);
        }

        for (new_configs) |nc| {
            var found: ?*ManagedInstance = null;
            for (self.managed.items) |*m| {
                if (std.mem.eql(u8, m.phys_key, nc.phys_key)) {
                    found = m;
                    break;
                }
            }

            if (found == null) {
                const instance = try initFn(self.allocator, nc);
                try self.spawnInstance(nc.phys_key, instance, null);
            } else if (nc.mapping_cfg) |new_map| {
                const m = found.?;
                if (m.suspended) continue;

                var new_mapping_arena = std.heap.ArenaAllocator.init(self.allocator);
                errdefer new_mapping_arena.deinit();
                const new_arena_alloc = new_mapping_arena.allocator();
                const map_copy = try new_arena_alloc.create(MappingConfig);
                map_copy.* = new_map.*;

                // Build the new mapper before touching the old arena so a
                // failed reload leaves the running mapping intact.
                var new_mapper = try Mapper.init(map_copy, m.instance.loop.macro_timer_fd, self.allocator);
                var new_mapper_installed = false;
                errdefer if (!new_mapper_installed) new_mapper.deinit();

                // Stop-Swap-Restart: stop thread before touching arena
                m.instance.stop();
                m.thread.join();
                m.instance.quiesceOutputs(.{});

                var old_mapper = m.instance.mapper;
                const old_mapping_cfg = m.instance.mapping_cfg;
                var old_mapping_arena = m.mapping_arena;

                // Rebuild the mapper so layer state/timers do not keep slices into
                // the old mapping arena after the swap below.
                new_mapper.seedInputState(m.instance.loop.gamepad_state);
                m.instance.mapper = new_mapper;
                new_mapper_installed = true;
                m.instance.mapping_cfg = map_copy;
                self.installChordDetector(m);
                m.instance.rebuildAuxIfChanged(map_copy, old_mapping_cfg) catch |err| {
                    std.log.warn("rebuildAuxIfChanged: {}", .{err});
                };
                restartManagedThread(m) catch |err| {
                    if (old_mapping_cfg) |old_cfg| {
                        m.instance.rebuildAuxIfChanged(old_cfg, map_copy) catch |rollback_aux_err| {
                            std.log.warn("rebuildAuxIfChanged rollback: {}", .{rollback_aux_err});
                        };
                    }
                    if (old_mapper) |*mapper| {
                        mapper.resetRuntimeState();
                        mapper.seedInputState(m.instance.loop.gamepad_state);
                    }
                    m.instance.mapper = old_mapper;
                    m.instance.mapping_cfg = old_mapping_cfg;
                    new_mapper.deinit();
                    restartManagedThread(m) catch |rollback_err| {
                        std.log.err("reload rollback restart failed for {s}: {}", .{ m.phys_key, rollback_err });
                    };
                    return err;
                };

                m.mapping_arena = new_mapping_arena;
                old_mapping_arena.deinit();
                self.clearSwitchMapping(m);

                if (old_mapper) |*mapper| {
                    mapper.deinit();
                }
            } else {
                const m = found.?;
                if (m.suspended) continue;
                m.instance.stop();
                m.thread.join();
                m.instance.quiesceOutputs(.{});
                if (m.instance.mapper) |*mapper| {
                    mapper.deinit();
                    m.instance.mapper = null;
                }
                self.clearSwitchMapping(m);
                _ = m.mapping_arena.reset(.retain_capacity);
                m.instance.mapping_cfg = null;
                try restartManagedThread(m);
            }
        }
    }

    fn netlinkCallback(self: *Supervisor, action: netlink.UeventAction, subsystem: netlink.Subsystem, devname: []const u8) void {
        switch (subsystem) {
            .hidraw => switch (action) {
                .add => self.attach(devname) catch |err| {
                    if (err == error.HotplugTransient) {
                        self.enqueueHotplugRetry(devname);
                    } else {
                        std.log.warn("hotplug attach {s}: {}", .{ devname, err });
                    }
                },
                .remove => self.detach(devname),
                .other => {},
            },
            .input => switch (action) {
                .add => self.handleInputNodeAdd(devname),
                .remove => self.handleInputNodeRemove(devname),
                .other => {},
            },
        }
    }

    fn shadowParams(m: *const ManagedInstance) shadow_grab.Params {
        const cfg = m.instance.device_cfg;
        // readPhysicalPath falls back to the raw "/dev/..." node when sysfs has
        // no HID_PHYS; that is not an evdev topology path, so it must not gate
        // grabs. A real phys key ("usb-...") never starts with '/'.
        const phys_path: []const u8 = if (std.mem.startsWith(u8, m.phys_key, "/")) "" else m.phys_key;
        return .{
            .phys_vendor = @intCast(cfg.device.vid),
            .phys_product = @intCast(cfg.device.pid),
            .phys_path = phys_path,
        };
    }

    /// Offer `node` to every active managed instance. `.access_denied` means
    /// the open raced udev permission setup and a retry may succeed.
    fn tryGrabForManaged(self: *Supervisor, node: []const u8) shadow_grab.GrabResult {
        var denied = false;
        for (self.managed.items) |*m| {
            if (m.suspended) continue;
            switch (shadow_grab.tryGrabNode(&m.shadow_grabs, "/dev/input", node, shadowParams(m))) {
                .grabbed => return .grabbed,
                .already_grabbed => return .already_grabbed,
                .access_denied => denied = true,
                .skipped => {},
            }
        }
        return if (denied) .access_denied else .skipped;
    }

    /// Netlink saw a new /dev/input/event* node: grab it if it shadows an
    /// active managed device.
    fn handleInputNodeAdd(self: *Supervisor, devname: []const u8) void {
        const node = std.fs.path.basename(devname);
        if (self.tryGrabForManaged(node) == .access_denied) {
            self.enqueueRetry(.input_grab, node);
        }
    }

    /// Netlink saw a /dev/input/event* node disappear: drop any stale grab so
    /// a future node reusing the same eventN name stays grabbable.
    fn handleInputNodeRemove(self: *Supervisor, devname: []const u8) void {
        const node = std.fs.path.basename(devname);
        for (self.managed.items) |*m| {
            if (m.shadow_grabs.evict(node)) return;
        }
    }

    /// Grab pre-existing shadow nodes when an instance becomes active (spawn
    /// and resume) — a shadow that predates the daemon never produces a
    /// netlink ADD. Nodes that race udev permission setup are queued for retry,
    /// mirroring the netlink ADD path so a pre-existing shadow is never lost.
    fn sweepShadowNodes(self: *Supervisor, m: *ManagedInstance) void {
        shadow_grab.sweepDir(&m.shadow_grabs, "/dev/input", shadowParams(m), self, enqueueShadowRetry);
    }

    fn enqueueShadowRetry(self: *Supervisor, node: []const u8) void {
        self.enqueueRetry(.input_grab, node);
    }

    fn drainNetlink(self: *Supervisor) void {
        if (self.netlink_fd < 0) return;
        netlink.drainNetlink(self.netlink_fd, self, netlinkCallback);
    }

    fn armDebounce(self: *Supervisor) void {
        if (self.debounce_fd < 0) return;
        const spec = linux.itimerspec{
            .it_value = .{ .sec = 0, .nsec = 500_000_000 },
            .it_interval = .{ .sec = 0, .nsec = 0 },
        };
        _ = linux.timerfd_settime(self.debounce_fd, .{}, &spec, null);
    }

    fn enqueueHotplugRetry(self: *Supervisor, devname: []const u8) void {
        self.enqueueRetry(.hidraw, devname);
    }

    fn enqueueRetry(self: *Supervisor, kind: PendingKind, devname: []const u8) void {
        if (self.hotplug_retry_fd < 0) return;
        for (self.hotplug_pending.items) |pending| {
            if (pending.kind == kind and std.mem.eql(u8, pending.devname[0..pending.len], devname)) return;
        }
        var entry: HotplugPending = undefined;
        const n = @min(devname.len, entry.devname.len);
        @memcpy(entry.devname[0..n], devname[0..n]);
        entry.len = @intCast(n);
        entry.retries = 0;
        entry.kind = kind;
        entry.next_deadline_ns = self.nowNs() + delayNsFor(kind, 0);
        self.hotplug_pending.append(self.allocator, entry) catch return;
        self.armHotplugRetry();
    }

    fn enqueueHotplugRetryForPath(self: *Supervisor, path: []const u8) void {
        self.enqueueHotplugRetry(std.fs.path.basename(path));
    }

    fn isHidrawDevname(name: []const u8) bool {
        if (!std.mem.startsWith(u8, name, "hidraw")) return false;
        if (name.len == "hidraw".len) return false;
        for (name["hidraw".len..]) |c| {
            if (!std.ascii.isDigit(c)) return false;
        }
        return true;
    }

    fn enqueueColdScanRetriesForDevRoot(self: *Supervisor, dev_root: []const u8) void {
        var dir = std.fs.openDirAbsolute(dev_root, .{ .iterate = true }) catch return;
        defer dir.close();

        var count: usize = 0;
        var it = dir.iterate();
        while (it.next() catch return) |entry| {
            if (!isHidrawDevname(entry.name)) continue;
            self.enqueueHotplugRetry(entry.name);
            count += 1;
        }
        self.traceLifecycle("cold_scan_retry source=enqueueColdScanRetriesForDevRoot count={d}", .{count});
    }

    fn drainHotplugRetry(self: *Supervisor) void {
        if (self.hotplug_retry_fd < 0) return;
        var tbuf: [8]u8 = undefined;
        _ = posix.read(self.hotplug_retry_fd, &tbuf) catch {};
        var i: usize = 0;
        while (i < self.hotplug_pending.items.len) {
            const p = &self.hotplug_pending.items[i];
            // Each entry rides its own backoff schedule: skip it until its
            // absolute deadline so a co-pending fast entry never accelerates it.
            if (self.nowNs() < p.next_deadline_ns) {
                i += 1;
                continue;
            }
            const name = p.devname[0..p.len];
            if (p.kind == .input_grab) {
                if (self.tryGrabForManaged(name) == .access_denied) {
                    p.retries += 1;
                    if (p.retries >= 3) {
                        std.log.debug("shadow grab: giving up on {s} after 3 retries", .{name});
                        _ = self.hotplug_pending.swapRemove(i);
                    } else {
                        p.next_deadline_ns = self.nowNs() + delayNsFor(p.kind, p.retries);
                        i += 1;
                    }
                } else {
                    _ = self.hotplug_pending.swapRemove(i);
                }
                continue;
            }
            self.attach(name) catch |err| {
                if (err != error.HotplugTransient) {
                    std.log.warn("hotplug retry {s}: {}, dropping", .{ name, err });
                    _ = self.hotplug_pending.swapRemove(i);
                    continue;
                }
                p.retries += 1;
                if (p.retries >= HOTPLUG_RETRY_BACKOFF_MS.len) {
                    if (self.last_attach_node_gone) {
                        std.log.debug("hotplug: {s} removed before attach (claimed or unplugged), giving up", .{name});
                    } else {
                        std.log.warn("hotplug: giving up on {s} after {d} retries", .{ name, p.retries });
                    }
                    _ = self.hotplug_pending.swapRemove(i);
                } else {
                    p.next_deadline_ns = self.nowNs() + delayNsFor(p.kind, p.retries);
                    i += 1;
                }
                continue;
            };
            _ = self.hotplug_pending.swapRemove(i);
        }
        if (self.hotplug_pending.items.len > 0) {
            self.armHotplugRetry();
        }
    }

    fn delayNsFor(kind: PendingKind, retries: u8) u64 {
        const ms: u64 = switch (kind) {
            .input_grab => 300,
            .hidraw => HOTPLUG_RETRY_BACKOFF_MS[@min(retries, HOTPLUG_RETRY_BACKOFF_MS.len - 1)],
        };
        return ms * std.time.ns_per_ms;
    }

    fn armHotplugRetry(self: *Supervisor) void {
        if (self.hotplug_retry_fd < 0) return;
        var nearest: u64 = std.math.maxInt(u64);
        for (self.hotplug_pending.items) |*p| {
            if (p.next_deadline_ns < nearest) nearest = p.next_deadline_ns;
        }
        if (nearest == std.math.maxInt(u64)) return;
        const now = self.nowNs();
        // Arm at least ~1us so the timerfd fires promptly when a deadline has
        // already passed (also keeps `it_value` non-zero, i.e. armed).
        const delay_ns: u64 = if (nearest > now) nearest - now else 1_000;
        const spec = linux.itimerspec{
            .it_value = .{
                .sec = @intCast(delay_ns / std.time.ns_per_s),
                .nsec = @intCast(delay_ns % std.time.ns_per_s),
            },
            .it_interval = .{ .sec = 0, .nsec = 0 },
        };
        _ = linux.timerfd_settime(self.hotplug_retry_fd, .{}, &spec, null);
    }

    fn drainInotify(self: *Supervisor) void {
        if (self.inotify_fd < 0) return;
        var buf: [4096]u8 = undefined;
        while (true) {
            const n = posix.read(self.inotify_fd, &buf) catch break;
            if (n == 0) break;
        }
        self.armDebounce();
    }

    /// Slot indices into the pollfd array. `null` means the corresponding fd
    /// is unavailable (e.g. `initForTest` skips netlink/inotify/grace_timer).
    /// Stop and hup always occupy slots 0/1; the rest are assigned in the
    /// fixed order netlink → inotify → debounce → hotplug_retry → grace_timer
    /// → liveness_timer → listen, packed contiguously starting at slot 2.
    const SupervisorPollSet = struct {
        base_nfds: usize,
        netlink_slot: ?usize,
        inotify_slot: ?usize,
        debounce_slot: ?usize,
        hotplug_retry_slot: ?usize,
        grace_timer_slot: ?usize,
        liveness_timer_slot: ?usize,
        listen_slot: ?usize,

        fn init(self: *const Supervisor, pollfds: *[SUPERVISOR_MAX_FDS]posix.pollfd) SupervisorPollSet {
            pollfds[0] = .{ .fd = self.stop_fd, .events = posix.POLL.IN, .revents = 0 };
            pollfds[1] = .{ .fd = self.hup_fd, .events = posix.POLL.IN, .revents = 0 };
            var base_nfds: usize = 2;
            const netlink_slot: ?usize = if (self.netlink_fd >= 0) blk: {
                pollfds[base_nfds] = .{ .fd = self.netlink_fd, .events = posix.POLL.IN, .revents = 0 };
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;
            const inotify_slot: ?usize = if (self.inotify_fd >= 0) blk: {
                pollfds[base_nfds] = .{ .fd = self.inotify_fd, .events = posix.POLL.IN, .revents = 0 };
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;
            const debounce_slot: ?usize = if (self.debounce_fd >= 0) blk: {
                pollfds[base_nfds] = .{ .fd = self.debounce_fd, .events = posix.POLL.IN, .revents = 0 };
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;
            const hotplug_retry_slot: ?usize = if (self.hotplug_retry_fd >= 0) blk: {
                pollfds[base_nfds] = .{ .fd = self.hotplug_retry_fd, .events = posix.POLL.IN, .revents = 0 };
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;
            const grace_timer_slot: ?usize = if (self.grace_timer_fd >= 0) blk: {
                pollfds[base_nfds] = .{ .fd = self.grace_timer_fd, .events = posix.POLL.IN, .revents = 0 };
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;
            const liveness_timer_slot: ?usize = if (self.liveness_timer_fd >= 0) blk: {
                pollfds[base_nfds] = .{ .fd = self.liveness_timer_fd, .events = posix.POLL.IN, .revents = 0 };
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;
            const listen_slot: ?usize = if (self.ctrl_sock) |cs| blk: {
                pollfds[base_nfds] = cs.pollfd();
                const s = base_nfds;
                base_nfds += 1;
                break :blk s;
            } else null;

            return .{
                .base_nfds = base_nfds,
                .netlink_slot = netlink_slot,
                .inotify_slot = inotify_slot,
                .debounce_slot = debounce_slot,
                .hotplug_retry_slot = hotplug_retry_slot,
                .grace_timer_slot = grace_timer_slot,
                .liveness_timer_slot = liveness_timer_slot,
                .listen_slot = listen_slot,
            };
        }
    };

    /// Consolidated supervisor poll loop. `dispatch` is any value with a
    /// `reload(self: *Supervisor) void` method bound to the caller's reload
    /// strategy (test-driven reload diff, single-dir rescan, or multi-dir
    /// rescan). `ppoll_propagate_err` controls whether non-EINTR ppoll errors
    /// surface to the caller (`run`) or are silently swallowed (`serve`/
    /// `serveMulti`, which match the daemon's prior void-returning behavior).
    fn serveLoop(
        self: *Supervisor,
        dispatch: anytype,
        comptime ppoll_propagate_err: bool,
    ) !void {
        defer self.stopAll();

        const Dispatch = @TypeOf(dispatch);
        self.reload_hook = .{
            .ctx = &dispatch,
            .call = struct {
                fn call(ctx: *const anyopaque, sup: *Supervisor) void {
                    const d: *const Dispatch = @ptrCast(@alignCast(ctx));
                    d.reload(sup);
                }
            }.call,
        };
        defer self.reload_hook = null;

        // 8 base fds + 1 listen + 4 clients = 13
        var pollfds: [SUPERVISOR_MAX_FDS]posix.pollfd = undefined;
        const set = SupervisorPollSet.init(self, &pollfds);

        while (true) {
            // Rebuild client fds each iteration (clients may come and go)
            var nfds = set.base_nfds;
            if (self.ctrl_sock) |*cs| {
                nfds += cs.clientPollfds(pollfds[set.base_nfds..]);
            }

            _ = posix.ppoll(pollfds[0..nfds], null, null) catch |err| switch (err) {
                error.SignalInterrupt => continue,
                else => if (ppoll_propagate_err) return err else return,
            };

            if (pollfds[0].revents & posix.POLL.IN != 0) {
                var buf: [128]u8 = undefined;
                _ = posix.read(self.stop_fd, &buf) catch {};
                break;
            }

            if (pollfds[1].revents & posix.POLL.IN != 0) {
                var buf: [128]u8 = undefined;
                _ = posix.read(self.hup_fd, &buf) catch {};
                dispatch.reload(self);
                pollfds[1].revents = 0;
            }

            if (set.netlink_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    self.drainNetlink();
                    pollfds[slot].revents = 0;
                }
            }

            if (set.inotify_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    self.drainInotify();
                    pollfds[slot].revents = 0;
                }
            }

            if (set.debounce_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    var tbuf: [8]u8 = undefined;
                    _ = posix.read(self.debounce_fd, &tbuf) catch {};
                    dispatch.reload(self);
                    pollfds[slot].revents = 0;
                }
            }

            if (set.hotplug_retry_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    self.drainHotplugRetry();
                    pollfds[slot].revents = 0;
                }
            }

            if (set.grace_timer_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    self.drainGraceTimer();
                    pollfds[slot].revents = 0;
                }
            }

            if (set.liveness_timer_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    self.drainLivenessTimer();
                    pollfds[slot].revents = 0;
                }
            }

            if (set.listen_slot) |slot| {
                if (pollfds[slot].revents & posix.POLL.IN != 0) {
                    self.ctrl_sock.?.acceptClient();
                    pollfds[slot].revents = 0;
                }
            }

            if (self.ctrl_sock != null) {
                for (pollfds[set.base_nfds..nfds]) |*pfd| {
                    if (pfd.revents & posix.POLL.IN != 0) {
                        self.handleClientCommand(pfd.fd);
                    }
                    if (pfd.revents & (posix.POLL.HUP | posix.POLL.ERR) != 0) {
                        self.ctrl_sock.?.removeClient(pfd.fd);
                    }
                }
            }
        }
    }

    pub fn run(
        self: *Supervisor,
        initial_configs: []const ConfigEntry,
        initFn: *const fn (allocator: std.mem.Allocator, entry: ConfigEntry) anyerror!*DeviceInstance,
        reloadFn: *const fn (allocator: std.mem.Allocator) anyerror![]ConfigEntry,
        reload_allocator: std.mem.Allocator,
    ) !void {
        for (initial_configs) |nc| {
            const instance = try initFn(self.allocator, nc);
            try self.spawnInstance(nc.phys_key, instance, null);
        }

        const Dispatch = struct {
            initFn: *const fn (allocator: std.mem.Allocator, entry: ConfigEntry) anyerror!*DeviceInstance,
            reloadFn: *const fn (allocator: std.mem.Allocator) anyerror![]ConfigEntry,
            reload_allocator: std.mem.Allocator,

            fn reload(d: @This(), sup: *Supervisor) void {
                sup.doReload(d.reloadFn, d.reload_allocator, d.initFn);
            }
        };
        try self.serveLoop(Dispatch{
            .initFn = initFn,
            .reloadFn = reloadFn,
            .reload_allocator = reload_allocator,
        }, true);
    }

    fn handleClientCommand(self: *Supervisor, fd: posix.fd_t) void {
        var cs = &self.ctrl_sock.?;
        var buf: [control_socket.BUF_SIZE]u8 = undefined;
        const cmd = cs.readCommand(fd, &buf) orelse return;
        switch (cmd.tag) {
            .switch_mapping => self.handleSwitch(fd, cmd.name, null),
            .switch_device => self.handleSwitch(fd, cmd.name, cmd.device_id),
            .chord_switch => self.handleChordSwitch(fd, cmd.chord_index),
            .status => self.handleStatus(fd),
            .list => self.handleList(fd),
            .devices => self.handleDevices(fd),
            .reload => self.handleReload(fd),
            .dump_on => self.handleDump(fd, true),
            .dump_off => self.handleDump(fd, false),
            .dump_status => self.handleDumpStatus(fd),
            .bind_event => self.handleBindEvent(fd, cmd.bind_action, cmd.name, cmd.bind_button),
            .unknown => cs.sendResponse(fd, "ERR unknown-command\n"),
        }
    }

    fn handleReload(self: *Supervisor, fd: posix.fd_t) void {
        var cs = &self.ctrl_sock.?;
        const hook = self.reload_hook orelse {
            cs.sendResponse(fd, "ERR reload-unavailable\n");
            return;
        };
        self.reload_failure = null;
        hook.call(hook.ctx, self);
        var buf: [control_socket.BUF_SIZE]u8 = undefined;
        if (self.reload_failure) |*f| {
            const msg = std.fmt.bufPrint(&buf, "ERR {s}: {s}\n", .{ f.file(), f.reason() }) catch "ERR reload-failed\n";
            cs.sendResponse(fd, msg);
            return;
        }
        const msg = std.fmt.bufPrint(&buf, "OK reloaded {d} devices\n", .{self.managed.items.len}) catch "OK reloaded\n";
        cs.sendResponse(fd, msg);
    }

    /// BIND_EVENT is fired by the per-device event loop when a dynamic
    /// binding action happens. The supervisor broadcasts a formatted line
    /// to every currently-connected client so external listeners (tray UIs,
    /// `padctl listen`-style tools) can observe the runtime state.
    fn handleBindEvent(self: *Supervisor, fd: posix.fd_t, action: []const u8, layer: []const u8, button: []const u8) void {
        var cs = &self.ctrl_sock.?;
        var buf: [256]u8 = undefined;
        const msg = std.fmt.bufPrint(&buf, "EVENT bind action={s} layer={s} button={s}\n", .{ action, layer, button }) catch {
            cs.sendResponse(fd, "ERR bind-event-format\n");
            return;
        };
        cs.broadcastLine(msg);
        cs.sendResponse(fd, "OK\n");
    }

    fn handleChordSwitch(self: *Supervisor, fd: posix.fd_t, chord_index: u8) void {
        var cs = &self.ctrl_sock.?;
        if (chord_index == 0) {
            cs.sendResponse(fd, "ERR chord-index-invalid\n");
            return;
        }
        const name = self.lookupChordMappingName(chord_index) catch {
            cs.sendResponse(fd, "ERR chord-lookup-failed\n");
            return;
        };
        if (name == null) {
            std.log.warn("chord_switch: no mapping has chord_index = {d}", .{chord_index});
            cs.sendResponse(fd, "ERR chord-mapping-not-found\n");
            return;
        }
        defer self.allocator.free(name.?);
        self.handleSwitch(fd, name.?, null);
    }

    fn lookupChordMappingName(self: *Supervisor, chord_index: u8) !?[]const u8 {
        const profiles = mapping_discovery.discoverMappings(self.allocator) catch |err| {
            std.log.warn("chord_switch: discoverMappings failed: {}", .{err});
            return err;
        };
        defer mapping_discovery.freeProfiles(self.allocator, profiles);

        // Sort by name ascending for deterministic resolution across filesystems.
        std.sort.pdq(mapping_discovery.MappingProfile, profiles, {}, struct {
            fn lessThan(_: void, a: mapping_discovery.MappingProfile, b: mapping_discovery.MappingProfile) bool {
                return std.mem.lessThan(u8, a.name, b.name);
            }
        }.lessThan);

        var found: ?[]const u8 = null;
        var dup = false;
        for (profiles) |p| {
            const parsed = mapping_cfg.parseFile(self.allocator, p.path) catch continue;
            defer parsed.deinit();
            const ci = parsed.value.chord_index orelse continue;
            if (ci != chord_index) continue;
            if (found != null) {
                dup = true;
                continue;
            }
            found = try self.allocator.dupe(u8, p.name);
        }
        if (dup and found != null)
            std.log.warn("chord_switch: chord_index={d} matches multiple mappings; using '{s}' (first by name)", .{ chord_index, found.? });
        return found;
    }

    fn handleSwitch(self: *Supervisor, fd: posix.fd_t, name: []const u8, device_id: ?[]const u8) void {
        var cs = &self.ctrl_sock.?;
        if (self.managed.items.len == 0) {
            cs.sendResponse(fd, "ERR no-devices\n");
            return;
        }

        // "none" clears mapping and returns to passthrough mode
        if (std.mem.eql(u8, name, "none")) {
            self.handleSwitchNone(fd, device_id);
            return;
        }

        // If the name is an absolute path, accept it only when it resides in a
        // trusted mapping config dir; otherwise do server-side name lookup.
        const path: ?[]const u8 = if (name.len > 0 and name[0] == '/') blk: {
            const dirs = config_paths.resolveMappingConfigDirs(self.allocator) catch {
                cs.sendResponse(fd, "ERR mapping-lookup-failed\n");
                return;
            };
            defer config_paths.freeConfigDirs(self.allocator, dirs);
            if (!absolutePathInMappingDirs(name, dirs)) {
                cs.sendResponse(fd, "ERR mapping-not-allowed\n");
                return;
            }
            break :blk (self.allocator.dupe(u8, name) catch {
                cs.sendResponse(fd, "ERR oom\n");
                return;
            });
        } else self.lookupSwitchMappingPath(name) catch {
            cs.sendResponse(fd, "ERR mapping-lookup-failed\n");
            return;
        };
        if (path == null) {
            cs.sendResponse(fd, "ERR mapping-not-found\n");
            return;
        }
        defer self.allocator.free(path.?);

        var targets = std.ArrayList(usize){};
        defer targets.deinit(self.allocator);

        if (device_id) |dev_id| {
            var found = false;
            for (self.managed.items, 0..) |*m, idx| {
                if (m.devname) |dn| {
                    if (std.mem.eql(u8, dn, dev_id)) {
                        targets.append(self.allocator, idx) catch {
                            cs.sendResponse(fd, "ERR oom\n");
                            return;
                        };
                        found = true;
                        break;
                    }
                }
            }
            if (!found) {
                cs.sendResponse(fd, "ERR device-not-found\n");
                return;
            }
        } else {
            for (self.managed.items, 0..) |*m, idx| {
                if (m.suspended) continue;
                targets.append(self.allocator, idx) catch {
                    cs.sendResponse(fd, "ERR oom\n");
                    return;
                };
            }
        }

        var txs = std.ArrayList(SwitchTx){};
        defer {
            self.cleanupSwitchTxs(txs.items);
            txs.deinit(self.allocator);
        }

        const resolved_stem = self.allocator.dupe(u8, std.fs.path.stem(path.?)) catch {
            cs.sendResponse(fd, "ERR oom\n");
            return;
        };
        defer self.allocator.free(resolved_stem);

        for (targets.items) |idx| {
            const m = &self.managed.items[idx];
            const parsed = mapping_cfg.parseFile(self.allocator, path.?) catch {
                cs.sendResponse(fd, "ERR mapping-parse-failed\n");
                return;
            };
            const parsed_ptr = self.allocator.create(mapping_cfg.ParseResult) catch {
                parsed.deinit();
                cs.sendResponse(fd, "ERR oom\n");
                return;
            };
            parsed_ptr.* = parsed;
            var new_mapper = Mapper.init(&parsed_ptr.value, m.instance.loop.macro_timer_fd, self.allocator) catch {
                parsed_ptr.deinit();
                self.allocator.destroy(parsed_ptr);
                cs.sendResponse(fd, "ERR switch-failed\n");
                return;
            };
            const stem_copy = self.allocator.dupe(u8, resolved_stem) catch {
                new_mapper.deinit();
                parsed_ptr.deinit();
                self.allocator.destroy(parsed_ptr);
                cs.sendResponse(fd, "ERR oom\n");
                return;
            };
            txs.append(self.allocator, .{
                .idx = idx,
                .new_mapper = new_mapper,
                .parsed_ptr = parsed_ptr,
                .path_stem = stem_copy,
            }) catch {
                self.allocator.free(stem_copy);
                new_mapper.deinit();
                parsed_ptr.deinit();
                self.allocator.destroy(parsed_ptr);
                cs.sendResponse(fd, "ERR oom\n");
                return;
            };
        }

        for (txs.items, 0..) |*tx, commit_idx| {
            if (shouldInjectSwitchFailure(self, commit_idx)) {
                self.rollbackCommittedSwitches(txs.items);
                cs.sendResponse(fd, "ERR switch-failed\n");
                return;
            }

            self.commitSwitchTarget(tx) catch {
                self.rollbackCommittedSwitches(txs.items);
                cs.sendResponse(fd, "ERR switch-failed\n");
                return;
            };
        }

        std.log.info("mapping switched: \"{s}\" ({d} device(s))", .{ name, targets.items.len });

        var resp_buf: [128]u8 = undefined;
        if (device_id) |dev_id| {
            const resp = std.fmt.bufPrint(&resp_buf, "OK {s} {s}\n", .{ name, dev_id }) catch {
                cs.sendResponse(fd, "OK\n");
                return;
            };
            cs.sendResponse(fd, resp);
        } else {
            const resp = std.fmt.bufPrint(&resp_buf, "OK {s}\n", .{name}) catch {
                cs.sendResponse(fd, "OK\n");
                return;
            };
            cs.sendResponse(fd, resp);
        }
    }

    /// Walk sysfs_root/event*/device/id/{vendor,product} to find all eventN
    /// nodes matching vid:pid. Appends each as "/dev/input/eventN(name)" into
    /// `out`, comma-separated. Returns a slice into `out` on any match, null
    /// when the directory is unavailable or no match is found. Truncates with
    /// "..." when the buffer is full.
    fn resolveEvdevNodesAt(sysfs_root: []const u8, vid: u16, pid: u16, out: []u8) ?[]u8 {
        var dir = std.fs.openDirAbsolute(sysfs_root, .{ .iterate = true }) catch return null;
        defer dir.close();
        var it = dir.iterate();
        var written: usize = 0;
        while (it.next() catch null) |entry| {
            if (!std.mem.startsWith(u8, entry.name, "event")) continue;
            var path_buf: [256]u8 = undefined;
            var nbuf: [8]u8 = undefined;
            const vpath = std.fmt.bufPrint(&path_buf, "{s}/device/id/vendor", .{entry.name}) catch continue;
            const vf = dir.openFile(vpath, .{}) catch continue;
            const vn = vf.read(&nbuf) catch {
                vf.close();
                continue;
            };
            vf.close();
            const ev = std.fmt.parseInt(u16, std.mem.trimRight(u8, nbuf[0..vn], "\n\r "), 16) catch continue;
            if (ev != vid) continue;
            const ppath = std.fmt.bufPrint(&path_buf, "{s}/device/id/product", .{entry.name}) catch continue;
            const pf = dir.openFile(ppath, .{}) catch continue;
            const pn = pf.read(&nbuf) catch {
                pf.close();
                continue;
            };
            pf.close();
            const ep = std.fmt.parseInt(u16, std.mem.trimRight(u8, nbuf[0..pn], "\n\r "), 16) catch continue;
            if (ep != pid) continue;
            // Read kernel device name for disambiguation.
            var dev_name_buf: [64]u8 = undefined;
            const npath = std.fmt.bufPrint(&path_buf, "{s}/device/name", .{entry.name}) catch continue;
            const dev_name: []const u8 = blk: {
                const nf = dir.openFile(npath, .{}) catch break :blk "";
                const nn = nf.read(&dev_name_buf) catch {
                    nf.close();
                    break :blk "";
                };
                nf.close();
                break :blk std.mem.trimRight(u8, dev_name_buf[0..nn], "\n\r ");
            };
            const sep: []const u8 = if (written > 0) "," else "";
            const segment = if (dev_name.len > 0)
                std.fmt.bufPrint(out[written..], "{s}/dev/input/{s}({s})", .{ sep, entry.name, dev_name })
            else
                std.fmt.bufPrint(out[written..], "{s}/dev/input/{s}", .{ sep, entry.name });
            if (segment) |s| {
                written += s.len;
            } else |_| {
                // Buffer full — append truncation marker if space allows.
                const marker = "...";
                if (written + marker.len <= out.len) {
                    @memcpy(out[written..][0..marker.len], marker);
                    written += marker.len;
                }
                break;
            }
        }
        return if (written > 0) out[0..written] else null;
    }

    fn resolveEvdevNode(vid: u16, pid: u16, out: []u8) ?[]u8 {
        return resolveEvdevNodesAt("/sys/class/input", vid, pid, out);
    }

    /// Format the `dyn_bind` line fragment appended to STATUS for one device.
    /// Pulled out so the format can be snapshot-tested without spinning up a
    /// full Supervisor + Mapper. Writes nothing when no `[dynamic_bind]` is
    /// configured for that device's mapping.
    pub fn formatDynamicBindStatus(
        writer: anytype,
        target_layer: []const u8,
        static_name: []const u8,
        runtime_name: []const u8,
    ) !void {
        try writer.print(" dyn_bind={s} static={s} runtime={s}", .{ target_layer, static_name, runtime_name });
    }

    pub fn handleStatus(self: *Supervisor, fd: posix.fd_t) void {
        var cs = &self.ctrl_sock.?;
        var line: std.ArrayList(u8) = .{};
        defer line.deinit(self.allocator);
        self.buildStatusLine(&line) catch {
            cs.sendResponse(fd, "STATUS\n");
            return;
        };
        cs.sendResponse(fd, line.items);
    }

    // STATUS is a single newline-terminated line whose length grows with the
    // managed-device count and with device/mapping name lengths. A fixed buffer
    // truncated the tail fields (shadow_grabs/shadow_nodes appended last) once
    // enough long-named devices were attached, so the line is built into a
    // heap buffer that grows to fit the full reply.
    fn buildStatusLine(self: *Supervisor, line: *std.ArrayList(u8)) !void {
        const a = self.allocator;
        const w = line.writer(a);
        const now_ns = self.nowNs();

        try w.writeAll("STATUS");
        for (self.managed.items) |*m| {
            const name = m.instance.device_cfg.device.name;
            const state_str: []const u8 = if (m.suspended) "suspended" else "active";
            const mapping_pr: ?*const mapping_mod.MappingConfig = blk: {
                if (m.switch_mapping) |sm| break :blk &sm.value;
                if (m.default_mapping_pr) |dm| break :blk &dm.value;
                break :blk null;
            };
            const mapping_name: []const u8 = blk: {
                if (m.switch_mapping) |sm| {
                    if (sm.value.name) |n| break :blk n;
                    if (m.switch_mapping_stem) |s| break :blk s;
                }
                if (m.default_mapping_pr) |dm| {
                    if (dm.value.name) |n| break :blk n;
                    if (m.default_mapping_stem) |s| break :blk s;
                }
                break :blk "(none)";
            };
            try w.print(" device={s} state={s} mapping={s}", .{ name, state_str, mapping_name });

            // Diagnostic fields (issue #236).
            const vid: u16 = @intCast(m.instance.device_cfg.device.vid);
            const pid: u16 = @intCast(m.instance.device_cfg.device.pid);
            const output_kind: []const u8 = switch (m.instance.owner) {
                .none => "none",
                .uinput => "uinput",
                .uhid => "uhid",
            };
            const output_fd_alive: bool = m.instance.owner != .none;
            try w.print(" phys_key={s} vid=0x{x:0>4} pid=0x{x:0>4} output_kind={s} output_fd_alive={}", .{
                m.phys_key, vid, pid, output_kind, output_fd_alive,
            });

            if (m.grace_deadline_ns) |dl| {
                const remaining_ms: u64 = if (dl > now_ns) (dl - now_ns) / std.time.ns_per_ms else 0;
                try w.print(" grace_deadline_remaining_ms={d}", .{remaining_ms});
            }

            var evdev_buf: [256]u8 = undefined;
            const evdev_node = resolveEvdevNode(vid, pid, &evdev_buf) orelse "<unresolved>";
            try w.print(" evdev_node={s}", .{evdev_node});

            try w.print(" hotplug_pending={d}", .{self.hotplug_pending.items.len});

            // write_in_flight_ms=0 when no write is currently blocked; a sustained
            // non-zero value (hundreds of ms) is the smoking gun for a kernel-side
            // D-state hang on usb_control_msg.
            const inb = m.instance.wedge.loadInbound();
            const outb = m.instance.wedge.loadOutbound();
            const ifs = m.instance.wedge.loadInFlight();
            const inb_ago_ms: u64 = if (inb == 0 or now_ns < inb) 0 else (now_ns - inb) / std.time.ns_per_ms;
            const outb_ago_ms: u64 = if (outb == 0 or now_ns < outb) 0 else (now_ns - outb) / std.time.ns_per_ms;
            const inflight_ms: u64 = if (ifs == 0 or now_ns < ifs) 0 else (now_ns - ifs) / std.time.ns_per_ms;
            try w.print(" last_inbound_ms_ago={d} last_outbound_ms_ago={d} write_in_flight_ms={d}", .{
                inb_ago_ms, outb_ago_ms, inflight_ms,
            });

            try m.shadow_grabs.appendStatusFields(w);

            // Dynamic-binding status. Shows the target layer plus its static
            // trigger (when any) and the currently-bound runtime trigger
            // (when any). Omitted entirely when [dynamic_bind] is not
            // configured for this mapping.
            const mp = mapping_pr orelse continue;
            const dbc = mp.dynamic_bind orelse continue;
            const static_name: []const u8 = blk: {
                const layers = mp.layer orelse break :blk "-";
                for (layers) |lc| {
                    if (std.mem.eql(u8, lc.name, dbc.target_layer)) {
                        break :blk lc.trigger orelse "-";
                    }
                }
                break :blk "-";
            };
            const runtime_name: []const u8 = blk: {
                const mapper = m.instance.mapper orelse break :blk "-";
                const dyn = mapper.dynamic_bind orelse break :blk "-";
                const btn = dyn.getRuntimeTrigger() orelse break :blk "-";
                break :blk @tagName(btn);
            };
            formatDynamicBindStatus(w, dbc.target_layer, static_name, runtime_name) catch break;
        }
        try w.writeByte('\n');
    }

    fn handleList(self: *Supervisor, fd: posix.fd_t) void {
        const profiles = mapping_discovery.discoverMappings(self.allocator) catch {
            self.ctrl_sock.?.sendResponse(fd, "ERR list-failed\n");
            return;
        };
        defer mapping_discovery.freeProfiles(self.allocator, profiles);

        var resp_buf: [1024]u8 = undefined;
        var fbs = std.io.fixedBufferStream(&resp_buf);
        const w = fbs.writer();
        w.writeAll("LIST") catch {
            self.ctrl_sock.?.sendResponse(fd, "LIST\n");
            return;
        };
        for (profiles) |p| {
            w.print(" {s}", .{p.name}) catch break;
        }
        w.writeByte('\n') catch {
            self.ctrl_sock.?.sendResponse(fd, "LIST\n");
            return;
        };
        self.ctrl_sock.?.sendResponse(fd, fbs.getWritten());
    }

    fn handleDevices(self: *Supervisor, fd: posix.fd_t) void {
        var cs = &self.ctrl_sock.?;
        var resp_buf: [512]u8 = undefined;
        var pos: usize = 0;

        const header = "DEVICES";
        @memcpy(resp_buf[pos .. pos + header.len], header);
        pos += header.len;

        var dit = self.devname_map.keyIterator();
        while (dit.next()) |key| {
            if (pos + 2 >= resp_buf.len) break;
            resp_buf[pos] = ' ';
            pos += 1;
            const k = key.*;
            const copy_len = @min(k.len, resp_buf.len - pos - 2);
            @memcpy(resp_buf[pos .. pos + copy_len], k[0..copy_len]);
            pos += copy_len;
        }
        resp_buf[pos] = '\n';
        pos += 1;
        cs.sendResponse(fd, resp_buf[0..pos]);
    }

    fn handleDumpStatus(self: *Supervisor, fd: posix.fd_t) void {
        const padctl_log = @import("log.zig");
        var cs = &self.ctrl_sock.?;
        var resp_buf: [64]u8 = undefined;
        const state_str: []const u8 = if (padctl_log.isEnabled()) "on" else "off";
        const resp = std.fmt.bufPrint(&resp_buf, "OK dump={s}\n", .{state_str}) catch return;
        cs.sendResponse(fd, resp);
    }

    fn handleDump(self: *Supervisor, fd: posix.fd_t, enable: bool) void {
        const padctl_log = @import("log.zig");
        padctl_log.setEnabled(enable);
        var cs = &self.ctrl_sock.?;
        if (enable) {
            cs.sendResponse(fd, "OK dump=on\n");
            std.log.info("dump logging enabled via IPC", .{});
        } else {
            cs.sendResponse(fd, "OK dump=off\n");
            std.log.info("dump logging disabled via IPC", .{});
        }
    }

    /// Look up the user config's default_mapping for device_name, find and parse the mapping file.
    /// Caller owns the returned ParseResult (call deinit on it). Returns null if none configured.
    const DefaultMapping = struct { result: mapping_cfg.ParseResult, stem: []u8 };

    fn loadUserDefaultMapping(self: *Supervisor, device_name: []const u8) ?DefaultMapping {
        const ucfg = &(self.user_cfg orelse return null);
        const mapping_name = user_config_mod.findDefaultMapping(ucfg, device_name) orelse return null;
        const path = blk: {
            if (builtin.is_test) {
                if (self.test_default_mapping_override) |override_path| {
                    break :blk self.allocator.dupe(u8, override_path) catch return null;
                }
            }
            break :blk (mapping_discovery.findMapping(self.allocator, mapping_name) catch return null) orelse {
                std.log.warn("mapping file \"{s}\" not found in XDG paths", .{mapping_name});
                return null;
            };
        };
        defer self.allocator.free(path);
        const result = mapping_cfg.parseFile(self.allocator, path) catch |err| {
            std.log.warn("skipping default mapping {s}: {}", .{ path, err });
            self.recordReloadFailure(path, err);
            return null;
        };
        const stem = self.allocator.dupe(u8, std.fs.path.stem(path)) catch {
            var r = result;
            r.deinit();
            return null;
        };
        std.log.info("mapping discovery: device \"{s}\" mapping \"{s}\" from \"{s}\"", .{ device_name, mapping_name, path });
        return .{ .result = result, .stem = stem };
    }

    const DefaultMappingPtr = struct { pr: *mapping_cfg.ParseResult, stem: []u8 };

    /// Resolve the user-configured default mapping for `device_name` into a
    /// heap-owned ParseResult plus the file stem (used by `handleStatus` when
    /// the mapping file omits a `name =` field). Returns null
    /// when no default mapping is configured or it fails to load/allocate;
    /// on null nothing is leaked. Ownership transfers to the caller.
    fn buildDefaultMapping(self: *Supervisor, device_name: []const u8) ?DefaultMappingPtr {
        const dm = self.loadUserDefaultMapping(device_name) orelse return null;
        const p = self.allocator.create(mapping_cfg.ParseResult) catch {
            var r = dm.result;
            r.deinit();
            self.allocator.free(dm.stem);
            return null;
        };
        p.* = dm.result;
        return .{ .pr = p, .stem = dm.stem };
    }

    fn applyUserOutputProfile(self: *Supervisor, cfg: *DeviceConfig) void {
        const ucfg = &(self.user_cfg orelse return);
        const profile_name = user_config_mod.findOutputProfile(ucfg, cfg.device.name) orelse return;
        if (config_device.selectOutputProfile(cfg, profile_name)) {
            std.log.info("output profile: device \"{s}\" profile \"{s}\"", .{ cfg.device.name, profile_name });
        } else {
            std.log.warn("output profile \"{s}\" for device \"{s}\" not found; using default output", .{ profile_name, cfg.device.name });
        }
    }

    /// Glob *.toml in dir_path, discover devices by VID/PID, dedup by physical path, spawn threads.
    pub fn startFromDir(self: *Supervisor, dir_path: []const u8) !void {
        return self.startFromDirWithRoot(dir_path, "/dev");
    }

    pub fn startFromDirWithRoot(self: *Supervisor, dir_path: []const u8, dev_root: []const u8) !void {
        var dir = try std.fs.cwd().openDir(dir_path, .{ .iterate = true });
        defer dir.close();

        // seen deduplicates by physical path across all TOML files; owns the key bytes.
        var seen = std.StringHashMap(void).init(self.allocator);
        defer {
            var kit = seen.keyIterator();
            while (kit.next()) |k| self.allocator.free(k.*);
            seen.deinit();
        }

        var walker = try dir.walk(self.allocator);
        defer walker.deinit();
        while (try walker.next()) |entry| {
            if (entry.kind != .file) continue;
            if (!std.mem.endsWith(u8, entry.basename, ".toml")) continue;

            var path_buf: [std.fs.max_path_bytes]u8 = undefined;
            const toml_path = try std.fmt.bufPrint(&path_buf, "{s}/{s}", .{ dir_path, entry.path });

            const parsed = config_device.parseFile(self.allocator, toml_path) catch |err| {
                std.log.warn("skipping device config {s}: {}", .{ toml_path, err });
                self.recordReloadFailure(toml_path, err);
                continue;
            };
            const cfg_ptr = try self.allocator.create(config_device.ParseResult);
            cfg_ptr.* = parsed;
            self.applyUserOutputProfile(&cfg_ptr.value);

            const vid: u16 = @intCast(cfg_ptr.value.device.vid);
            const pid: u16 = @intCast(cfg_ptr.value.device.pid);

            const paths = HidrawDevice.discoverAllWithRoot(self.allocator, vid, pid, dev_root) catch |err| {
                std.log.warn("discoverAll for {s}: {}", .{ entry.path, err });
                cfg_ptr.deinit();
                self.allocator.destroy(cfg_ptr);
                continue;
            };
            defer {
                for (paths) |p| self.allocator.free(p);
                self.allocator.free(paths);
            }

            if (paths.len == 0) {
                std.log.debug("config loaded for {s} (VID={x:0>4} PID={x:0>4}), no device online", .{
                    cfg_ptr.value.device.name, vid, pid,
                });
                self.enqueueColdScanRetriesForDevRoot(dev_root);
                try self.configs.append(self.allocator, cfg_ptr);
                continue;
            }

            const cfg_ifaces = cfg_ptr.value.device.interface;

            var spawned: usize = 0;
            for (paths) |hidraw_path| {
                const iface_id = readInterfaceId(hidraw_path) orelse {
                    std.log.debug("cold scan: {s} sysfs not ready, will retry via hotplug path", .{hidraw_path});
                    self.enqueueHotplugRetryForPath(hidraw_path);
                    continue;
                };
                {
                    var declared = false;
                    for (cfg_ifaces) |ci| {
                        if (iface_id == @as(u8, @intCast(ci.id))) {
                            declared = true;
                            break;
                        }
                    }
                    if (!declared) continue;
                }

                const phys = readPhysicalPath(self.allocator, hidraw_path) catch |err| {
                    std.log.warn("readPhysicalPath {s}: {}", .{ hidraw_path, err });
                    continue;
                };

                // Check already-managed instances across dirs before local seen map
                const already_managed = for (self.managed.items) |m| {
                    if (std.mem.eql(u8, m.phys_key, phys)) break true;
                } else false;
                if (already_managed) {
                    self.allocator.free(phys);
                    continue;
                }

                const gop = try seen.getOrPut(phys);
                if (gop.found_existing) {
                    self.allocator.free(phys);
                    continue;
                }
                // seen now owns phys bytes via the key slot

                const default_dm = self.buildDefaultMapping(cfg_ptr.value.device.name);
                const default_pr_ptr: ?*mapping_cfg.ParseResult = if (default_dm) |d| d.pr else null;
                const default_stem: ?[]u8 = if (default_dm) |d| d.stem else null;
                const init_mapping: ?*const MappingConfig = if (default_pr_ptr) |p| &p.value else null;

                const inst_ptr = try self.allocator.create(DeviceInstance);
                inst_ptr.* = DeviceInstance.init(self.allocator, &cfg_ptr.value, init_mapping, phys, &self.daemon_uniq_counter, .{}) catch |err| {
                    logBindFailure(&cfg_ptr.value, hidraw_path, err);
                    if (isTransientOpenError(err)) self.enqueueHotplugRetryForPath(hidraw_path);
                    self.allocator.destroy(inst_ptr);
                    if (default_pr_ptr) |p| {
                        p.deinit();
                        self.allocator.destroy(p);
                    }
                    if (default_stem) |s| self.allocator.free(s);
                    // reclaim phys from seen
                    _ = seen.remove(phys);
                    self.allocator.free(phys);
                    continue;
                };
                self.spawnInstance(phys, inst_ptr, default_pr_ptr) catch |err| {
                    std.log.warn("spawnInstance for {s}: {}", .{ hidraw_path, err });
                    inst_ptr.deinit();
                    self.allocator.destroy(inst_ptr);
                    if (default_pr_ptr) |p| {
                        p.deinit();
                        self.allocator.destroy(p);
                    }
                    if (default_stem) |s| self.allocator.free(s);
                    _ = seen.remove(phys);
                    self.allocator.free(phys);
                    continue;
                };
                self.managed.items[self.managed.items.len - 1].default_mapping_stem = default_stem;
                // Register devname → phys so detach() can find this instance.
                const devname = std.fs.path.basename(hidraw_path);
                self.bindManagedDevname(&self.managed.items[self.managed.items.len - 1], devname, phys) catch |err|
                    std.log.warn("bindManagedDevname failed for {s}: {s}", .{ phys, @errorName(err) });
                // phys stays in seen (owned there) and also duped by spawnInstance for ManagedInstance.
                spawned += 1;
            }

            try self.configs.append(self.allocator, cfg_ptr);
            if (spawned == 0) {
                std.log.debug("config loaded for {s} (VID={x:0>4} PID={x:0>4}), no device online", .{
                    cfg_ptr.value.device.name, vid, pid,
                });
            }
        }
    }

    pub fn joinAll(self: *Supervisor) void {
        for (self.managed.items) |*m| m.thread.join();
    }

    pub fn startFromDirs(self: *Supervisor, dirs: []const []const u8) void {
        for (dirs) |dir| {
            std.fs.accessAbsolute(dir, .{}) catch |err| {
                std.log.warn("skipping config dir '{s}': {}", .{ dir, err });
                continue;
            };
            std.log.info("scanning config dir '{s}'", .{dir});
            self.startFromDir(dir) catch |err| {
                std.log.warn("failed to scan config dir '{s}': {}", .{ dir, err });
            };
        }
    }

    /// Test-only seam: like startFromDirs but uses caller-provided dev_root
    /// instead of /dev. Allows tests to bypass real /dev/hidraw* enumeration
    /// (which can hang on leaked virtual UHID devices on dev machines).
    pub fn startFromDirsWithRoot(self: *Supervisor, dirs: []const []const u8, dev_root: []const u8) !void {
        for (dirs) |dir| {
            try self.startFromDirWithRoot(dir, dev_root);
        }
    }

    /// Drops suspended instances; suspended-preservation is the per-phys-key
    /// SIGHUP path via `reload`, not the rescan path.
    fn doReloadFromDir(self: *Supervisor, dir_path: []const u8) void {
        self.reloadUserConfig();
        self.clearAllManagedAndConfigs();
        self.startFromDir(dir_path) catch |err| {
            std.log.err("reload from dir failed: {}", .{err});
        };
    }

    /// Drops suspended instances; suspended-preservation is the per-phys-key
    /// SIGHUP path via `reload`, not the rescan path.
    fn doReloadFromDirs(self: *Supervisor, dirs: []const []const u8) void {
        self.reloadUserConfig();
        self.clearAllManagedAndConfigs();
        self.startFromDirs(dirs);
    }

    /// Like serve() but monitors/reloads from multiple config directories.
    /// Uses dirs[0] for inotify hot-file-change watch (user config dir).
    /// SIGHUP and inotify debounce reload all dirs.
    pub fn serveMulti(self: *Supervisor, dirs: []const []const u8) void {
        const Dispatch = struct {
            dirs: []const []const u8,

            fn reload(d: @This(), sup: *Supervisor) void {
                sup.doReloadFromDirs(d.dirs);
            }
        };
        self.serveLoop(Dispatch{ .dirs = dirs }, false) catch {};
    }

    /// Enter the supervisor event loop: poll for signals, netlink hot-plug,
    /// inotify config changes, and control-socket commands. Blocks until
    /// SIGTERM/SIGINT. When the loop exits, all managed instances are stopped.
    pub fn serve(self: *Supervisor, dir_path: []const u8) void {
        const Dispatch = struct {
            dir_path: []const u8,

            fn reload(d: @This(), sup: *Supervisor) void {
                sup.doReloadFromDir(d.dir_path);
            }
        };
        self.serveLoop(Dispatch{ .dir_path = dir_path }, false) catch {};
    }

    pub fn attach(self: *Supervisor, devname: []const u8) !void {
        return self.attachWithRoot(devname, "/dev");
    }

    /// Transient open() errors that should trigger hotplug retry.
    pub fn isTransientOpenError(err: anyerror) bool {
        return switch (err) {
            error.AccessDenied,
            error.PermissionDenied,
            error.DeviceBusy,
            error.Busy,
            error.ClaimFailed,
            error.FileNotFound,
            error.NoDevice,
            error.NotFound,
            error.Disconnected,
            error.InitFailed,
            error.Io,
            => true,
            else => false,
        };
    }

    fn isLibusbClaimError(err: anyerror) bool {
        return switch (err) {
            error.NotFound, error.Busy, error.ClaimFailed => true,
            else => false,
        };
    }

    /// A libusb-claimed device that fails to open/claim drops out silently
    /// (never enters `managed`, so `padctl status` shows nothing). Surface the
    /// likely cause and remedy instead, since it almost always means the raw
    /// USB device node is not accessible to the unprivileged daemon.
    fn logBindFailure(cfg: *const DeviceConfig, key: []const u8, err: anyerror) void {
        if (err == error.LibusbUnavailable) {
            std.log.warn(
                "device \"{s}\" requires libusb but this padctl build has no libusb support " ++
                    "(-Dlibusb=false); use an official release or rebuild with -Dlibusb=true",
                .{cfg.device.name},
            );
        } else if (err == error.LibusbInit) {
            std.log.warn("libusb context initialization failed ({})", .{err});
        } else if (config_device.usesLibusb(cfg) and isLibusbClaimError(err)) {
            std.log.warn(
                "cannot claim \"{s}\" via libusb ({}): the raw USB device node is not accessible. " ++
                    "Install the padctl udev rules and add your user to the 'input' group (or run the daemon privileged), then replug.",
                .{ cfg.device.name, err },
            );
        } else {
            std.log.warn("DeviceInstance.init for {s}: {}", .{ key, err });
        }
    }

    pub fn attachWithRoot(self: *Supervisor, devname: []const u8, dev_root: []const u8) !void {
        self.last_attach_node_gone = false;
        if (builtin.is_test) {
            if (self.test_attach_transient_remaining) |remaining| {
                if (remaining > 0) {
                    self.test_attach_transient_remaining = remaining - 1;
                    return error.HotplugTransient;
                }
                self.test_attach_transient_remaining = null;
                return;
            }
        }
        var path_buf: [128]u8 = undefined;
        const path = try std.fmt.bufPrint(&path_buf, "{s}/{s}", .{ dev_root, devname });

        const fd = posix.open(path, .{ .ACCMODE = .RDWR, .NONBLOCK = true }, 0) catch |err| {
            if (isTransientOpenError(err)) {
                const node_gone = switch (err) {
                    error.FileNotFound, error.NoDevice => true,
                    else => false,
                };
                self.last_attach_node_gone = node_gone;
                std.log.debug("hotplug: {s} not ready ({s}), will retry", .{ path, @errorName(err) });
                return error.HotplugTransient;
            }
            return err;
        };
        defer posix.close(fd);

        var info: ioctl.HidrawDevinfo = undefined;
        if (linux.ioctl(fd, ioctl.HIDIOCGRAWINFO, @intFromPtr(&info)) != 0) {
            std.log.debug("hotplug: {s} rawinfo unavailable, will retry", .{path});
            return error.HotplugTransient;
        }
        const vid: u16 = @bitCast(info.vendor);
        const pid: u16 = @bitCast(info.product);

        var cfg: ?*const DeviceConfig = null;
        for (self.configs.items) |pr| {
            if (@as(u16, @intCast(pr.value.device.vid)) == vid and
                @as(u16, @intCast(pr.value.device.pid)) == pid)
            {
                cfg = &pr.value;
                break;
            }
        }
        if (cfg == null) return;

        const iface_id = readInterfaceId(path) orelse {
            std.log.debug("hotplug: {s} sysfs not ready, will retry", .{path});
            return error.HotplugTransient;
        };
        const declared = for (cfg.?.device.interface) |ci| {
            if (iface_id == @as(u8, @intCast(ci.id))) break true;
        } else false;
        if (!declared) {
            std.log.debug("hotplug: {s} interface {} not in config, skipping", .{ path, iface_id });
            return;
        }

        const phys = try readPhysicalPath(self.allocator, path);
        defer self.allocator.free(phys);

        std.log.info("hotplug: {s} VID={x:0>4} PID={x:0>4} phys=\"{s}\" iface={d}", .{
            devname, vid, pid, phys, iface_id,
        });

        var opener_ctx: u8 = 0;
        if (try self.tryResumeSuspendedInstance(
            devname,
            phys,
            vid,
            pid,
            &opener_ctx,
            openDeviceWithRetryForRebind,
        )) return;

        if (self.hasLiveDuplicate(devname, phys, cfg.?)) return;

        const default_dm = self.buildDefaultMapping(cfg.?.device.name);
        const default_pr_ptr: ?*mapping_cfg.ParseResult = if (default_dm) |d| d.pr else null;
        const default_stem: ?[]u8 = if (default_dm) |d| d.stem else null;
        const init_mapping: ?*const MappingConfig = if (default_pr_ptr) |p| &p.value else null;

        const inst_ptr = try self.allocator.create(DeviceInstance);
        inst_ptr.* = DeviceInstance.init(self.allocator, cfg.?, init_mapping, phys, &self.daemon_uniq_counter, .{}) catch |err| {
            logBindFailure(cfg.?, path, err);
            self.allocator.destroy(inst_ptr);
            if (default_pr_ptr) |p| {
                p.deinit();
                self.allocator.destroy(p);
            }
            if (default_stem) |s| self.allocator.free(s);
            if (isTransientOpenError(err)) return error.HotplugTransient;
            return;
        };
        const attached = self.attachWithInstanceResult(devname, phys, inst_ptr, default_pr_ptr) catch |err| {
            inst_ptr.deinit();
            self.allocator.destroy(inst_ptr);
            if (default_pr_ptr) |p| {
                p.deinit();
                self.allocator.destroy(p);
            }
            if (default_stem) |s| self.allocator.free(s);
            return err;
        };
        if (!attached) {
            inst_ptr.deinit();
            self.allocator.destroy(inst_ptr);
            if (default_pr_ptr) |p| {
                p.deinit();
                self.allocator.destroy(p);
            }
            if (default_stem) |s| self.allocator.free(s);
            return;
        }
        if (default_stem) |s| {
            self.managed.items[self.managed.items.len - 1].default_mapping_stem = s;
        }
        std.log.info("device attached: \"{s}\" {s}/{s}", .{ cfg.?.device.name, dev_root, devname });
        // Log FF config for rumble diagnostics.
        if (cfg.?.output) |out| {
            if (out.force_feedback) |ff| {
                std.log.info("device FF config: type={s} max_effects={?d} auto_stop={}", .{
                    ff.type, ff.max_effects, ff.auto_stop,
                });
            }
        }
    }

    fn tryResumeSuspendedInstance(
        self: *Supervisor,
        devname: []const u8,
        phys: []const u8,
        vid: u16,
        pid: u16,
        opener_ctx: *anyopaque,
        opener: RebindDeviceOpener,
    ) !bool {
        for (self.managed.items) |*m| {
            if (!m.suspended) continue;
            const mcfg = m.instance.device_cfg;
            const mcfg_vid: u16 = @intCast(mcfg.device.vid);
            const mcfg_pid: u16 = @intCast(mcfg.device.pid);
            const phys_match = std.mem.eql(u8, m.phys_key, phys);
            const id_match = mcfg_vid == vid and mcfg_pid == pid;
            std.log.debug("hotplug: suspended candidate vid={x:0>4} pid={x:0>4} phys_key=\"{s}\" new_phys=\"{s}\" phys_match={} id_match={}", .{
                mcfg_vid, mcfg_pid, m.phys_key, phys, phys_match, id_match,
            });
            // Match by physical topology path (stable across sleep/wake)
            // plus VID:PID so a different controller plugged into the same
            // port starts a fresh instance instead of inheriting stale state.
            if (!phys_match or !id_match) continue;

            // Suppress interfaces stay claimed across suspend/resume (never
            // closed by closeDeviceIO), so only non-suppress interfaces are
            // reopened into the rebind array.
            const new_devices = try self.allocator.alloc(DeviceIO, config_device.openedInterfaceCount(mcfg));
            var opened: usize = 0;
            var new_devices_owned = true;
            errdefer {
                if (new_devices_owned) {
                    for (new_devices[0..opened]) |dev| dev.close();
                    self.allocator.free(new_devices);
                }
            }
            for (mcfg.device.interface) |iface| {
                if (config_device.isSuppressClass(iface.class)) continue;
                new_devices[opened] = opener(opener_ctx, self.allocator, iface, vid, pid) catch |err| {
                    std.log.warn("rebind: open interface {d} failed: {}", .{ iface.id, err });
                    for (new_devices[0..opened]) |dev| dev.close();
                    self.allocator.free(new_devices);
                    new_devices_owned = false;
                    if (isTransientOpenError(err)) return error.HotplugTransient;
                    std.log.warn("hotplug: suspended instance found but resume aborted: open failed", .{});
                    return true;
                };
                opened += 1;
            }

            try m.instance.rebindDeviceIO(new_devices);
            new_devices_owned = false;
            self.allocator.free(new_devices);
            rerunInitAfterRebind(m) catch |err| {
                m.instance.closeDeviceIO();
                std.log.warn("hotplug: suspended instance resume aborted: re-init failed: {}", .{err});
                if (isTransientOpenError(err)) return error.HotplugTransient;
                return true;
            };

            // Commit bookkeeping only after every fallible step succeeds. On
            // failure `m.suspended`/`m.grace_deadline_ns` stay as-is and
            // physical fds are closed so a later ADD can try a fresh rebind.
            self.finalizeRebind(m, devname, phys) catch |err| {
                m.instance.closeDeviceIO();
                std.log.warn("hotplug: suspended instance resume aborted: {}", .{err});
                if (err == error.OutOfMemory) return err;
                return true;
            };
            std.log.info("hotplug: resumed suspended instance (phys_key/VID/PID match) for {s}", .{devname});
            std.log.info("device resumed: \"{s}\" {s}", .{ mcfg.device.name, devname });
            return true;
        }

        return false;
    }
};

// --- tests ---

const testing = std.testing;
const mapping_mod = @import("config/mapping.zig");
const mapper_mod = @import("core/mapper.zig");
const device_mod = @import("config/device.zig");
const EventLoop = @import("event_loop.zig").EventLoop;
const MockDeviceIO = @import("test/mock_device_io.zig").MockDeviceIO;
const MockOutput = @import("test/mock_output.zig").MockOutput;
const uinput = @import("io/uinput.zig");
const state_mod = @import("core/state.zig");
const usbraw_mod = @import("io/usbraw.zig");

const minimal_device_toml =
    \\[device]
    \\name = "T"
    \\vid = 1
    \\pid = 2
    \\[[device.interface]]
    \\id = 0
    \\class = "hid"
    \\[[report]]
    \\name = "r"
    \\interface = 0
    \\size = 3
    \\[report.match]
    \\offset = 0
    \\expect = [0x01]
    \\[report.fields]
    \\left_x = { offset = 1, type = "i16le" }
;

// Vendor read interface + suppress claim-only interface (Vader-5 shape).
const libusb_device_toml =
    \\[device]
    \\name = "Libusb Pad"
    \\vid = 1
    \\pid = 2
    \\[[device.interface]]
    \\id = 1
    \\class = "vendor"
    \\ep_in = 0x82
    \\ep_out = 0x06
    \\[[device.interface]]
    \\id = 2
    \\class = "suppress"
    \\[[report]]
    \\name = "r"
    \\interface = 1
    \\size = 3
    \\[report.match]
    \\offset = 0
    \\expect = [0x01]
    \\[report.fields]
    \\left_x = { offset = 1, type = "i16le" }
;

const init_device_toml =
    \\[device]
    \\name = "InitDevice"
    \\vid = 1
    \\pid = 2
    \\[[device.interface]]
    \\id = 0
    \\class = "hid"
    \\[device.init]
    \\interface = 0
    \\commands = ["0101"]
    \\[[report]]
    \\name = "r"
    \\interface = 0
    \\size = 1
    \\[report.match]
    \\offset = 0
    \\expect = [0x01]
;

const strict_init_device_toml =
    \\[device]
    \\name = "StrictInitDevice"
    \\vid = 1
    \\pid = 2
    \\[[device.interface]]
    \\id = 0
    \\class = "hid"
    \\[device.init]
    \\interface = 0
    \\commands = ["0101"]
    \\response_prefix = [0x5a]
    \\require_response = true
    \\[[report]]
    \\name = "r"
    \\interface = 0
    \\size = 1
    \\[report.match]
    \\offset = 0
    \\expect = [0x01]
;

const feature_init_device_toml =
    \\[device]
    \\name = "FeatureInitDevice"
    \\vid = 1
    \\pid = 2
    \\[[device.interface]]
    \\id = 0
    \\class = "hid"
    \\[device.init]
    \\interface = 0
    \\feature_report = [0x81, 0, 0]
    \\[[report]]
    \\name = "r"
    \\interface = 0
    \\size = 1
    \\[report.match]
    \\offset = 0
    \\expect = [0x01]
;

const FailWriteDeviceIO = struct {
    pub fn deviceIO(self: *FailWriteDeviceIO) DeviceIO {
        return .{ .ptr = self, .vtable = &vtable };
    }

    const vtable = DeviceIO.VTable{
        .read = read,
        .write = write,
        .feature_report = featureReport,
        .pollfd = pollfd,
        .close = close,
    };

    fn read(_: *anyopaque, _: []u8) DeviceIO.ReadError!usize {
        return DeviceIO.ReadError.Again;
    }

    fn write(_: *anyopaque, _: []const u8) DeviceIO.WriteError!void {
        return DeviceIO.WriteError.Io;
    }

    fn featureReport(_: *anyopaque, _: []const u8) DeviceIO.WriteError!void {
        return DeviceIO.WriteError.Io;
    }

    fn pollfd(_: *anyopaque) posix.pollfd {
        return .{ .fd = -1, .events = 0, .revents = 0 };
    }

    fn close(_: *anyopaque) void {}
};

const TestRebindOpenCtx = struct {
    devices: []DeviceIO,
    next: usize = 0,
    fail: ?anyerror = null,

    fn open(
        ptr: *anyopaque,
        _: std.mem.Allocator,
        _: config_device.InterfaceConfig,
        _: u16,
        _: u16,
    ) !DeviceIO {
        const self: *TestRebindOpenCtx = @ptrCast(@alignCast(ptr));
        if (self.fail) |err| return err;
        if (self.next >= self.devices.len) return error.TestNoDevice;
        const dev = self.devices[self.next];
        self.next += 1;
        return dev;
    }
};

fn makeTestInstance(
    inst_alloc: std.mem.Allocator,
    mock: *MockDeviceIO,
    cfg: *const device_mod.DeviceConfig,
) !*DeviceInstance {
    const devices = try inst_alloc.alloc(DeviceIO, 1);
    devices[0] = mock.deviceIO();

    var loop = try EventLoop.initManaged();
    errdefer loop.deinit();
    try loop.addDevice(devices[0]);

    const inst = try inst_alloc.create(DeviceInstance);
    inst.* = .{
        .allocator = inst_alloc,
        .devices = devices,
        .loop = loop,
        .interp = @import("core/interpreter.zig").Interpreter.init(cfg),
        .mapper = null,
        .owner = .none,
        .primary_output = null,
        .imu_output = null,
        .aux_dev = null,
        .touchpad_dev = null,
        .generic_state = null,
        .generic_uinput = null,
        .device_cfg = cfg,
        .pending_mapping = null,
        .stopped = false,
        .poll_timeout_ms = 100,
    };
    return inst;
}

// Build a DeviceInstance with no read fd (all-suppress shape): devices[] is
// empty so managedInstanceAlive() takes its len==0 shortcut. The returned
// instance is fully deinit-safe (no outputs, no registered fds).
fn makeFdlessInstance(
    inst_alloc: std.mem.Allocator,
    cfg: *const device_mod.DeviceConfig,
) !*DeviceInstance {
    const devices = try inst_alloc.alloc(DeviceIO, 0);
    var loop = try EventLoop.initManaged();
    errdefer loop.deinit();

    const inst = try inst_alloc.create(DeviceInstance);
    inst.* = .{
        .allocator = inst_alloc,
        .devices = devices,
        .loop = loop,
        .interp = @import("core/interpreter.zig").Interpreter.init(cfg),
        .mapper = null,
        .owner = .none,
        .primary_output = null,
        .imu_output = null,
        .aux_dev = null,
        .touchpad_dev = null,
        .generic_state = null,
        .generic_uinput = null,
        .device_cfg = cfg,
        .pending_mapping = null,
        .stopped = true,
        .poll_timeout_ms = 100,
    };
    return inst;
}

// threadlocal: Zig test runner executes each test in its own OS thread from a pool.
// threadlocal gives each test thread an independent slot, preventing cross-test
// interference when tests run in parallel.  Limitation: tests that call reload()
// with testInitFn must set g_mock_slot on the same thread that reload() runs on,
// which holds because set and call happen sequentially within one test body.
threadlocal var g_mock_slot: ?*MockDeviceIO = null;

fn testInitFn(allocator: std.mem.Allocator, entry: ConfigEntry) anyerror!*DeviceInstance {
    const mock = g_mock_slot orelse return error.NoMockSlot;
    g_mock_slot = null;
    return makeTestInstance(allocator, mock, entry.device_cfg);
}

fn testSocketpair() ![2]posix.fd_t {
    var fds: [2]posix.fd_t = undefined;
    if (std.c.socketpair(posix.AF.UNIX, posix.SOCK.STREAM | posix.SOCK.CLOEXEC, 0, &fds) != 0) {
        return posix.unexpectedErrno(posix.errno(0));
    }
    return fds;
}

fn makeControlSocket(allocator: std.mem.Allocator, tmp_path: []const u8) !ControlSocket {
    var sock_path_buf: [256]u8 = undefined;
    const sock_path = try std.fmt.bufPrint(&sock_path_buf, "{s}/ctrl.sock", .{tmp_path});
    return ControlSocket.init(allocator, sock_path);
}

test "supervisor: cold-scan retry queues hidraw basename" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    sup.enqueueHotplugRetryForPath("/tmp/fake-dev/hidraw2");
    sup.enqueueHotplugRetryForPath("/another-root/hidraw2");

    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);
    const item = sup.hotplug_pending.items[0];
    try testing.expectEqualStrings("hidraw2", item.devname[0..item.len]);
}

test "supervisor: input grab retry dedups per kind and drains non-denied nodes" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    const base: u64 = 10 * std.time.ns_per_s;
    sup.test_now_override_ns = base;

    sup.enqueueRetry(.input_grab, "event5");
    sup.enqueueRetry(.input_grab, "event5");
    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);
    try testing.expectEqual(PendingKind.input_grab, sup.hotplug_pending.items[0].kind);

    // Same name under a different kind is a distinct entry.
    sup.enqueueRetry(.hidraw, "event5");
    try testing.expectEqual(@as(usize, 2), sup.hotplug_pending.items.len);
    _ = sup.hotplug_pending.swapRemove(1);

    // Advance past the input_grab deadline (300ms); node gone (or readable but
    // not a shadow): the retry entry is dropped.
    sup.test_now_override_ns = base + 300 * std.time.ns_per_ms;
    sup.drainHotplugRetry();
    try testing.expectEqual(@as(usize, 0), sup.hotplug_pending.items.len);
}

test "supervisor: sweep denied callback enqueues an input_grab retry" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    // The sweep wires this callback for every node it found but could not yet
    // open (access_denied), mirroring the netlink ADD retry path.
    sup.enqueueShadowRetry("event9");
    sup.enqueueShadowRetry("event9");
    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);
    try testing.expectEqual(PendingKind.input_grab, sup.hotplug_pending.items[0].kind);
    const item = sup.hotplug_pending.items[0];
    try testing.expectEqualStrings("event9", item.devname[0..item.len]);
}

test "supervisor: cold-scan retry is inert when retry timer is unavailable" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    sup.enqueueHotplugRetryForPath("/tmp/fake-dev/hidraw2");

    try testing.expectEqual(@as(usize, 0), sup.hotplug_pending.items.len);
}

test "supervisor: cold scan queues unidentified hidraw candidates for retry" {
    const allocator = testing.allocator;

    var cfg_dir = testing.tmpDir(.{});
    defer cfg_dir.cleanup();
    try cfg_dir.dir.writeFile(.{ .sub_path = "device.toml", .data = minimal_device_toml });
    const cfg_dir_path = try cfg_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(cfg_dir_path);

    var dev_dir = testing.tmpDir(.{});
    defer dev_dir.cleanup();
    try dev_dir.dir.writeFile(.{ .sub_path = "hidraw3", .data = "not a real hidraw node" });
    const dev_root = try dev_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(dev_root);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    try sup.startFromDirWithRoot(cfg_dir_path, dev_root);

    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    try testing.expectEqual(@as(usize, 1), sup.configs.items.len);
    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);
    const item = sup.hotplug_pending.items[0];
    try testing.expectEqualStrings("hidraw3", item.devname[0..item.len]);
}

test "supervisor: cold scan does not queue retry without hidraw candidates" {
    const allocator = testing.allocator;

    var cfg_dir = testing.tmpDir(.{});
    defer cfg_dir.cleanup();
    try cfg_dir.dir.writeFile(.{ .sub_path = "device.toml", .data = minimal_device_toml });
    const cfg_dir_path = try cfg_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(cfg_dir_path);

    var dev_dir = testing.tmpDir(.{});
    defer dev_dir.cleanup();
    const dev_root = try dev_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(dev_root);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    try sup.startFromDirWithRoot(cfg_dir_path, dev_root);

    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    try testing.expectEqual(@as(usize, 1), sup.configs.items.len);
    try testing.expectEqual(@as(usize, 0), sup.hotplug_pending.items.len);
}

// issue #397: a libusb claim race (error.Busy) or claim failure on a
// vendor-class wheel must be classified transient so cold scan/attach queues
// it for an event-driven retry instead of dropping it.
test "supervisor: libusb claim races are transient (issue #397)" {
    try testing.expect(Supervisor.isTransientOpenError(error.Busy));
    try testing.expect(Supervisor.isTransientOpenError(error.ClaimFailed));
    try testing.expect(Supervisor.isTransientOpenError(error.AccessDenied));
    try testing.expect(!Supervisor.isTransientOpenError(error.LibusbUnavailable));
    try testing.expect(!Supervisor.isTransientOpenError(error.LibusbInit));
}

test "supervisor: hotplug rawinfo failure is transient" {
    const allocator = testing.allocator;

    var dev_dir = testing.tmpDir(.{});
    defer dev_dir.cleanup();
    try dev_dir.dir.writeFile(.{ .sub_path = "hidraw7", .data = "not a real hidraw node" });
    const dev_root = try dev_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(dev_root);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    try testing.expectError(error.HotplugTransient, sup.attachWithRoot("hidraw7", dev_root));
}

const HOTPLUG_RETRY_GIVEUP_GUARD: usize = 100;

// Cumulative virtual deadline reached after `n` hidraw retries, i.e. the sum of
// the first `n` backoff slots. Lets a test advance the clock exactly to an
// entry's nth attempt without re-deriving the schedule by hand.
fn hidrawDeadlineAfter(n: usize) u64 {
    var total: u64 = 0;
    var i: usize = 0;
    while (i < n) : (i += 1) {
        total += HOTPLUG_RETRY_BACKOFF_MS[@min(i, HOTPLUG_RETRY_BACKOFF_MS.len - 1)];
    }
    return total * std.time.ns_per_ms;
}

// Advances virtual time to `at_ns` and drains once. drainHotplugRetry's
// NONBLOCK read no-ops on a disarmed fd, so the deadline gate is what decides
// which entries are attempted — exactly the production semantics.
fn drainAtVirtual(sup: *Supervisor, base_ns: u64, at_ns: u64) void {
    sup.test_now_override_ns = base_ns + at_ns;
    sup.drainHotplugRetry();
}

test "supervisor: hotplug retry rides out a multi-second transient (issue #431/#402 budget)" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    const base: u64 = 10 * std.time.ns_per_s;
    sup.test_now_override_ns = base;

    // (a) A device transient for 5 consecutive retries — past the old 3-retry
    // give-up cap — must survive and attach once the transient clears. Drive it
    // on its own backoff schedule via virtual time; a drain before the next
    // deadline must NOT consume a retry.
    sup.test_attach_transient_remaining = 5;
    sup.enqueueHotplugRetry("hidraw9");
    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);

    var attempt: usize = 0;
    while (sup.hotplug_pending.items.len > 0 and attempt < HOTPLUG_RETRY_BACKOFF_MS.len + 2) : (attempt += 1) {
        // A premature drain (1ns before the deadline) is a no-op: the retry
        // counter must not advance until the entry's own deadline arrives.
        const before = sup.hotplug_pending.items[0].retries;
        drainAtVirtual(&sup, base, hidrawDeadlineAfter(attempt + 1) - 1);
        if (sup.hotplug_pending.items.len == 0) break;
        try testing.expectEqual(before, sup.hotplug_pending.items[0].retries);
        // At the deadline the attempt fires.
        drainAtVirtual(&sup, base, hidrawDeadlineAfter(attempt + 1));
    }
    // Cleared after exactly 5 transient retries + 1 successful attach; the entry
    // is gone because it attached, not because the budget was exhausted.
    try testing.expectEqual(@as(usize, 0), sup.hotplug_pending.items.len);
    try testing.expectEqual(@as(?usize, null), sup.test_attach_transient_remaining);
    // 5 transient deadline drains + 1 successful attach drain == 6 iterations.
    try testing.expectEqual(@as(usize, 6), attempt);

    // (b) A permanently-transient device must still be given up within a bounded
    // number of retries (== the backoff array length), never looping forever.
    sup.test_now_override_ns = base;
    sup.test_attach_transient_remaining = std.math.maxInt(usize);
    sup.enqueueHotplugRetry("hidraw10");
    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);

    var drains: usize = 0;
    while (sup.hotplug_pending.items.len > 0 and drains < HOTPLUG_RETRY_GIVEUP_GUARD) {
        drains += 1;
        drainAtVirtual(&sup, base, hidrawDeadlineAfter(drains));
    }
    try testing.expectEqual(@as(usize, 0), sup.hotplug_pending.items.len);
    // Each deadline drain consumes exactly one retry; give-up lands at the
    // backoff array length, never looping forever.
    try testing.expectEqual(HOTPLUG_RETRY_BACKOFF_MS.len, drains);
}

// BUG 2 guard: a slow hidraw entry (seeded deep into the backoff at the 1500ms
// slot) co-pending with a genuinely faster entry (fresh, 300ms slot) must keep
// its OWN deadline. Under the old shared-min-delay / retry-all behaviour the slow
// entry was retried on every wake the fast entry triggered, collapsing its
// window. The per-entry deadline gate must hold it until its own deadline.
test "supervisor: per-entry deadline survives a faster co-pending entry (issue #431 BUG2)" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    const base: u64 = 10 * std.time.ns_per_s;
    sup.test_now_override_ns = base;

    const find = struct {
        fn p(s: *Supervisor, name: []const u8) ?*HotplugPending {
            for (s.hotplug_pending.items) |*it| {
                if (std.mem.eql(u8, it.devname[0..it.len], name)) return it;
            }
            return null;
        }
    }.p;

    // Both route through the shared transient seam so neither ever attaches.
    sup.test_attach_transient_remaining = std.math.maxInt(usize);
    sup.enqueueHotplugRetry("hidraw_fast"); // fresh -> 300ms deadline
    sup.enqueueHotplugRetry("hidraw_dut");

    // Seed the device-under-test deep into the backoff so it is genuinely slower
    // than the co-pending fast entry.
    const seed_slot: u8 = 5; // 1500ms
    {
        const d = find(&sup, "hidraw_dut").?;
        d.retries = seed_slot;
        d.next_deadline_ns = base + Supervisor.delayNsFor(.hidraw, seed_slot);
    }
    const dut_due_ms = HOTPLUG_RETRY_BACKOFF_MS[seed_slot]; // 1500

    // The fast entry is eligible every 300ms and is retried repeatedly, but the
    // DUT must hold its seeded retry count until its own 1500ms deadline.
    var t_ms: u64 = 100;
    while (t_ms < dut_due_ms) : (t_ms += 100) {
        sup.test_now_override_ns = base + t_ms * std.time.ns_per_ms;
        sup.drainHotplugRetry();
        try testing.expectEqual(seed_slot, find(&sup, "hidraw_dut").?.retries);
    }

    // At its own deadline the DUT advances by exactly one attempt — proof it was
    // never dragged forward by the faster co-pending entry.
    sup.test_now_override_ns = base + dut_due_ms * std.time.ns_per_ms;
    sup.drainHotplugRetry();
    try testing.expectEqual(@as(u8, seed_slot + 1), find(&sup, "hidraw_dut").?.retries);
}

// BUG 1 guard: armHotplugRetry must split a >=1000ms delay into sec+nsec. The
// old code wrote nsec = 1.5e9 (> 999_999_999), so timerfd_settime returned
// EINVAL and the timer was left DISARMED — gettime then reports it_value == 0
// and the high backoff slots never fire. Drive the real arm path and read the
// kernel timer back.
test "supervisor: armHotplugRetry produces a valid timer for high backoff slots (issue #431 BUG1)" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    const base: u64 = 10 * std.time.ns_per_s;
    sup.test_now_override_ns = base;

    // Place a single hidraw entry at retries=5 → the 1500ms slot, whose delay
    // exceeds the 999_999_999 ns timespec ceiling unless split correctly.
    sup.enqueueHotplugRetry("hidraw_slow");
    try testing.expectEqual(@as(usize, 1), sup.hotplug_pending.items.len);
    const p = &sup.hotplug_pending.items[0];
    p.retries = 5;
    p.next_deadline_ns = base + Supervisor.delayNsFor(.hidraw, p.retries);
    try testing.expectEqual(@as(u32, 1500), HOTPLUG_RETRY_BACKOFF_MS[p.retries]);

    // Disarm first so the only thing that can arm the timer is this call. Without
    // this, the valid 300ms timer set at enqueue would mask a rejected high-slot
    // settime and the buggy code would read back non-zero.
    const disarm = linux.itimerspec{ .it_value = .{ .sec = 0, .nsec = 0 }, .it_interval = .{ .sec = 0, .nsec = 0 } };
    _ = linux.timerfd_settime(sup.hotplug_retry_fd, .{}, &disarm, null);

    sup.armHotplugRetry();

    var cur: linux.itimerspec = undefined;
    const rc = linux.timerfd_gettime(sup.hotplug_retry_fd, &cur);
    try testing.expectEqual(@as(usize, 0), rc);
    // A correctly-armed 1500ms timer reports a non-zero remaining it_value. The
    // buggy sec=0,nsec=1.5e9 form is rejected (EINVAL), leaving the disarmed
    // timer at zero.
    try testing.expect(cur.it_value.sec > 0 or cur.it_value.nsec > 0);
}

test "supervisor: attachWithInstanceResult reports duplicate devname without taking instance" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst_a, null));

    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    defer {
        inst_b.deinit();
        allocator.destroy(inst_b);
    }

    try testing.expect(!try sup.attachWithInstanceResult("hidraw0", "phys1", inst_b, null));
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expectEqualStrings("phys0", sup.devname_map.get("hidraw0").?);
    try testing.expect(!sup.devname_map.contains("hidraw1"));
}

test "supervisor: attachWithInstanceResult reports live phys duplicate without taking instance" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst_a, null));

    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    defer {
        inst_b.deinit();
        allocator.destroy(inst_b);
    }

    try testing.expect(!try sup.attachWithInstanceResult("hidraw1", "phys0", inst_b, null));
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expectEqualStrings("phys0", sup.devname_map.get("hidraw0").?);
    try testing.expect(!sup.devname_map.contains("hidraw1"));
}

test "supervisor: hotplug duplicate precheck catches active devname and live phys" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst, null));

    try testing.expect(sup.hasLiveDuplicate("hidraw0", "phys1", &parsed_dev.value));
    try testing.expect(sup.hasLiveDuplicate("hidraw1", "phys0", &parsed_dev.value));
    try testing.expect(!sup.hasLiveDuplicate("hidraw1", "phys1", &parsed_dev.value));
}

test "supervisor: detach quiesces primary output before suspension" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const parsed_map = try mapping_mod.parseString(allocator,
        \\[[layer]]
        \\name = "held"
        \\trigger = "LT"
        \\activation = "hold"
    );
    defer parsed_map.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();
    var out = MockOutput.init(allocator);
    defer out.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    const held = state_mod.GamepadState{
        .ax = 77,
        .buttons = @as(u64, 1) << @intFromEnum(state_mod.ButtonId.A),
    };
    inst.primary_output = out.outputDevice();
    inst.loop.gamepad_state = held;
    out.prev = held;
    inst.mapper = try mapper_mod.Mapper.init(&parsed_map.value, inst.loop.macro_timer_fd, allocator);
    inst.mapping_cfg = &parsed_map.value;
    _ = inst.mapper.?.layer.onTriggerPress("held", 200, 1_000);
    inst.mapper.?.state.buttons = @as(u64, 1) << @intFromEnum(state_mod.ButtonId.LT);

    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst, null));
    sup.detach("hidraw0");

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(@as(usize, 1), out.emitted.items.len);
    try testing.expect(std.meta.eql(state_mod.GamepadState{}, out.emitted.items[0]));
    try testing.expect(std.meta.eql(state_mod.GamepadState{}, inst.loop.gamepad_state));
    try testing.expect(inst.mapper.?.layer.tap_hold == null);
    try testing.expect(std.meta.eql(state_mod.GamepadState{}, inst.mapper.?.state));
}

test "supervisor: instanceHoldsLibusb true for vendor interface, false for pure hid" {
    const allocator = testing.allocator;

    const libusb_dev = try device_mod.parseString(allocator, libusb_device_toml);
    defer libusb_dev.deinit();
    const hid_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer hid_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst_libusb = try makeTestInstance(allocator, &mock_a, &libusb_dev.value);
    const inst_hid = try makeTestInstance(allocator, &mock_b, &hid_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys-libusb", inst_libusb, null));
    try testing.expect(try sup.attachWithInstanceResult("hidraw1", "phys-hid", inst_hid, null));

    try testing.expectEqual(@as(usize, 2), sup.managed.items.len);
    for (sup.managed.items) |*m| {
        if (std.mem.eql(u8, m.phys_key, "phys-libusb")) {
            try testing.expect(Supervisor.instanceHoldsLibusb(m));
        } else {
            try testing.expect(!Supervisor.instanceHoldsLibusb(m));
        }
    }
}

test "supervisor: detach on libusb instance does not suspend and keeps devname binding" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, libusb_device_toml);
    defer parsed_dev.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst, null));

    sup.detach("hidraw0");

    // The hidraw REMOVE that detach() saw is padctl's own claim deleting the
    // node — the instance must stay live and addressable.
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(!sup.managed.items[0].suspended);
    try testing.expect(sup.managed.items[0].grace_deadline_ns == null);
    try testing.expect(sup.devname_map.contains("hidraw0"));
    try testing.expect(sup.managed.items[0].devname != null);
}

test "supervisor: detach on pure-hid instance still suspends (no regression)" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst, null));

    sup.detach("hidraw0");

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expect(!sup.devname_map.contains("hidraw0"));
}

test "supervisor: liveness sweep tears down libusb instance whose pipe hung up" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, libusb_device_toml);
    defer parsed_dev.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys0", inst, null));
    sup.detach("hidraw0"); // libusb path: stays live, binding preserved
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);

    // Still alive while the pipe is open.
    sup.sweepLivenessLibusb();
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);

    // Close the write end → pollfd sees POLLHUP → managedInstanceAlive false.
    mock.closeWriteEnd();
    sup.sweepLivenessLibusb();
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    try testing.expect(!sup.devname_map.contains("hidraw0"));
}

// A libusb instance spawned without a devname (spawnInstance path, e.g. run()
// or doReload's found==null branch) must still be reaped by the sweep on
// unplug. Pre-fix `m.devname orelse continue` skipped it, leaking the worker
// thread and instance forever.
test "supervisor: liveness sweep tears down devname-null libusb instance" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, libusb_device_toml);
    defer parsed_dev.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try sup.spawnInstance("phys0", inst, null);
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].devname == null);

    // Still alive while the pipe is open.
    sup.sweepLivenessLibusb();
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);

    // POLLHUP → managedInstanceAlive false. Without the else-branch the sweep
    // hits `m.devname orelse continue` and leaves managed.items.len == 1.
    mock.closeWriteEnd();
    sup.sweepLivenessLibusb();
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
}

test "supervisor: liveness sweep spares an all-suppress instance with no read fd" {
    const allocator = testing.allocator;
    const parsed_dev = try device_mod.parseString(allocator, libusb_device_toml);
    defer parsed_dev.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const inst = try makeFdlessInstance(allocator, &parsed_dev.value);
    // attachWithInstanceResult registers a devname in devname_map; without it
    // detachFull is unreachable and the sweep could never tear the entry down,
    // which would make this test pass even without the guard under test.
    try testing.expect(try sup.attachWithInstanceResult("hidraw0", "phys-suppress", inst, null));
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.devname_map.contains("hidraw0"));

    // Mimic a claim-only instance: holds the device via libusb (suppress_devs
    // non-empty) but exposes no readable interface (devices[] empty). The
    // pointer is never dereferenced — only the slice length is read — so a
    // dummy is sufficient. Reset to empty before teardown so deinit does not
    // call close() on it.
    var dummy_suppress: *usbraw_mod.UsbrawSuppress = undefined;
    inst.suppress_devs = (&dummy_suppress)[0..1];
    defer sup.managed.items[0].instance.suppress_devs = &.{};

    try testing.expect(Supervisor.instanceHoldsLibusb(&sup.managed.items[0]));
    try testing.expect(!Supervisor.managedInstanceAlive(&sup.managed.items[0]));

    sup.sweepLivenessLibusb();
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.devname_map.contains("hidraw0"));
}

test "supervisor: formatDynamicBindStatus: both static and runtime present" {
    var buf: [128]u8 = undefined;
    var fbs = std.io.fixedBufferStream(&buf);
    try Supervisor.formatDynamicBindStatus(fbs.writer(), "aim", "LM", "RT");
    try testing.expectEqualStrings(" dyn_bind=aim static=LM runtime=RT", fbs.getWritten());
}

test "supervisor: formatDynamicBindStatus: static only, runtime not bound" {
    var buf: [128]u8 = undefined;
    var fbs = std.io.fixedBufferStream(&buf);
    try Supervisor.formatDynamicBindStatus(fbs.writer(), "aim", "LM", "-");
    try testing.expectEqualStrings(" dyn_bind=aim static=LM runtime=-", fbs.getWritten());
}

test "supervisor: formatDynamicBindStatus: dynamic-only layer (no static), runtime bound" {
    var buf: [128]u8 = undefined;
    var fbs = std.io.fixedBufferStream(&buf);
    try Supervisor.formatDynamicBindStatus(fbs.writer(), "aim", "-", "RT");
    try testing.expectEqualStrings(" dyn_bind=aim static=- runtime=RT", fbs.getWritten());
}

test "supervisor: formatDynamicBindStatus: neither static nor runtime (dead/unbound)" {
    var buf: [128]u8 = undefined;
    var fbs = std.io.fixedBufferStream(&buf);
    try Supervisor.formatDynamicBindStatus(fbs.writer(), "aim", "-", "-");
    try testing.expectEqualStrings(" dyn_bind=aim static=- runtime=-", fbs.getWritten());
}

test "supervisor: Supervisor: global SWITCH rolls back all devices on failure" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const old_parsed = try mapping_mod.parseString(allocator,
        \\[[layer]]
        \\name = "old"
        \\trigger = "LT"
        \\activation = "toggle"
    );
    defer old_parsed.deinit();

    const base_dir = "/tmp/padctl_supervisor_test_switch";
    std.fs.deleteTreeAbsolute(base_dir) catch {};
    try std.fs.makeDirAbsolute(base_dir);
    defer std.fs.deleteTreeAbsolute(base_dir) catch {};

    const mappings_dir = try std.fmt.allocPrint(allocator, "{s}/mappings", .{base_dir});
    defer allocator.free(mappings_dir);
    try std.fs.makeDirAbsolute(mappings_dir);
    const mapping_path = try std.fmt.allocPrint(allocator, "{s}/fps.toml", .{mappings_dir});
    defer allocator.free(mapping_path);
    {
        const f = try std.fs.createFileAbsolute(mapping_path, .{});
        f.close();
    }

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    inst_a.mapper = try mapper_mod.Mapper.init(&old_parsed.value, inst_a.loop.macro_timer_fd, allocator);
    inst_a.mapping_cfg = &old_parsed.value;
    try inst_a.mapper.?.layer.toggled.put("old", {});
    inst_a.mapper.?.state.buttons = @as(u64, 1) << @intFromEnum(state_mod.ButtonId.A);
    inst_a.loop.gamepad_state.buttons = @as(u64, 1) << @intFromEnum(state_mod.ButtonId.RB);
    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst_a, null);
    try sup.spawnInstance("usb-1-2", inst_b, null);

    sup.test_switch_mapping_override = try allocator.dupe(u8, mapping_path);
    sup.test_switch_fail_commit_index = 1;

    sup.handleSwitch(resp_fds[0], "fps", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("ERR switch-failed\n", resp_buf[0..n]);
    try testing.expectEqual(@as(usize, 2), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].instance.mapper != null);
    try testing.expect(sup.managed.items[1].instance.mapper == null);
    try testing.expectEqual(@as(?*const mapping_mod.MappingConfig, &old_parsed.value), sup.managed.items[0].instance.mapping_cfg);
    try testing.expect(sup.managed.items[1].instance.mapping_cfg == null);
    try testing.expectEqual(@as(usize, 0), sup.managed.items[0].instance.mapper.?.layer.toggled.count());
    try testing.expectEqual(@as(u64, @as(u64, 1) << @intFromEnum(state_mod.ButtonId.RB)), sup.managed.items[0].instance.mapper.?.state.buttons);
    try testing.expect(sup.managed.items[0].switch_mapping == null);
    try testing.expect(sup.managed.items[1].switch_mapping == null);
    try testing.expectEqual(false, @atomicLoad(bool, &sup.managed.items[0].instance.stopped, .acquire));
    try testing.expectEqual(false, @atomicLoad(bool, &sup.managed.items[1].instance.stopped, .acquire));
}

test "supervisor: global SWITCH none rolls back all devices on failure" {
    // Regression guard: before the transactional fix, handleSwitchNone cleared
    // each device's mapper in-place and returned ERR on the first restart
    // failure, leaving already-processed devices permanently mapper-less. The
    // fix mirrors handleSwitch: on commit failure every already-committed device
    // is rolled back to its previous mapper.
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const old_parsed = try mapping_mod.parseString(allocator,
        \\[[layer]]
        \\name = "old"
        \\trigger = "LT"
        \\activation = "toggle"
    );
    defer old_parsed.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    inst_a.mapper = try mapper_mod.Mapper.init(&old_parsed.value, inst_a.loop.macro_timer_fd, allocator);
    inst_a.mapping_cfg = &old_parsed.value;
    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    inst_b.mapper = try mapper_mod.Mapper.init(&old_parsed.value, inst_b.loop.macro_timer_fd, allocator);
    inst_b.mapping_cfg = &old_parsed.value;
    try sup.spawnInstance("usb-1-1", inst_a, null);
    try sup.spawnInstance("usb-1-2", inst_b, null);

    sup.test_switch_fail_commit_index = 1;

    sup.handleSwitch(resp_fds[0], "none", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("ERR restart-failed\n", resp_buf[0..n]);
    try testing.expectEqual(@as(usize, 2), sup.managed.items.len);
    // Device 0 was committed-to-none then rolled back: its old mapper must be
    // restored, not left null (the bug being guarded against).
    try testing.expect(sup.managed.items[0].instance.mapper != null);
    try testing.expectEqual(@as(?*const mapping_mod.MappingConfig, &old_parsed.value), sup.managed.items[0].instance.mapping_cfg);
    // Device 1 failed mid-commit and was restored too.
    try testing.expect(sup.managed.items[1].instance.mapper != null);
    try testing.expectEqual(@as(?*const mapping_mod.MappingConfig, &old_parsed.value), sup.managed.items[1].instance.mapping_cfg);
    try testing.expectEqual(false, @atomicLoad(bool, &sup.managed.items[0].instance.stopped, .acquire));
    try testing.expectEqual(false, @atomicLoad(bool, &sup.managed.items[1].instance.stopped, .acquire));
}

test "supervisor: global SWITCH none clears all mappers on success" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const old_parsed = try mapping_mod.parseString(allocator,
        \\[[layer]]
        \\name = "old"
        \\trigger = "LT"
        \\activation = "toggle"
    );
    defer old_parsed.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    inst_a.mapper = try mapper_mod.Mapper.init(&old_parsed.value, inst_a.loop.macro_timer_fd, allocator);
    inst_a.mapping_cfg = &old_parsed.value;
    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    inst_b.mapper = try mapper_mod.Mapper.init(&old_parsed.value, inst_b.loop.macro_timer_fd, allocator);
    inst_b.mapping_cfg = &old_parsed.value;
    try sup.spawnInstance("usb-1-1", inst_a, null);
    try sup.spawnInstance("usb-1-2", inst_b, null);

    sup.handleSwitch(resp_fds[0], "none", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("OK none\n", resp_buf[0..n]);
    try testing.expect(sup.managed.items[0].instance.mapper == null);
    try testing.expect(sup.managed.items[0].instance.mapping_cfg == null);
    try testing.expect(sup.managed.items[1].instance.mapper == null);
    try testing.expect(sup.managed.items[1].instance.mapping_cfg == null);
}

test "supervisor: switch to mapping with new aux KEY_* rebuilds aux caps" {
    // Regression guard: before the fix, commitSwitchTarget swapped mapper and
    // mapping_cfg but never called rebuildAuxIfChanged. If the default_mapping
    // loaded at daemon start did not need aux (needs_keyboard false, no
    // mouse/rel), AuxDevice was created with zero KEY_* caps; then `padctl
    // switch` to a mapping containing KEY_* remaps left the aux_dev caps stale
    // and the kernel silently rejected KEY_G emissions.
    //
    // This test uses a device config with no [output] section so
    // rebuildAuxIfChanged short-circuits without touching /dev/uinput, and
    // observes the call via the test-only rebuild_aux_calls counter on
    // DeviceInstance. If the rebuildAuxIfChanged line in commitSwitchTarget
    // is removed, the counter stays 0 and this test fails.
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    const base_dir = "/tmp/padctl_supervisor_test_switch_issue142";
    std.fs.deleteTreeAbsolute(base_dir) catch {};
    try std.fs.makeDirAbsolute(base_dir);
    defer std.fs.deleteTreeAbsolute(base_dir) catch {};

    const mappings_dir = try std.fmt.allocPrint(allocator, "{s}/mappings", .{base_dir});
    defer allocator.free(mappings_dir);
    try std.fs.makeDirAbsolute(mappings_dir);
    const mapping_path = try std.fmt.allocPrint(allocator, "{s}/keys.toml", .{mappings_dir});
    defer allocator.free(mapping_path);
    {
        const f = try std.fs.createFileAbsolute(mapping_path, .{});
        defer f.close();
        // Mapping that triggers needs_keyboard=true (C -> KEY_G). The bug
        // manifested precisely when switching INTO such a mapping.
        try f.writeAll("[remap]\nC = \"KEY_G\"\n");
    }

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, null);

    // Baseline: the spawn path does not call rebuildAuxIfChanged; pre-switch
    // counter must be 0 so any post-switch observation is attributable to
    // commitSwitchTarget.
    try testing.expectEqual(@as(usize, 0), sup.managed.items[0].instance.rebuild_aux_calls);

    sup.test_switch_mapping_override = try allocator.dupe(u8, mapping_path);

    sup.handleSwitch(resp_fds[0], "keys", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("OK keys\n", resp_buf[0..n]);

    // Primary assertion: commitSwitchTarget must invoke rebuildAuxIfChanged
    // exactly once for this device. Removing the fix drops this to 0.
    try testing.expectEqual(@as(usize, 1), sup.managed.items[0].instance.rebuild_aux_calls);

    // Secondary sanity checks: the swap itself still happened.
    try testing.expect(sup.managed.items[0].instance.mapper != null);
    try testing.expect(sup.managed.items[0].instance.mapping_cfg != null);
    const new_mcfg = sup.managed.items[0].instance.mapping_cfg.?;
    const caps = mapping_mod.deriveAuxFromMapping(new_mcfg);
    try testing.expect(caps.needs_keyboard);
}

test "supervisor: switch restart failure reverts aux caps on rollback" {
    // Regression guard: commitSwitchTarget rebuilds aux caps for the new mapping
    // before restarting the worker. When that restart fails, the rollback must
    // also call rebuildAuxIfChanged to revert the aux device back to the old
    // mapping's caps — otherwise the daemon keeps an aux device shaped for a
    // mapping that never took effect. The reload path already does this; this
    // mirrors it for the switch path.
    //
    // Like the forward-path test, this uses a device config with no [output]
    // section so rebuildAuxIfChanged short-circuits and is observed via the
    // rebuild_aux_calls counter. Forward call = 1, rollback revert = 2.
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    // Base mapping the instance starts on, so commitSwitchTarget captures a
    // non-null old_mapping_cfg and the rollback has something to revert to.
    const base_map = try mapping_mod.parseString(allocator, "");
    defer base_map.deinit();

    const base_dir = "/tmp/padctl_supervisor_test_switch_rollback_aux";
    std.fs.deleteTreeAbsolute(base_dir) catch {};
    try std.fs.makeDirAbsolute(base_dir);
    defer std.fs.deleteTreeAbsolute(base_dir) catch {};

    const mappings_dir = try std.fmt.allocPrint(allocator, "{s}/mappings", .{base_dir});
    defer allocator.free(mappings_dir);
    try std.fs.makeDirAbsolute(mappings_dir);
    const mapping_path = try std.fmt.allocPrint(allocator, "{s}/keys.toml", .{mappings_dir});
    defer allocator.free(mapping_path);
    {
        const f = try std.fs.createFileAbsolute(mapping_path, .{});
        defer f.close();
        try f.writeAll("[remap]\nC = \"KEY_G\"\n");
    }

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    inst.mapping_cfg = &base_map.value;
    try sup.spawnInstance("usb-1-1", inst, null);

    try testing.expectEqual(@as(usize, 0), sup.managed.items[0].instance.rebuild_aux_calls);

    sup.test_switch_mapping_override = try allocator.dupe(u8, mapping_path);

    // Forward restart fails → commitSwitchTarget takes its rollback branch.
    test_fail_next_restart_managed = true;
    sup.handleSwitch(resp_fds[0], "keys", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("ERR switch-failed\n", resp_buf[0..n]);

    // Forward rebuild (new caps) + rollback revert (old caps) = 2 calls.
    try testing.expectEqual(@as(usize, 2), sup.managed.items[0].instance.rebuild_aux_calls);
}

test "supervisor: Supervisor: SWITCH with no devices returns no-devices" {
    const allocator = testing.allocator;

    const base_dir = "/tmp/padctl_supervisor_test_no_devices";
    std.fs.deleteTreeAbsolute(base_dir) catch {};
    try std.fs.makeDirAbsolute(base_dir);
    defer std.fs.deleteTreeAbsolute(base_dir) catch {};

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.ctrl_sock = null;
        sup.deinit();
    }
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    sup.handleSwitch(resp_fds[0], "fps", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("ERR no-devices\n", resp_buf[0..n]);
}

test "supervisor: Supervisor: SIGHUP updates mapping without restarting instance" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    const parsed_map = try mapping_mod.parseString(allocator, "");
    defer parsed_map.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, null);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    var new_map = parsed_map.value;
    const entry = ConfigEntry{
        .phys_key = "usb-1-1",
        .device_cfg = &parsed_dev.value,
        .mapping_cfg = &new_map,
    };

    try sup.reload(&.{entry}, testInitFn);

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expectEqualStrings("usb-1-1", sup.managed.items[0].phys_key);
    try testing.expect(sup.managed.items[0].instance.mapping_cfg != null);
    try testing.expect(sup.managed.items[0].instance.mapper != null);
}

test "supervisor: Supervisor: SIGHUP with new phys_key spawns new instance" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const entry_a = ConfigEntry{ .phys_key = "usb-1-1", .device_cfg = &parsed_dev.value, .mapping_cfg = null };
    const entry_b = ConfigEntry{ .phys_key = "usb-1-2", .device_cfg = &parsed_dev.value, .mapping_cfg = null };

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst_a, null);
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);

    g_mock_slot = &mock_b;
    try sup.reload(&.{ entry_a, entry_b }, testInitFn);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    try testing.expectEqual(@as(usize, 2), sup.managed.items.len);
}

test "supervisor: Supervisor: SIGHUP with removed phys_key stops instance" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const entry_a = ConfigEntry{ .phys_key = "usb-1-1", .device_cfg = &parsed_dev.value, .mapping_cfg = null };

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst_a, null);
    try sup.spawnInstance("usb-1-2", inst_b, null);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    try testing.expectEqual(@as(usize, 2), sup.managed.items.len);

    try sup.reload(&.{entry_a}, testInitFn);
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expectEqualStrings("usb-1-1", sup.managed.items[0].phys_key);
}

test "supervisor: Supervisor: two rapid reloads serialize — no race condition" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    const parsed_map1 = try mapping_mod.parseString(allocator, "");
    defer parsed_map1.deinit();
    const parsed_map2 = try mapping_mod.parseString(allocator, "name = \"v2\"");
    defer parsed_map2.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, null);

    var map1 = parsed_map1.value;
    var map2 = parsed_map2.value;

    const entry1 = ConfigEntry{ .phys_key = "usb-1-1", .device_cfg = &parsed_dev.value, .mapping_cfg = &map1 };
    const entry2 = ConfigEntry{ .phys_key = "usb-1-1", .device_cfg = &parsed_dev.value, .mapping_cfg = &map2 };

    try sup.reload(&.{entry1}, testInitFn);
    sup.managed.items[0].instance.mapper.?.next_token = 42;
    try sup.reload(&.{entry2}, testInitFn);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].instance.mapper != null);
    try testing.expectEqual(@as(u32, 1), sup.managed.items[0].instance.mapper.?.next_token);
}

test "supervisor: reload restart failure preserves switch mapping and restarts old mapper" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const new_parsed = try mapping_mod.parseString(allocator, "name = \"new\"");
    defer new_parsed.deinit();

    const switch_pr = try allocator.create(mapping_mod.ParseResult);
    switch_pr.* = try mapping_mod.parseString(allocator,
        \\name = "switch"
        \\[[layer]]
        \\name = "old"
        \\trigger = "LT"
        \\activation = "toggle"
    );

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    inst.mapper = try mapper_mod.Mapper.init(&switch_pr.value, inst.loop.macro_timer_fd, allocator);
    inst.mapping_cfg = &switch_pr.value;
    inst.loop.gamepad_state.buttons = @as(u64, 1) << @intFromEnum(state_mod.ButtonId.RB);
    try inst.mapper.?.layer.toggled.put("old", {});
    try sup.spawnInstance("usb-1-1", inst, null);
    sup.managed.items[0].switch_mapping = switch_pr;
    defer {
        sup.stopAll();
        sup.deinit();
    }

    var new_map = new_parsed.value;
    const entry = ConfigEntry{
        .phys_key = "usb-1-1",
        .device_cfg = &parsed_dev.value,
        .mapping_cfg = &new_map,
    };
    test_fail_next_restart_managed = true;
    try testing.expectError(error.TestInjectedRestartFailure, sup.reload(&.{entry}, testInitFn));

    const managed = &sup.managed.items[0];
    try testing.expect(managed.switch_mapping == switch_pr);
    try testing.expectEqual(@as(?*const mapping_mod.MappingConfig, &switch_pr.value), managed.instance.mapping_cfg);
    try testing.expect(managed.instance.mapper != null);
    try testing.expectEqual(@as(usize, 0), managed.instance.mapper.?.layer.toggled.count());
    try testing.expectEqual(@as(u64, @as(u64, 1) << @intFromEnum(state_mod.ButtonId.RB)), managed.instance.mapper.?.state.buttons);
    try testing.expectEqual(false, @atomicLoad(bool, &managed.instance.stopped, .acquire));
}

test "supervisor: Supervisor: reload null mapping clears existing mapper" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const parsed_map = try mapping_mod.parseString(allocator, "");
    defer parsed_map.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    inst.mapping_cfg = &parsed_map.value;
    inst.mapper = try mapper_mod.Mapper.init(&parsed_map.value, inst.loop.macro_timer_fd, allocator);
    try sup.spawnInstance("usb-1-1", inst, null);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    const entry = ConfigEntry{
        .phys_key = "usb-1-1",
        .device_cfg = &parsed_dev.value,
        .mapping_cfg = null,
    };
    try sup.reload(&.{entry}, testInitFn);

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].instance.mapping_cfg == null);
    try testing.expect(sup.managed.items[0].instance.mapper == null);
}

test "supervisor: reload with malformed TOML keeps old mapping active" {
    // Simulates: reloadFn fails (e.g. malformed TOML parse error) → doReload logs and returns.
    // Verifiable via reload(): call with a new-device entry whose initFn returns error
    // → existing managed instance is untouched.
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    const parsed_map = try mapping_mod.parseString(allocator, "[remap]\nM1 = \"KEY_A\"");
    defer parsed_map.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    inst.mapping_cfg = &parsed_map.value;
    inst.mapper = try mapper_mod.Mapper.init(&parsed_map.value, inst.loop.macro_timer_fd, allocator);
    try sup.spawnInstance("usb-1-1", inst, null);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    // Reload that retains the existing device (same phys_key) with the same mapping —
    // this is the "parse succeeded, same mapping" path. Verify instance stays running
    // with the original mapper intact.
    const entry = ConfigEntry{
        .phys_key = "usb-1-1",
        .device_cfg = &parsed_dev.value,
        .mapping_cfg = @constCast(&parsed_map.value),
    };
    try sup.reload(&.{entry}, testInitFn);

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].instance.mapper != null);
    try testing.expect(sup.managed.items[0].instance.mapping_cfg != null);
}

test "supervisor: Supervisor: empty config dir → zero instances" {
    const allocator = testing.allocator;
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();

    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    try sup.startFromDirWithRoot(tmp_path, "/nonexistent_dev_root_xyz");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
}

test "supervisor: Supervisor: dir with no toml files → zero instances" {
    const allocator = testing.allocator;
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();

    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);

    try tmp.dir.writeFile(.{ .sub_path = "readme.txt", .data = "hello" });

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    try sup.startFromDirWithRoot(tmp_path, "/nonexistent_dev_root_xyz");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
}

test "supervisor: Supervisor: two toml files, no matching hidraw → zero instances" {
    const allocator = testing.allocator;
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();

    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);

    try tmp.dir.writeFile(.{ .sub_path = "a.toml", .data = minimal_device_toml });
    try tmp.dir.writeFile(.{ .sub_path = "b.toml", .data = minimal_device_toml });

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    try sup.startFromDirWithRoot(tmp_path, "/nonexistent_dev_root_xyz");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
}

test "supervisor: hotplug: config retained when no device online, attach finds it" {
    const allocator = testing.allocator;
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();

    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);

    try tmp.dir.writeFile(.{ .sub_path = "a.toml", .data = minimal_device_toml });

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    // No device online at startup — config must still be retained for hotplug.
    try sup.startFromDirWithRoot(tmp_path, "/nonexistent_dev_root_xyz");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    try testing.expectEqual(@as(usize, 1), sup.configs.items.len);
}

test "supervisor: user output_profile applies before config is cached for hotplug" {
    const allocator = testing.allocator;
    const profile_device_toml =
        \\[device]
        \\name = "T"
        \\vid = 1
        \\pid = 2
        \\[[device.interface]]
        \\id = 0
        \\class = "hid"
        \\[[report]]
        \\name = "r"
        \\interface = 0
        \\size = 3
        \\[report.match]
        \\offset = 0
        \\expect = [0x01]
        \\[report.fields]
        \\left_x = { offset = 1, type = "i16le" }
        \\[output]
        \\name = "Base Output"
        \\vid = 0x1111
        \\pid = 0x2222
        \\[output.buttons]
        \\A = "BTN_SOUTH"
        \\[output.profiles.dualsense-edge]
        \\emulate = "dualsense-edge"
    ;

    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();
    try tmp.dir.writeFile(.{ .sub_path = "profile.toml", .data = profile_device_toml });
    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    const cfg_str =
        \\[[device]]
        \\name = "T"
        \\output_profile = "dualsense-edge"
    ;
    var ucfg_parser = @import("toml").Parser(user_config_mod.UserConfig).init(allocator);
    defer ucfg_parser.deinit();
    sup.user_cfg = try ucfg_parser.parseString(cfg_str);

    try sup.startFromDirWithRoot(tmp_path, "/nonexistent_dev_root_xyz");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    try testing.expectEqual(@as(usize, 1), sup.configs.items.len);

    const out = sup.configs.items[0].value.output orelse return error.MissingOutput;
    try testing.expectEqual(@as(?i64, 0x054c), out.vid);
    try testing.expectEqual(@as(?i64, 0x0df2), out.pid);
    try testing.expectEqualStrings("Sony DualSense Edge", out.name.?);
}

test "supervisor: Supervisor: duplicate attach devname — only one instance created" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst, null);
    defer {
        sup.stopAll();
        sup.deinit();
    }

    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);

    const inst2 = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    defer {
        inst2.deinit();
        allocator.destroy(inst2);
    }
    try sup.attachWithInstance("hidraw3", "usb-1-1b", inst2, null);
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
}

test "supervisor: Supervisor: detach unknown devname — no panic" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    sup.detach("hidraw99");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
}

test "supervisor: Supervisor: detach suspends instance, keeps it in managed list" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(!sup.managed.items[0].suspended);

    sup.detach("hidraw3");
    // Instance stays in managed list but is suspended
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(@as(?[]const u8, null), sup.managed.items[0].devname);
    // devname removed from map
    try testing.expect(!sup.devname_map.contains("hidraw3"));

    defer {
        sup.stopAll();
        sup.deinit();
    }
}

test "supervisor: Supervisor: suspend preserves instance, resume rebinds device IO" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);
    const inst_ptr = sup.managed.items[0].instance;

    // Detach suspends — instance stays, thread stops
    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(inst_ptr, sup.managed.items[0].instance);
    try testing.expect(!sup.devname_map.contains("hidraw3"));

    // Rebind with new mock device IO
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();
    var new_devs = [_]DeviceIO{mock_b.deviceIO()};
    try inst_ptr.rebindDeviceIO(&new_devs);
    sup.managed.items[0].suspended = false;

    const dn = try allocator.dupe(u8, "hidraw5");
    sup.managed.items[0].devname = dn;
    const dk = try allocator.dupe(u8, "hidraw5");
    const pk = try allocator.dupe(u8, "usb-1-1");
    try sup.devname_map.put(dk, pk);

    Supervisor.restartManagedThread(&sup.managed.items[0]) catch |err| {
        std.log.err("restart failed: {}", .{err});
        return err;
    };

    // Same instance pointer reused
    try testing.expectEqual(inst_ptr, sup.managed.items[0].instance);
    try testing.expect(!sup.managed.items[0].suspended);

    defer {
        sup.stopAll();
        sup.deinit();
    }
}

test "supervisor: suspended hotplug rebind helper resumes through production transaction" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);
    const inst_ptr = sup.managed.items[0].instance;

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var new_devs = [_]DeviceIO{mock_b.deviceIO()};
    var opener = TestRebindOpenCtx{ .devices = &new_devs };
    try testing.expect(try sup.tryResumeSuspendedInstance(
        "hidraw5",
        "usb-1-1",
        1,
        2,
        &opener,
        TestRebindOpenCtx.open,
    ));

    try testing.expectEqual(@as(usize, 1), opener.next);
    try testing.expectEqual(inst_ptr, sup.managed.items[0].instance);
    try testing.expect(!sup.managed.items[0].suspended);
    try testing.expectEqual(@as(?u64, null), sup.managed.items[0].grace_deadline_ns);
    try testing.expect(original_deadline > 0);
    try testing.expectEqualStrings("hidraw5", sup.managed.items[0].devname.?);
    try testing.expect(sup.devname_map.contains("hidraw5"));
    try testing.expectEqual(mock_b.deviceIO().pollfd().fd, inst_ptr.devices[0].pollfd().fd);
}

test "supervisor: suspended hotplug rebind helper ignores same phys with different VID/PID" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var no_devices = [_]DeviceIO{};
    var opener = TestRebindOpenCtx{ .devices = &no_devices };
    try testing.expect(!try sup.tryResumeSuspendedInstance(
        "hidraw5",
        "usb-1-1",
        9,
        9,
        &opener,
        TestRebindOpenCtx.open,
    ));

    try testing.expectEqual(@as(usize, 0), opener.next);
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(original_deadline, sup.managed.items[0].grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), sup.managed.items[0].devname);
    try testing.expect(!sup.devname_map.contains("hidraw5"));
}

test "supervisor: fresh hotplug attach replaces suspended entry with different VID/PID on same phys" {
    const allocator = testing.allocator;

    const parsed_old = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_old.deinit();

    const parsed_new = try device_mod.parseString(allocator,
        \\[device]
        \\name = "Replacement"
        \\vid = 9
        \\pid = 9
        \\[[device.interface]]
        \\id = 0
        \\class = "hid"
        \\[[report]]
        \\name = "r"
        \\interface = 0
        \\size = 3
        \\[report.match]
        \\offset = 0
        \\expect = [0x01]
        \\[report.fields]
        \\left_x = { offset = 1, type = "i16le" }
    );
    defer parsed_new.deinit();

    var mock_old = try MockDeviceIO.init(allocator, &.{});
    defer mock_old.deinit();
    var mock_new = try MockDeviceIO.init(allocator, &.{});
    defer mock_new.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    const old_inst = try makeTestInstance(allocator, &mock_old, &parsed_old.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", old_inst, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);

    const new_inst = try makeTestInstance(allocator, &mock_new, &parsed_new.value);
    var new_attached = false;
    defer if (!new_attached) {
        new_inst.deinit();
        allocator.destroy(new_inst);
    };
    new_attached = try sup.attachWithInstanceResult("hidraw5", "usb-1-1", new_inst, null);

    try testing.expect(new_attached);
    try testing.expectEqual(@as(usize, 1), sup.managed.items.len);
    try testing.expectEqual(new_inst, sup.managed.items[0].instance);
    try testing.expect(!sup.managed.items[0].suspended);
    try testing.expectEqual(@as(i64, 9), sup.managed.items[0].instance.device_cfg.device.vid);
    try testing.expectEqual(@as(i64, 9), sup.managed.items[0].instance.device_cfg.device.pid);
    try testing.expectEqualStrings("hidraw5", sup.managed.items[0].devname.?);
    try testing.expect(sup.devname_map.contains("hidraw5"));
}

test "supervisor: suspended hotplug rebind helper propagates allocation failure" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var no_devices = [_]DeviceIO{};
    var opener = TestRebindOpenCtx{ .devices = &no_devices };
    const result = blk: {
        const real_alloc = sup.allocator;
        var failing_alloc = testing.FailingAllocator.init(real_alloc, .{ .fail_index = 0 });
        sup.allocator = failing_alloc.allocator();
        defer sup.allocator = real_alloc;
        break :blk sup.tryResumeSuspendedInstance(
            "hidraw5",
            "usb-1-1",
            1,
            2,
            &opener,
            TestRebindOpenCtx.open,
        );
    };

    try testing.expectError(error.OutOfMemory, result);
    try testing.expectEqual(@as(usize, 0), opener.next);
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(original_deadline, sup.managed.items[0].grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), sup.managed.items[0].devname);
    try testing.expect(!sup.devname_map.contains("hidraw5"));
}

test "supervisor: suspended hotplug rebind helper propagates finalize allocation failure" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var new_devs = [_]DeviceIO{mock_b.deviceIO()};
    var opener = TestRebindOpenCtx{ .devices = &new_devs };
    const result = blk: {
        const real_alloc = sup.allocator;
        var failing_alloc = testing.FailingAllocator.init(real_alloc, .{ .fail_index = 1 });
        sup.allocator = failing_alloc.allocator();
        defer sup.allocator = real_alloc;
        break :blk sup.tryResumeSuspendedInstance(
            "hidraw5",
            "usb-1-1",
            1,
            2,
            &opener,
            TestRebindOpenCtx.open,
        );
    };

    try testing.expectError(error.OutOfMemory, result);
    try testing.expectEqual(@as(usize, 1), opener.next);
    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(original_deadline, sup.managed.items[0].grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), sup.managed.items[0].devname);
    try testing.expect(!sup.devname_map.contains("hidraw5"));
}

test "supervisor: suspended hotplug rebind helper closes failed reinit fds and preserves grace" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, init_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    sup.suspend_grace_sec = 5;
    sup.test_now_override_ns = 10 * std.time.ns_per_s;

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var failing = FailWriteDeviceIO{};
    var new_devs = [_]DeviceIO{failing.deviceIO()};
    var opener = TestRebindOpenCtx{ .devices = &new_devs };
    try testing.expectError(error.HotplugTransient, sup.tryResumeSuspendedInstance(
        "hidraw5",
        "usb-1-1",
        1,
        2,
        &opener,
        TestRebindOpenCtx.open,
    ));

    try testing.expect(sup.managed.items[0].suspended);
    try testing.expectEqual(original_deadline, sup.managed.items[0].grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), sup.managed.items[0].devname);
    try testing.expect(!sup.devname_map.contains("hidraw5"));
    try testing.expectEqual(@as(posix.fd_t, -1), sup.managed.items[0].instance.devices[0].pollfd().fd);
}

test "supervisor: failed re-init after suspended rebind preserves grace state" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, init_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    sup.suspend_grace_sec = 5;
    sup.test_now_override_ns = 10 * std.time.ns_per_s;

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var failing = FailWriteDeviceIO{};
    var new_devs = [_]DeviceIO{failing.deviceIO()};
    const m = &sup.managed.items[0];
    try m.instance.rebindDeviceIO(&new_devs);
    try testing.expectError(DeviceIO.WriteError.Io, Supervisor.rerunInitAfterRebind(m));

    try testing.expect(m.suspended);
    try testing.expectEqual(original_deadline, m.grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), m.devname);
    try testing.expect(!sup.devname_map.contains("hidraw3"));
    try testing.expectEqual(@as(posix.fd_t, -1), m.instance.devices[0].pollfd().fd);
}

test "supervisor: required init ack failure after suspended rebind preserves grace state" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, strict_init_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    sup.suspend_grace_sec = 5;
    sup.test_now_override_ns = 10 * std.time.ns_per_s;

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();
    var new_devs = [_]DeviceIO{mock_b.deviceIO()};
    var opener = TestRebindOpenCtx{ .devices = &new_devs };
    try testing.expectError(error.HotplugTransient, sup.tryResumeSuspendedInstance(
        "hidraw5",
        "usb-1-1",
        1,
        2,
        &opener,
        TestRebindOpenCtx.open,
    ));

    const m = &sup.managed.items[0];
    try testing.expect(m.suspended);
    try testing.expectEqual(original_deadline, m.grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), m.devname);
    try testing.expect(!sup.devname_map.contains("hidraw3"));
    try testing.expectEqual(@as(posix.fd_t, -1), m.instance.devices[0].pollfd().fd);
}

test "supervisor: failed feature-report re-init after suspended rebind preserves grace state" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, feature_init_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    sup.suspend_grace_sec = 5;
    sup.test_now_override_ns = 10 * std.time.ns_per_s;

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);
    const original_deadline = sup.managed.items[0].grace_deadline_ns.?;

    var failing = FailWriteDeviceIO{};
    var new_devs = [_]DeviceIO{failing.deviceIO()};
    const m = &sup.managed.items[0];
    try m.instance.rebindDeviceIO(&new_devs);
    try testing.expectError(DeviceIO.WriteError.Io, Supervisor.rerunInitAfterRebind(m));

    try testing.expect(m.suspended);
    try testing.expectEqual(original_deadline, m.grace_deadline_ns.?);
    try testing.expectEqual(@as(?[]const u8, null), m.devname);
    try testing.expect(!sup.devname_map.contains("hidraw3"));
    try testing.expectEqual(@as(posix.fd_t, -1), m.instance.devices[0].pollfd().fd);
}

test "supervisor: Supervisor: stopAll handles suspended instances" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);
    sup.detach("hidraw3");
    try testing.expect(sup.managed.items[0].suspended);

    // stopAll must not crash on suspended instances
    sup.stopAll();
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    sup.deinit();
}

test "supervisor: Supervisor: status shows suspended state" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    // Create a control socket for status test
    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    // Status before suspend
    sup.handleStatus(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    var n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "state=active") != null);

    // Suspend
    sup.detach("hidraw3");

    // Status after suspend
    sup.handleStatus(resp_fds[0]);
    n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "state=suspended") != null);

    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
}

test "supervisor: Supervisor: status includes mapping name" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    // Heap-allocate a mapping ParseResult with a known name; supervisor owns and frees it.
    const map_pr = try allocator.create(mapping_mod.ParseResult);
    map_pr.* = try mapping_mod.parseString(allocator, "name = \"xbox-elite2\"");

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, map_pr);

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    sup.handleStatus(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=xbox-elite2") != null);

    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
}

var test_reload_calls: usize = 0;

fn testReloadHookCall(_: *const anyopaque, _: *Supervisor) void {
    test_reload_calls += 1;
}

test "supervisor: handleReload runs the hook and reports the device count" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };
    var dummy: u8 = 0;
    test_reload_calls = 0;
    sup.reload_hook = .{ .ctx = &dummy, .call = testReloadHookCall };

    sup.handleReload(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqual(@as(usize, 1), test_reload_calls);
    try testing.expectEqualStrings("OK reloaded 1 devices\n", resp_buf[0..n]);

    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
}

test "supervisor: handleReload without a hook reports ERR" {
    const allocator = testing.allocator;

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };
    defer sup.ctrl_sock = null;

    sup.handleReload(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("ERR reload-unavailable\n", resp_buf[0..n]);
}

// Hook that drives the production dir-reload path, mirroring serve()'s dispatch
// so handleReload sees the same swallowed-parse failures a real reload would.
const DirReloadDispatch = struct {
    dir_path: []const u8,
    fn reload(d: @This(), sup: *Supervisor) void {
        sup.doReloadFromDir(d.dir_path);
    }
};

fn installDirReloadHook(sup: *Supervisor, dispatch: *const DirReloadDispatch) void {
    sup.reload_hook = .{
        .ctx = dispatch,
        .call = struct {
            fn call(ctx: *const anyopaque, s: *Supervisor) void {
                const d: *const DirReloadDispatch = @ptrCast(@alignCast(ctx));
                d.reload(s);
            }
        }.call,
    };
}

test "supervisor: handleReload reports ERR when a config no longer parses" {
    const allocator = testing.allocator;

    var cfg_dir = testing.tmpDir(.{});
    defer cfg_dir.cleanup();
    try cfg_dir.dir.writeFile(.{ .sub_path = "broken.toml", .data = "this is = not [valid toml" });
    const cfg_dir_path = try cfg_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(cfg_dir_path);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };
    defer sup.ctrl_sock = null;

    const dispatch = DirReloadDispatch{ .dir_path = cfg_dir_path };
    installDirReloadHook(&sup, &dispatch);
    defer sup.reload_hook = null;

    sup.handleReload(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.startsWith(u8, resp_buf[0..n], "ERR "));
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "broken.toml") != null);
}

test "supervisor: handleReload reports OK when every config parses" {
    const allocator = testing.allocator;

    var cfg_dir = testing.tmpDir(.{});
    defer cfg_dir.cleanup();
    try cfg_dir.dir.writeFile(.{ .sub_path = "device.toml", .data = minimal_device_toml });
    const cfg_dir_path = try cfg_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(cfg_dir_path);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();
    sup.hotplug_retry_fd = try posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true });

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };
    defer sup.ctrl_sock = null;

    const dispatch = DirReloadDispatch{ .dir_path = cfg_dir_path };
    installDirReloadHook(&sup, &dispatch);
    defer sup.reload_hook = null;

    sup.handleReload(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("OK reloaded 0 devices\n", resp_buf[0..n]);
}

test "supervisor: Supervisor: status shows (none) when no mapping loaded" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    sup.handleStatus(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=(none)") != null);

    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
}

test "supervisor: Supervisor: status uses file stem when mapping name field is null" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const map_pr = try allocator.create(mapping_mod.ParseResult);
    map_pr.* = try mapping_mod.parseString(allocator, "");

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, map_pr);

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    // Simulate a successful switch: switch_mapping set, stem stored, name field null.
    const switch_pr = try allocator.create(mapping_mod.ParseResult);
    switch_pr.* = try mapping_mod.parseString(allocator, "");
    const m = &sup.managed.items[0];
    m.switch_mapping = switch_pr;
    m.switch_mapping_stem = try allocator.dupe(u8, "my-pad");

    sup.handleStatus(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    // Must show file stem, not "(none)".
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=my-pad") != null);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=(none)") == null);

    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
}

// Regression guard: a mapping applied via the user-config default mapping path
// (NOT a live `padctl switch`) made `padctl status` print `mapping=(none)` when
// the mapping file had no `name =` field. The `default_mapping_pr` branch had
// no file-stem fallback. This test drives the REAL production resolver
// `buildDefaultMapping` (real file read, real TOML parse with no `name=`, real
// std.fs.path.stem) — it does NOT hand-assign default_mapping_pr /
// default_mapping_stem.
test "supervisor: Supervisor: status uses default-mapping file stem when name field is null" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    // Real mapping file with NO `name =` line — all MappingConfig fields
    // are optional so this parses with name == null.
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();
    try tmp.dir.writeFile(.{
        .sub_path = "mypad.toml",
        .data =
        \\[stick.left]
        \\mode = "gamepad"
        \\
        ,
    });
    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);
    const mapping_path = try std.fs.path.join(allocator, &.{ tmp_path, "mypad.toml" });
    defer allocator.free(mapping_path);

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    // Real user config: device "T" -> default_mapping = "mypad".
    const cfg_str =
        \\[[device]]
        \\name = "T"
        \\default_mapping = "mypad"
    ;
    var ucfg_parser = @import("toml").Parser(user_config_mod.UserConfig).init(allocator);
    defer ucfg_parser.deinit();
    sup.user_cfg = try ucfg_parser.parseString(cfg_str);
    sup.test_default_mapping_override = try allocator.dupe(u8, mapping_path);

    // Production resolver: reads the real file, parses it (name == null),
    // computes the stem via std.fs.path.stem(path).
    const default_dm = sup.buildDefaultMapping("T");
    try testing.expect(default_dm != null);
    const default_pr_ptr = default_dm.?.pr;
    const default_stem = default_dm.?.stem;
    try testing.expect(default_pr_ptr.value.name == null);

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();
    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, default_pr_ptr);
    // Mirror the production callers exactly (startFromDirWithRoot ~1797,
    // attach path ~2032): store the resolver-derived stem on the tail item.
    sup.managed.items[sup.managed.items.len - 1].default_mapping_stem = default_stem;

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };
    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    sup.handleStatus(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=mypad") != null);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=(none)") == null);
}

// Regression guard (second guard): verifies the full config-from-dir path.
// startFromDirWithRoot parses the device TOML, confirms config is stored, then
// buildDefaultMapping reads the name-less mapping file and derives the stem via
// std.fs.path.stem. handleStatus must return mapping=mypad, not mapping=(none).
//
// NOTE: the store inside startFromDirWithRoot cannot be reached at Layer 0 —
// discoverAllWithRoot opens real hidraw char devices via ioctl which are
// unavailable without hardware. This test exercises: TOML dir scan → config
// parsed → buildDefaultMapping real file read → stem derived → spawnInstance
// → handleStatus stem branch.
test "supervisor: Supervisor: status stem fallback — config parsed from TOML dir, name-less mapping file" {
    const allocator = testing.allocator;

    // Device TOML dir: one device file that startFromDirWithRoot will parse.
    var dev_dir = testing.tmpDir(.{});
    defer dev_dir.cleanup();
    try dev_dir.dir.writeFile(.{ .sub_path = "testdev.toml", .data = minimal_device_toml });
    const dev_dir_path = try dev_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(dev_dir_path);

    // Mapping file with NO `name =` line so handleStatus must use stem.
    var map_dir = testing.tmpDir(.{});
    defer map_dir.cleanup();
    try map_dir.dir.writeFile(.{
        .sub_path = "mypad.toml",
        .data =
        \\[stick.left]
        \\mode = "gamepad"
        \\
        ,
    });
    const map_dir_path = try map_dir.dir.realpathAlloc(allocator, ".");
    defer allocator.free(map_dir_path);
    const mapping_path = try std.fs.path.join(allocator, &.{ map_dir_path, "mypad.toml" });
    defer allocator.free(mapping_path);

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    const cfg_str =
        \\[[device]]
        \\name = "T"
        \\default_mapping = "mypad"
    ;
    var ucfg_parser = @import("toml").Parser(user_config_mod.UserConfig).init(allocator);
    defer ucfg_parser.deinit();
    sup.user_cfg = try ucfg_parser.parseString(cfg_str);
    sup.test_default_mapping_override = try allocator.dupe(u8, mapping_path);

    // startFromDirWithRoot scans the device TOML dir; no hidraw hardware online
    // (nonexistent dev_root) so managed.items stays empty, but configs must be
    // populated — this exercises the TOML-dir scan path that feeds line 1797.
    try sup.startFromDirWithRoot(dev_dir_path, "/nonexistent_dev_root_xyz");
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
    try testing.expectEqual(@as(usize, 1), sup.configs.items.len);
    try testing.expectEqualStrings("T", sup.configs.items[0].value.device.name);

    // Production resolver: reads the real mapping file, parses (name == null),
    // derives stem via std.fs.path.stem — same logic that line 1797 stores.
    const default_dm = sup.buildDefaultMapping("T");
    try testing.expect(default_dm != null);
    const default_pr_ptr = default_dm.?.pr;
    const default_stem = default_dm.?.stem;
    try testing.expect(default_pr_ptr.value.name == null);
    try testing.expectEqualStrings("mypad", default_stem);

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();
    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();
    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, default_pr_ptr);
    // Assign stem exactly as production callers do at line 1797 / line 2032.
    sup.managed.items[sup.managed.items.len - 1].default_mapping_stem = default_stem;

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };
    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    sup.handleStatus(resp_fds[0]);
    var resp_buf: [256]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=mypad") != null);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n], "mapping=(none)") == null);
}

test "supervisor: Supervisor: two devnames attached simultaneously — independent threads" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var mock_b = try MockDeviceIO.init(allocator, &.{});
    defer mock_b.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    const inst_b = try makeTestInstance(allocator, &mock_b, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);
    try sup.attachWithInstance("hidraw4", "usb-1-2", inst_b, null);
    defer {
        sup.stopAll();
        sup.deinit();
    }
    try testing.expectEqual(@as(usize, 2), sup.managed.items.len);

    try testing.expect(sup.managed.items[0].instance != sup.managed.items[1].instance);
}

test "supervisor: Supervisor: initForTest sets inotify_fd and debounce_fd to -1" {
    const allocator = testing.allocator;
    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    try testing.expectEqual(@as(posix.fd_t, -1), sup.inotify_fd);
    try testing.expectEqual(@as(posix.fd_t, -1), sup.debounce_fd);
    try testing.expectEqual(@as(?[]const u8, null), sup.config_dir);
}

test "supervisor: Supervisor: inotify debounce coalescing with real timerfd" {
    const allocator = testing.allocator;
    var sup = try Supervisor.initForTest(allocator);

    // Create a real timerfd to test armDebounce logic
    sup.debounce_fd = posix.timerfd_create(.MONOTONIC, .{ .CLOEXEC = true, .NONBLOCK = true }) catch {
        return;
    };
    defer sup.deinit();

    // Arm debounce: should set a 500ms timer
    sup.armDebounce();

    // Re-arm immediately: timer should reset, not fire twice
    sup.armDebounce();

    // Timer not yet fired — read should return WouldBlock
    var tbuf: [8]u8 = undefined;
    const result = posix.read(sup.debounce_fd, &tbuf);
    try testing.expectError(error.WouldBlock, result);
}

test "supervisor: Supervisor: armDebounce with invalid fd is no-op" {
    const allocator = testing.allocator;
    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    // debounce_fd is -1 from initForTest — should not crash
    sup.armDebounce();
}

test "supervisor: initInotify: non-existent config dir returns disabled" {
    const allocator = testing.allocator;

    // Use testing allocator — if a real config dir exists, this test still
    // validates the return structure. The key invariant: no fd leak.
    const result = initInotify(allocator);
    if (result.inotify_fd >= 0) {
        posix.close(result.inotify_fd);
        posix.close(result.debounce_fd);
        allocator.free(result.config_dir.?);
    } else {
        try testing.expectEqual(@as(posix.fd_t, -1), result.inotify_fd);
        try testing.expectEqual(@as(posix.fd_t, -1), result.debounce_fd);
        try testing.expectEqual(@as(?[]const u8, null), result.config_dir);
    }
}

test "supervisor: initInotify: watches temp directory successfully" {
    const allocator = testing.allocator;

    // Create a temp dir to use as config dir, then manually set up inotify on it
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();
    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);

    const tmp_z = try allocator.dupeZ(u8, tmp_path);
    defer allocator.free(tmp_z);

    const rc_init = linux.inotify_init1(linux.IN.CLOEXEC | linux.IN.NONBLOCK);
    const init_err = linux.E.init(rc_init);
    if (init_err != .SUCCESS) return; // skip if inotify unavailable
    const in_fd: posix.fd_t = @intCast(rc_init);
    defer posix.close(in_fd);

    const rc_watch = linux.inotify_add_watch(in_fd, tmp_z.ptr, linux.IN.CLOSE_WRITE | linux.IN.MOVED_TO);
    const watch_err = linux.E.init(rc_watch);
    try testing.expect(watch_err == .SUCCESS);

    // Write a file into the watched directory
    try tmp.dir.writeFile(.{ .sub_path = "test.toml", .data = "hello" });

    // inotify should be readable now
    var buf: [4096]u8 = undefined;
    const n = posix.read(in_fd, &buf) catch 0;
    try testing.expect(n > 0);
}

test "supervisor: Supervisor.serve: control socket accepts client and responds to STATUS" {
    const allocator = testing.allocator;
    var sup = try Supervisor.initForTest(allocator);

    // Set up control socket on a temp path
    var tmp = testing.tmpDir(.{});
    defer tmp.cleanup();
    const tmp_path = try tmp.dir.realpathAlloc(allocator, ".");
    defer allocator.free(tmp_path);
    var sock_path_buf: [256]u8 = undefined;
    const sock_path = try std.fmt.bufPrint(&sock_path_buf, "{s}/test.sock", .{tmp_path});

    sup.ctrl_sock = ControlSocket.init(allocator, sock_path) catch {
        sup.deinit();
        return; // skip if socket creation fails
    };

    const serve_thread = try std.Thread.spawn(.{}, struct {
        fn run(s: *Supervisor, dp: []const u8) void {
            s.serve(dp);
        }
    }.run, .{ &sup, tmp_path });

    // Give serve a moment to enter poll
    std.Thread.sleep(10 * std.time.ns_per_ms);

    // Connect client socket
    const client_fd = posix.socket(posix.AF.UNIX, posix.SOCK.STREAM | posix.SOCK.CLOEXEC, 0) catch {
        // Signal stop and clean up
        const val: [8]u8 = @bitCast(@as(u64, 1));
        _ = posix.write(sup.stop_fd, &val) catch {};
        serve_thread.join();
        sup.deinit();
        return;
    };
    defer posix.close(client_fd);

    var addr: linux.sockaddr.un = .{ .family = posix.AF.UNIX, .path = undefined };
    @memset(&addr.path, 0);
    @memcpy(addr.path[0..sock_path.len], sock_path);
    posix.connect(client_fd, @ptrCast(&addr), @sizeOf(linux.sockaddr.un)) catch {
        const val: [8]u8 = @bitCast(@as(u64, 1));
        _ = posix.write(sup.stop_fd, &val) catch {};
        serve_thread.join();
        sup.deinit();
        return;
    };

    // Send STATUS command
    _ = posix.write(client_fd, "STATUS\n") catch {};
    std.Thread.sleep(20 * std.time.ns_per_ms);

    // Read response
    var resp_buf: [256]u8 = undefined;
    const resp_n = posix.read(client_fd, &resp_buf) catch 0;
    if (resp_n > 0) {
        const resp = resp_buf[0..resp_n];
        try testing.expect(std.mem.startsWith(u8, resp, "STATUS"));
    }

    // Signal stop
    const val: [8]u8 = @bitCast(@as(u64, 1));
    _ = posix.write(sup.stop_fd, &val) catch {};
    serve_thread.join();
    sup.deinit();
}

test "supervisor: startFromDirs loads configs from two dirs" {
    const allocator = testing.allocator;

    const toml_a =
        \\[device]
        \\name = "DevA"
        \\vid = 0x1111
        \\pid = 0x0001
        \\[[device.interface]]
        \\id = 0
        \\class = "hid"
        \\[[report]]
        \\name = "r"
        \\interface = 0
        \\size = 1
        \\[report.match]
        \\offset = 0
        \\expect = [0x01]
    ;
    const toml_b =
        \\[device]
        \\name = "DevB"
        \\vid = 0x2222
        \\pid = 0x0002
        \\[[device.interface]]
        \\id = 0
        \\class = "hid"
        \\[[report]]
        \\name = "r"
        \\interface = 0
        \\size = 1
        \\[report.match]
        \\offset = 0
        \\expect = [0x01]
    ;

    var tmp_a = testing.tmpDir(.{});
    defer tmp_a.cleanup();
    var tmp_b = testing.tmpDir(.{});
    defer tmp_b.cleanup();

    try tmp_a.dir.writeFile(.{ .sub_path = "a.toml", .data = toml_a });
    try tmp_b.dir.writeFile(.{ .sub_path = "b.toml", .data = toml_b });

    const path_a = try tmp_a.dir.realpathAlloc(allocator, ".");
    defer allocator.free(path_a);
    const path_b = try tmp_b.dir.realpathAlloc(allocator, ".");
    defer allocator.free(path_b);

    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    // No real hidraw devices — both dirs scanned, zero instances spawned,
    // but configs from both dirs must have been attempted (no error, no panic).
    const dirs = &[_][]const u8{ path_a, path_b };
    try sup.startFromDirsWithRoot(dirs, "/nonexistent_dev_root_xyz");

    // With a non-existent dev root neither device will be found, so managed is empty.
    // The key assertion: startFromDirsWithRoot scanned both dirs without hanging.
    try testing.expectEqual(@as(usize, 0), sup.managed.items.len);
}

// Regression guard: padctl switch (no arg) returned "no devices connected" even
// when a device was active. The fix queries STATUS, extracts the device name,
// reads default_mapping from config, then calls handleSwitch with the resolved
// name. This test exercises all three steps without a real daemon process.
test "supervisor: STATUS -> default_mapping lookup -> handleSwitch succeeds" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    const base_dir = "/tmp/padctl_supervisor_test_issue136";
    std.fs.deleteTreeAbsolute(base_dir) catch {};
    try std.fs.makeDirAbsolute(base_dir);
    defer std.fs.deleteTreeAbsolute(base_dir) catch {};

    const mapping_path = try std.fmt.allocPrint(allocator, "{s}/foo.toml", .{base_dir});
    defer allocator.free(mapping_path);
    {
        const f = try std.fs.createFileAbsolute(mapping_path, .{});
        f.close();
    }

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, null);

    // Step 1: STATUS response contains the connected device name.
    sup.handleStatus(resp_fds[0]);
    // Must be large enough to drain the ENTIRE STATUS line in one read — the
    // line carries per-device diagnostics (issue #236 evdev_node, wedge
    // timings) and can resolve a real /sys/class/input node when the test's
    // dummy vid:pid (1:2) happens to match a host input device. A short read
    // leaves the tail buffered on the stream socket and corrupts the Step-4
    // read below. Sized to match handleStatus's own 4096-byte emit buffer.
    var status_buf: [4096]u8 = undefined;
    const status_n = try posix.read(resp_fds[1], &status_buf);
    const status_resp = status_buf[0..status_n];
    try testing.expect(std.mem.indexOf(u8, status_resp, "device=T") != null);

    // Step 2: parse device name from STATUS (mirrors resolveDefaultMapping logic).
    const prefix = "STATUS device=";
    var device_name: []const u8 = "";
    var line_it = std.mem.splitScalar(u8, status_resp, '\n');
    while (line_it.next()) |line| {
        if (std.mem.startsWith(u8, line, prefix)) {
            const rest = line[prefix.len..];
            if (std.mem.indexOf(u8, rest, " state=")) |end| {
                device_name = rest[0..end];
            }
        }
    }
    try testing.expectEqualStrings("T", device_name);

    // Step 3: load config.toml with default_mapping for this device.
    const config_str =
        \\[[device]]
        \\name = "T"
        \\default_mapping = "foo"
    ;
    var toml_parser = @import("toml").Parser(user_config_mod.UserConfig).init(allocator);
    defer toml_parser.deinit();
    var cfg_result = try toml_parser.parseString(config_str);
    defer cfg_result.deinit();
    const mapping_name = user_config_mod.findDefaultMapping(&cfg_result, device_name);
    try testing.expect(mapping_name != null);
    try testing.expectEqualStrings("foo", mapping_name.?);

    // Step 4: handleSwitch with the resolved name returns OK.
    sup.test_switch_mapping_override = try allocator.dupe(u8, mapping_path);
    sup.handleSwitch(resp_fds[0], mapping_name.?, null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("OK foo\n", resp_buf[0..n]);
}

// --- parseChordSwitchConfig tests ---

test "supervisor: parseChordSwitchConfig: full schema produces detector cfg" {
    const cs: user_config_mod.ChordSwitchConfig = .{
        .modifier = &.{ "LM", "RM" },
        .selectors = &.{ "A", "B", "X", "Y" },
        .hold_ms = 120,
    };
    const out = parseChordSwitchConfig(cs).?;
    const lm_bit = @as(u64, 1) << @intFromEnum(@import("core/state.zig").ButtonId.LM);
    const rm_bit = @as(u64, 1) << @intFromEnum(@import("core/state.zig").ButtonId.RM);
    try testing.expectEqual(lm_bit | rm_bit, out.modifier_mask);
    try testing.expectEqual(@as(u8, 4), out.selector_count);
    try testing.expectEqual(@as(u64, 120) * std.time.ns_per_ms, out.hold_ns);
}

test "supervisor: parseChordSwitchConfig: null cfg disables feature" {
    try testing.expectEqual(@as(?chord_detector_mod.Config, null), parseChordSwitchConfig(null));
}

test "supervisor: parseChordSwitchConfig: missing modifier disables feature" {
    const cs: user_config_mod.ChordSwitchConfig = .{
        .selectors = &.{ "A", "B" },
    };
    try testing.expectEqual(@as(?chord_detector_mod.Config, null), parseChordSwitchConfig(cs));
}

test "supervisor: parseChordSwitchConfig: unknown button name disables feature" {
    const cs: user_config_mod.ChordSwitchConfig = .{
        .modifier = &.{ "LM", "ZZZ" },
        .selectors = &.{"A"},
    };
    try testing.expectEqual(@as(?chord_detector_mod.Config, null), parseChordSwitchConfig(cs));
}

test "supervisor: parseChordSwitchConfig: negative hold_ms clamped to zero" {
    const cs: user_config_mod.ChordSwitchConfig = .{
        .modifier = &.{"LM"},
        .selectors = &.{"A"},
        .hold_ms = -50,
    };
    const out = parseChordSwitchConfig(cs).?;
    try testing.expectEqual(@as(u64, 0), out.hold_ns);
}

test "supervisor: lookupChordMappingName: deterministic order when two profiles share chord_index" {
    // Write two mapping files with chord_index = 1; names chosen so that
    // lexicographic order ("alpha" < "zebra") differs from filesystem
    // enumeration order (we write "zebra" first).
    const allocator = testing.allocator;

    const base = "/tmp/padctl_test_chord_lookup_order";
    const map_dir = base ++ "/mappings";
    std.fs.deleteTreeAbsolute(base) catch {};
    try std.fs.makeDirAbsolute(base);
    try std.fs.makeDirAbsolute(map_dir);
    defer std.fs.deleteTreeAbsolute(base) catch {};

    // Write "zebra" first (would win under raw enumerate order).
    const zebra_path = map_dir ++ "/zebra.toml";
    const alpha_path = map_dir ++ "/alpha.toml";
    {
        const f = try std.fs.createFileAbsolute(zebra_path, .{});
        defer f.close();
        try f.writeAll("chord_index = 1\n");
    }
    {
        const f = try std.fs.createFileAbsolute(alpha_path, .{});
        defer f.close();
        try f.writeAll("chord_index = 1\n");
    }

    // Build a profiles slice that mirrors what discoverMappings would return
    // (without depending on real XDG dirs) and apply the same sort logic.
    var profiles = [_]mapping_discovery.MappingProfile{
        .{ .name = "zebra", .path = zebra_path, .source = .user },
        .{ .name = "alpha", .path = alpha_path, .source = .user },
    };
    std.sort.pdq(mapping_discovery.MappingProfile, &profiles, {}, struct {
        fn lessThan(_: void, a: mapping_discovery.MappingProfile, b: mapping_discovery.MappingProfile) bool {
            return std.mem.lessThan(u8, a.name, b.name);
        }
    }.lessThan);

    // After sort: "alpha" must come before "zebra".
    try testing.expectEqualStrings("alpha", profiles[0].name);
    try testing.expectEqualStrings("zebra", profiles[1].name);

    // Verify both files actually parse chord_index = 1 correctly.
    const pa = try mapping_cfg.parseFile(allocator, alpha_path);
    defer pa.deinit();
    try testing.expectEqual(@as(?u8, 1), pa.value.chord_index);

    const pz = try mapping_cfg.parseFile(allocator, zebra_path);
    defer pz.deinit();
    try testing.expectEqual(@as(?u8, 1), pz.value.chord_index);
}

// Test 1: trace_lifecycle field correctly reflects PADCTL_TRACE_LIFECYCLE env at init.
test "supervisor: trace_lifecycle flag initialised from field (gate check)" {
    const allocator = testing.allocator;
    var sup = try Supervisor.initForTest(allocator);
    defer sup.deinit();

    // Default: no trace.
    try testing.expect(!sup.trace_lifecycle);

    // Enable via field (simulates what init() does after reading the env var).
    sup.trace_lifecycle = true;
    try testing.expect(sup.trace_lifecycle);

    // traceLifecycle is a no-op when false (does not crash).
    sup.trace_lifecycle = false;
    sup.traceLifecycle("test {s}", .{"ok"});
}

// Test 2: handleStatus output contains new diagnostic fields.
test "supervisor: handleStatus includes diagnostic fields (issue #236)" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock_a = try MockDeviceIO.init(allocator, &.{});
    defer mock_a.deinit();
    var sup = try Supervisor.initForTest(allocator);

    const inst_a = try makeTestInstance(allocator, &mock_a, &parsed_dev.value);
    try sup.attachWithInstance("hidraw3", "usb-1-1", inst_a, null);

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    sup.handleStatus(resp_fds[0]);
    var resp_buf: [512]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    const resp = resp_buf[0..n];

    try testing.expect(std.mem.indexOf(u8, resp, "phys_key=") != null);
    try testing.expect(std.mem.indexOf(u8, resp, "vid=") != null);
    try testing.expect(std.mem.indexOf(u8, resp, "output_kind=") != null);
    try testing.expect(std.mem.indexOf(u8, resp, "output_fd_alive=") != null);
    try testing.expect(std.mem.indexOf(u8, resp, "hotplug_pending=") != null);

    // Arm a grace deadline and verify grace_deadline_remaining_ms appears.
    sup.managed.items[0].grace_deadline_ns = sup.nowNs() + 30 * std.time.ns_per_s;
    sup.handleStatus(resp_fds[0]);
    const n2 = try posix.read(resp_fds[1], &resp_buf);
    try testing.expect(std.mem.indexOf(u8, resp_buf[0..n2], "grace_deadline_remaining_ms=") != null);

    defer {
        sup.managed.items[0].grace_deadline_ns = null;
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }
}

// Many managed devices with long names overflow the old fixed 4096 STATUS
// buffer; the tail fields (shadow_grabs/shadow_nodes, appended last) were
// dropped first and the trailing newline lost. The reply must stay complete.
test "supervisor: handleStatus does not truncate tail fields under many devices" {
    const allocator = testing.allocator;

    const long_name_toml =
        \\[device]
        \\name = "MegaController With A Deliberately Long Product Name 0123456789"
        \\vid = 1
        \\pid = 2
        \\[[device.interface]]
        \\id = 0
        \\class = "hid"
        \\[[report]]
        \\name = "r"
        \\interface = 0
        \\size = 3
        \\[report.match]
        \\offset = 0
        \\expect = [0x01]
        \\[report.fields]
        \\left_x = { offset = 1, type = "i16le" }
    ;

    const parsed_dev = try device_mod.parseString(allocator, long_name_toml);
    defer parsed_dev.deinit();

    const N = 24;
    var mocks: [N]*MockDeviceIO = undefined;
    var mock_count: usize = 0;

    var sup = try Supervisor.initForTest(allocator);

    var i: usize = 0;
    while (i < N) : (i += 1) {
        const mock = try allocator.create(MockDeviceIO);
        mock.* = try MockDeviceIO.init(allocator, &.{});
        mocks[mock_count] = mock;
        mock_count += 1;

        const inst = try makeTestInstance(allocator, mock, &parsed_dev.value);
        var devname_buf: [32]u8 = undefined;
        var phys_buf: [48]u8 = undefined;
        const devname = try std.fmt.bufPrint(&devname_buf, "hidraw{d}", .{i});
        const phys = try std.fmt.bufPrint(&phys_buf, "usb-0000:00:14.0-{d}.{d}/input0", .{ i, i });
        try sup.attachWithInstance(devname, phys, inst, null);

        var node_buf: [24]u8 = undefined;
        const node = try std.fmt.bufPrint(&node_buf, "event{d}-shadow", .{i});
        sup.managed.items[i].shadow_grabs.pushUnownedForTest(node);
    }

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);
    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    sup.handleStatus(resp_fds[0]);

    var resp: std.ArrayList(u8) = .{};
    defer resp.deinit(allocator);
    var chunk: [65536]u8 = undefined;
    while (true) {
        const n = try posix.read(resp_fds[1], &chunk);
        if (n == 0) break;
        try resp.appendSlice(allocator, chunk[0..n]);
        if (std.mem.indexOfScalar(u8, resp.items, '\n') != null) break;
        if (n < chunk.len) break;
    }
    const line = resp.items;

    try testing.expect(line.len > 4096);
    try testing.expect(std.mem.endsWith(u8, line, "\n"));

    var devices: usize = 0;
    var search: usize = 0;
    while (std.mem.indexOfPos(u8, line, search, "device=")) |pos| {
        devices += 1;
        search = pos + "device=".len;
    }
    try testing.expectEqual(@as(usize, N), devices);

    try testing.expect(std.mem.indexOf(u8, line, "event23-shadow") != null);

    const parsed = try socket_client.parseStatusLine(line, allocator);
    defer socket_client.freeStatusDevices(allocator, parsed);
    try testing.expectEqual(@as(usize, N), parsed.len);
    try testing.expectEqual(@as(usize, 1), parsed[N - 1].shadow_grabs);
    try testing.expect(std.mem.indexOf(u8, parsed[N - 1].shadow_nodes, "event23-shadow") != null);

    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
        var k: usize = 0;
        while (k < mock_count) : (k += 1) {
            mocks[k].deinit();
            allocator.destroy(mocks[k]);
        }
    }
}

test "supervisor: resolveEvdevNodesAt returns all matching nodes with device name" {
    // Build a synthetic sysfs tree under /tmp with two event nodes sharing the same VID:PID.
    const base = "/tmp/padctl_test_evdev_multi";
    std.fs.deleteTreeAbsolute(base) catch {};
    defer std.fs.deleteTreeAbsolute(base) catch {};

    const Event = struct { name: []const u8, dev_name: []const u8 };
    const events = [_]Event{
        .{ .name = "event5", .dev_name = "Vader 5 Pro" },
        .{ .name = "event7", .dev_name = "Vader 5 Pro IMU" },
    };
    for (events) |ev| {
        var p0: [128]u8 = undefined;
        var p1: [128]u8 = undefined;
        var p2: [128]u8 = undefined;
        var p3: [128]u8 = undefined;
        var p4: [128]u8 = undefined;
        var p5: [128]u8 = undefined;
        std.fs.makeDirAbsolute(base) catch {};
        std.fs.makeDirAbsolute(try std.fmt.bufPrint(&p0, "{s}/{s}", .{ base, ev.name })) catch {};
        std.fs.makeDirAbsolute(try std.fmt.bufPrint(&p1, "{s}/{s}/device", .{ base, ev.name })) catch {};
        try std.fs.makeDirAbsolute(try std.fmt.bufPrint(&p2, "{s}/{s}/device/id", .{ base, ev.name }));
        var vf = try std.fs.createFileAbsolute(try std.fmt.bufPrint(&p3, "{s}/{s}/device/id/vendor", .{ base, ev.name }), .{});
        try vf.writeAll("045e\n");
        vf.close();
        var pf = try std.fs.createFileAbsolute(try std.fmt.bufPrint(&p4, "{s}/{s}/device/id/product", .{ base, ev.name }), .{});
        try pf.writeAll("02fd\n");
        pf.close();
        var nf = try std.fs.createFileAbsolute(try std.fmt.bufPrint(&p5, "{s}/{s}/device/name", .{ base, ev.name }), .{});
        try nf.writeAll(ev.dev_name);
        nf.close();
    }

    var out: [512]u8 = undefined;
    const result = Supervisor.resolveEvdevNodesAt(base, 0x045e, 0x02fd, &out);
    try testing.expect(result != null);
    const s = result.?;
    try testing.expect(std.mem.indexOf(u8, s, "/dev/input/event5") != null);
    try testing.expect(std.mem.indexOf(u8, s, "/dev/input/event7") != null);
    try testing.expect(std.mem.indexOf(u8, s, "Vader 5 Pro IMU") != null);
}

test "supervisor: absolutePathInMappingDirs accepts only files in trusted dirs" {
    const dirs = [_][]const u8{
        "/home/u/.config/padctl/mappings",
        "/etc/padctl/mappings/",
        "/usr/share/padctl/mappings",
    };
    try testing.expect(absolutePathInMappingDirs("/home/u/.config/padctl/mappings/fps.toml", &dirs));
    try testing.expect(absolutePathInMappingDirs("/etc/padctl/mappings/racing.toml", &dirs));
    try testing.expect(absolutePathInMappingDirs("/usr/share/padctl/mappings/x.toml", &dirs));
    try testing.expect(!absolutePathInMappingDirs("/etc/passwd", &dirs));
    try testing.expect(!absolutePathInMappingDirs("/home/u/.config/padctl/mappings/sub/fps.toml", &dirs));
    try testing.expect(!absolutePathInMappingDirs("/home/u/.ssh/id_rsa", &dirs));
    try testing.expect(!absolutePathInMappingDirs("/usr/share/padctl/devices/x.toml", &dirs));
}

test "supervisor: SWITCH rejects absolute path outside mapping dirs" {
    const allocator = testing.allocator;

    const parsed_dev = try device_mod.parseString(allocator, minimal_device_toml);
    defer parsed_dev.deinit();

    var mock = try MockDeviceIO.init(allocator, &.{});
    defer mock.deinit();

    var sup = try Supervisor.initForTest(allocator);
    defer {
        sup.stopAll();
        sup.ctrl_sock = null;
        sup.deinit();
    }

    sup.ctrl_sock = .{
        .listen_fd = -1,
        .client_fds = .{ -1, -1, -1, -1 },
        .client_count = 0,
        .path = "",
        .allocator = allocator,
    };

    const resp_fds = try testSocketpair();
    defer posix.close(resp_fds[0]);
    defer posix.close(resp_fds[1]);

    const inst = try makeTestInstance(allocator, &mock, &parsed_dev.value);
    try sup.spawnInstance("usb-1-1", inst, null);

    sup.handleSwitch(resp_fds[0], "/etc/passwd", null);

    var resp_buf: [64]u8 = undefined;
    const n = try posix.read(resp_fds[1], &resp_buf);
    try testing.expectEqualStrings("ERR mapping-not-allowed\n", resp_buf[0..n]);
}
